# korrPar
CLOSE ALL
if sound = 0:PLAY 'sound/korr.mp3',30
music_on = 0

$metkaM = $ARGS[0]
$locM = $CURLOC
$metka = $ARGS[0]
$loc = $CURLOC
clr
popolaini = 0
saunaYouRoom = 0
boycherdaksex = 0
frost = 0
minut += 1
bonusZ = 0
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)
if hour ! 18 and blockBedrPar = 1:blockBedrPar = 0

if blockBedrPar = 0:
	if sisboyday + 1 = daystart and hour = 18 and sisboytrioQW = 1:
		act 'Go to your room':gt 'sisterQW', 'sisboytrioQW_bedroom', 'start'
	elseif sisboyday + 1 = daystart and hour = 18 and (sisboyQW = 3 or sisboyQW = 5 or sisboyQW = 7 or sisboyQW = 9):
		act 'Go to your room':gt 'sisterQW', 'sisboyQW_bedroom'
	else
		act 'Go to your room':gt 'bedrPar'
	end
end

act 'Go to your parents'' room':gt 'bedrPar2'
act 'Go to the living room':gt 'sitrPar'
act 'Go to the bathroom':gt 'vanrPar'
act 'Go to the kitchen':gt 'kuhrPar'

act '<font color = maroon>Leave the apartment and go to the stairwell</font>':
	if $clothingworntype ! 'nude':
		gt'pod_ezd','etaj_2'
	else
		msg'<b><font color = red>You need to get dressed.</font></b>'
		gt $curloc
	end
end


act '<font color = maroon><b>Leave the apartment and go outside</b></font>':
	if $clothingworntype ! 'nude':
		if hour < 4 and motherWorry = 0 and age < 18:
			'<font color=red>Are you sure? Your mother will worry if you go out at this time.</font>'
			cla
			act '<font color=red>Yes, go outside</font>':gt'gorodok'
			act 'No, stay inside':gt $curloc
		else
			gt'gorodok'
		end
	else
		msg'<b><font color = red>You need to get dressed.</font></b>'
		gt $curloc
	end
end

'<center><b><font color="maroon">corridor</font></b></center>'
'<center><img src="images/qwest/alter/korrPar.jpg"></center>'
'A <a href="exec:gt ''mirror'', ''start''">mirror</a> hangs on the wall, near the front door.'

if palto > 0:'Your <<$palto>> is on a hook, next to the mirror.'

gs 'family'

if indorf = 1:'With your parents being away to visit your grandparents, the apartment seems quiet and empty.'

!to add a counter if you avoid Dimka
if nodimkaK >= rand(7,10) and DimkaWarnedToday = 0:
   if hour >= 16 and hour < 24 and week <=5 and DimkaWarnedToday = 0: gt 'DimkaWarning'
   if hour >= 9 and hour < 24 and week >=6 and DimkaWarnedToday = 0: gt 'DimkaWarning'
end

if hour >= 16 and hour <= 20 and indorf = 0:
	if artemvbrosvid = 1 and artemhakday ! daystart and motherQW < 2 and mothervideotalk = 0 or BrotherNo = 1 and BrotherNoDay ! day:
		cls
		BJvideoPublic = 1
		BrotherNo = 0
		BrotherNoDay = day
		manna = 0
		gs 'stat'
		'<center><b><font color="maroon">Mom</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'Your mother stands in the hallway waiting for you, clearly angry. As soon as she sees you, she grabs your ear and pinches it hard, making you wince in pain. "I just saw this video of you sucking some guy''s dick! What the hell were you thinking!? Your stepfather and I worked so hard to raise you properly, and then you do this. Do you realize that maybe this video was sent to others too? What will the people in town think of us now? I oughta teach you a lesson. What do you have to say for yourself!? Speak up!"'

		act 'Beg her to do nothing':
			cls
			mothervideotalk = 2
			minut += 5
			sub += 50
			gs 'stat'
			'<center><b><font color="maroon">mom</font></b></center>'
			'<center><img src="images/qwest/alter/mother.jpg"></center>'
			'You burst into tears: "Mommy, please forgive me! It will will never happen again! I made a stupid mistake, I''m sorry... I didn''t mean to... please... I don''t... sorry..." You fall to your knees sobbing uncontrollably, hoping she will have mercy on you. Your mother looks at down at you, still angry but not quite sure what to do. She lets out an exasperated gasp, shakes her head and walks away.'

			act 'Get back on your feet':gt $curloc
		end

		act 'Be silent':
			cls
			mothervideotalk = 3
			minut += 5
			dom += 10
			spank += 1
			gs 'stat'
			'<center><img src="images/qwest/alter/artom/spank.jpg"></center>'
			'You stand in the hallway staring at the floor, too embarrased to say anything. What is there to say, anyway?'
			'Your mother grabs you by the arm and pulls you into the living room: "Take off your pants and bend over!" You shake your head, not liking where this is going at all. Getting even angrier by your refusal, she shouts: "Vladimir, hold her down!" Your stepfather pulls you down and keeps you in place, while your mother pulls away your clothing. She then grabs a belt and starts to flog your bare ass with it, not holding back at all. You take your punishment as good as you can, but towards the end you''re in tears and your cries are heard throughout the apartment. When it''s finally over, you raise your head just in time to catch the startled faces of your brother and sister in the doorway.'

			act 'Stand up and straighten your clothes':gt $curloc
		end

		if momslut >= 2:
			act 'Threaten her':
				cls
				mothervideotalk = 1
				minut += 5
				gs 'stat'
				'<center><b><font color="maroon">Mom</font></b></center>'
				'<center><img src="images/qwest/alter/mother.jpg"></center>'
				'You slap away her hand with a defiant look in your eyes. "Oh, really? You''re no angel yourself either, Mom... does Dad know what YOU have been up to lately? Why don''t we go ask him, while we''re on this subject?"'
				'Your mother looks at you furiously, but is at a loss for words. After a few seconds of awkward silence she storms off, slamming the door shut behind her.'

				act 'Continue':gt $curloc
			end
		end

		exit
	end

	if artemvbrosvid=1 and artemhakday ! daystart and motherQW >= 2 and mothervideotalk = 0:
		cls
		BJvideoPublic = 1
		manna = 0
		gs 'stat'
		'<center><b><font color="maroon">Mom</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'When you enter the hallway, you find your mother waiting there for you. "What''s all this I hear about a video of you sucking some guy''s dick? How could you be so careless, <<$name>>? You do realize this makes our whole family look bad..."'

		act 'Apologize':
			cls
			mothervideotalk = 1
			minut += 5
			mother -= 40
			gs 'stat'
			'<center><b><font color="maroon">Mom</font></b></center>'
			'<center><img src="images/qwest/alter/mother.jpg"></center>'
			'"I don''t know what to say, mom... I''m really sorry."'
			'Your mother is clearly very upset with you. After a short discussion, she walks away.'

			act 'Continue':gt $curloc
		end

		exit
	end

	if schoolprogul > 2 and SchoolAtestat = 0:
		cls
		gs 'stat'
		'<center><b><font color="maroon">Mom</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'

		if belgangQW=10000:
			belgangQW=10001
			schoolprogul = 0
			GspravkaT = 2
			motherKnowSpravka = 1
			'"Oh, my darling <<$name>>, where have you been? We have been so worried."'
			'You go into the kitchen with your mom and spend some time explaining what happened to you. Many of the details are difficult to talk about, and you cry and hug as you tell your story.'
			'Your mom tells you she will sort out everything with the school and makes you a bowl of chicken soup.'
			if energy >= 30:
				'You are too full to eat it.'
			elseif energy >= 20 and energy < 30:
				if water < 20:
					water += 20
					cumspclnt = 2
					gs 'cum_cleanup'
					fbreath = 0
				end
				fat += 3
				energy += 10
				'You have some of the chicken soup, but can''t finish it.'
			elseif energy < 20:
				if water < 20:
					water += 20
					cumspclnt = 2
					gs 'cum_cleanup'
					fbreath = 0
				end
				energy += 20
				'You gladly eat it all.'
			end

			act 'Leave':gt 'kuhrPar'

			exit
		elseif schoolPredupr = 2:
			schoolPredupr = 3
			SchoolBlock = 1
			ParHomeBlock = 1
			schoolprogul = 0
			if $clothingworntype = 'school' and clothingwornnumber = 6:gs 'clothing', 'strip'
			'As you enter the hallway, you find your mother there waiting for you. You have never seen her this furious. "<<$name>>? I just got off the phone with the school, they told me you didn''t show up yet again and expelled you. I''ve had it with you! I''ve warned you so many times! Go ahead, live your irresponsible life the way you want. You''re no longer welcome here." She already packed up all your things from your room before you got home, and roughly shoves the bag into your hands. After that, she pushes you out the door and slams it shut in your face.'

			act 'Leave':gt 'gorodok'

			exit
		elseif schoolPredupr = 1:
			schoolPredupr = 2
			schoolprogul = 0
			Suspeh -= 40
			'As you enter the hallway, you find your mother there waiting for you. She seems very angry with you. "School called, they said you didn''t show up for class again. They wanted to expel you! I had to go over there and beg the principal to give you one last chance. I''ve never felt so humiliated! This is the last straw for me, <<$name>>, you will not embarrass me any further. If you miss one more day of school, they will kick you out. And if you get kicked out of school, I will personally kick you out of the house. If you cannot be responsible and behave like a normal person, then get out, no one is forcing you to stay." Having said her piece, your mother turns around and leaves.'
		elseif schoolPredupr = 0:
			schoolPredupr = 1
			mother = 0
			schoolprogul = 0
			Suspeh -= 20
			'As you enter the hallway, you find your mother there waiting for you. "<<$nickname>>? The school called, they said you weren''t at school today. You know they''re very unforgiving when it comes to absenteeism, they said they would expel you if it happens again. What are you doing with your life? Get it together!" Your mother angrily leaves the room, leaving you to your thoughts.'
		end

		act 'Continue':gt $curloc

		exit
	end
end

if hour >= 15 and hour < 20:
	if Gcall = 0:
		numnpcboy = 0
		:markboyfrendcall
		numnpcboy += 1

		if otnBoyFrend[numnpcboy] >= 20 and boyinday[numnpcboy]!daystart:
			Gcall = 1
			jump 'markboyfrendcall2'
		end

		if numboyFrend > numnpcboy:jump 'markboyfrendcall'
	end
else
	if Gcall > 0:Gcall = 0
end

:markboyfrendcall2

if Gcall > 0:'You hear the <a href="exec:gt ''gtelefon''">phone</a> ringing in the hallway.'
if motherWorry = 1 and indorf = 0:
	gt'mother'
elseif motherWorry = 1 and indorf > 0:
	motherWorry = 0
end

if notathomesleep > 7 and hour >= 16 and  hour < 21 and mother_go = 0:
	notathomesleep = 0
	cls
	if mother < 40:
		'<center><b><font color = maroon>Mom</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'You run into your mother in the hallway. "Well well, look who decided to come home for once.'
		act '<B>Continue</B>':gt 'korrPar'
	end
	if mother >= 40:
		'<center><b><font color = maroon>Mom</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'You run into your mother in the hallway. She looks happy to see you. "What a surprise! Welcome home honey, I missed you.'
		act '<B>Continue</B>':gt 'korrPar'
	end
end

!------------------------------------------!
!gg whore if she had given herself to boys in the stairwell, then customers hammering at the door: !
gs'pod_whore'
pod_client = RAND(0,100)
if pod_whore_countQW <= 15:
	if pod_client <= 5 and hour >= 10 and hour <= 23 and gorslut >=6 and podwhoreQW >=3: 'Someone is at the door: <a href="exec: dynamic $pod_client_talk">Answer</a>'	
elseif pod_whore_countQW > 15:
	if pod_client <= 20 and hour >= 10 and hour <= 23 and gorslut >=6 and podwhoreQW >=3: 'Someone is at the door: <a href="exec: dynamic $pod_client_talk">Answer</a>'
end	
!'<<$pod_client>>'
!==========================================!

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

--- korrPar ---------------------------------

