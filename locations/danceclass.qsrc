# danceclass

menu_off = 1
*clr & cla
minut += 5
gs 'stat'
gs 'themes', 'indoors'
'<center><b><font color="maroon">Dance Studio</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/city/citycenter/gym/dance/danceclass.jpg"></center>'
'A big, open room with good lighting and huge mirrors, perfect for improving your dance skills.'

act 'Modern dance class (0:30)':
	*clr & cla
	timemult = 2
	loopcount = 1
	:timeloop
	gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'danc_exp'
	if loopcount < timemult: loopcount += 1 & jump 'timeloop'
	loopcount = 0
	timemult = 0
	abonement -= 1
	if pcs_inhib < 40:inhib_exp += rand(1,2)
	'<center><img <<$set_imgh>> src="images/locations/shared/gym/fit1.jpg"></center>'

	if pcs_danc < 100:'You study modern dance, which is very popular in the clubs.'
	if pcs_danc >= 100:'The day has finally come, you''ve been working hard, and you realize that you''ve learned everything you can from this class.'

	act 'Leave':gt 'fit', 'dressing room'
end

act 'Burlesque class (0:30)':
	*clr & cla
	gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'danc_exp', 'dancero_exp'
	abonement -= 1
	if pcs_inhib < 60:inhib_exp += rand(1,2)
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/stripdance.jpg"></center>'

	if pcs_danc >= 50:
		gs 'exercise', 'tier2', 0, 'dancero_exp'
		'You study the art of burlesque, imaging yourself stripping to the music.'

		if pcs_dancero >= 100:'You''ve finally managed to master the art of burlesque, there is no need taking this class anymore.'
	else
		gs 'exercise', 'tier2', 0, 'danc_exp'
		'This class isn''t just about taking your clothes off, it''s a difficult dance style and your basics aren''t sufficent. Instead, the teacher helps you with your basic modern dance steps.'
	end

	act 'Leave':gt 'fit', 'dressing room'
end

if pcs_stren >=40 or needstrength = 0:
	act 'Pole dance class (0:30)':
		*clr & cla
		gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'dancpol_exp'
		abonement -= 1
		if pcs_inhib < 60:inhib_exp += rand(1,2)
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/gym/dance/poledance.jpg"></center>'

		if pcs_dancero >= 50 and pcs_stren >= 40:
			gs 'exercise', 'tier2', 0, 'dancpol_exp'
			'You study various acrobatics on the pole.'

			if pcs_dancpol >= 100:'You''ve already mastered the art of pole dancing, there is nothing more left to teach you.'
		else
			'It doesn''t look sensual enough and you bang your head pretty hard on the pole. This is far harder than it looks. It would help if you improved your modern dancing skills or the burlesque one, but you did learn a little.'

			if pcs_stren < 40:'The instructor tries to be patient with you, but it is clear that you are too weak for this sort of dance.' & needstrength = 1
		end

		act 'Leave':gt 'fit', 'dressing room'
	end
end


--- danceclass ---------------------------------

