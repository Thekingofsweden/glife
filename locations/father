# father
fatherAge = age + 30
gs 'stat'
gs 'dinfather'
gs 'fathersex'

$metkaM = $ARGS[0]
$locM = $CURLOC

'<center><b><font color="maroon">Your stepfather, Vladimir Mikhailovich Scriabin</font></b></center>'
'<center><img src="images/qwest/alter/father.jpg"></center>'
'Your stepfather is a greying, slightly flabby man working as a truck driver. He is <<fatherAge>> years old. Your mother divorced your biological father and married Vladimir when you were only one year old. You''ve never seen your biological father since their divorce, and as such you have no real memory of him.'

dynamic $fatherRep

act 'Move away':gt $loc, $metka

!--------------------------------father comments on appearance changes------------------------------------------------
! hair color
if hcolfronce = 0:
	hcolfatherremember = hcol
	hcolfronce = 1
end
if hcolfatherremember ! hcol:
	if hcol = 0: $texthcol = 'So you decided to become a brunette?'
	if hcol = 1: $texthcol = 'Or, rather, un-dyed your hair. So you decided to go back to your natural color?'
	if hcol = 2: $texthcol = 'So you decided to become a brazen redhead?'
	if hcol = 3: $texthcol = 'So you decided to become a blonde?'
	'Your stepfather looks at you for a second, noticing that something is different about you. His eyes widen as he notices your hair. "Oh <<$nickname>>, I see you dyed your hair! <<$texthcol>>" It looks good on you.'
	hcolfatherremember = hcol
end

! tongue piercing
if pirsA >= 1 and pirsAfather=0 and pirsadaytalk!daystart:
	pirseerand=rand(1,100)
	pirsadaytalk=daystart
	if pirseerand>=75:
		'Your stepfather looks at your mouth funnily as you greet him. "<<$nickname>>, do you have something shiny in your mouth?"'
		act'"No"':
			cla
			'You shake your head: "Umm, no! No I don''t!"'
			'You quickly turn away before he can get a good look at your tongue piercing.'
			act 'Move away':gt $loc, $metka
		end
		
		act'"Yes, want to see?"':
			cls
			pirsAfather=1
			gs'stat'
			'<center><img src="images/body/pirsA.jpg" ></center>'
			'You open your mouth and stick out your tongue, showing him your piercing.'
			if TouchedByFather=0:'Your stepfather shakes his head: "<<$nickname>>, tongue piercings are only for lewd women who use their mouth to please men."'
			if TouchedByFather>0:'Your stepfather grins: "Nice, I bet you could make someone feel really good with that..."'
			act 'Move away':gt $locM, $metkaM
		end
		exit
	end
end

! lip piercing
if pirsB = 1 and pirsBfather=0 and pirsbdaytalk!daystart:
	pirsadbytalk=daystart
	'Your stepfather takes a long look at your mouth, specifically at your new piercing. He seems confused and asks: "Why did you get that?"'
	act '"Don''t you think it looks cute?"':
		cls
		pirsBfather=1
		gs'stat'
		'<center><img src="images/body/pirsB.jpg" ></center>'
		'You shrug. "I don''t know... I just like the way lip rings look, so I got one. Don''t you think it looks cute?"'
		if TouchedByFather=0:
			'Your stepfather shakes his head: "Kids these days, always adding new holes to themselves..."'
		end
		if TouchedByFather>0:
			'Your stepfather grins: "I must admit, it does look sexy on you."'
		end
		act 'Move away':gt $locM, $metkaM
	end
	exit
end

! earrings
if pirsC = 1 and pirsCfather=0:
	pirsCfather=1
	'Your stepfather looks at your ears, noting your earrings: "I see you got your ears pierced? Your earrings are very cute."'
end

! nose ring
if pirsD = 1 and pirsDfather=0:
	pirsDfather=1
	'Your stepfather looks a bit shocked at your nose ring: "Jesus, <<$nickname>>, what is- why?! I understand putting a ring in a bull''s nose to lead them by, but on a person? There''s no reason to do that!"'
end

! eyebrow piercing
if pirsE = 1 and pirsEfather=0:
	pirsEfather=1
	'Your stepfather looks surprised when he sees your eyebrow piercing: "<<$nickname>>, why would you want that thing in your eyebrow?" He shakes his head in exasperation. "Kids these days..."'
end

! lip tattoo
if tatlip>0 and fatherTatLip=0 and father>=60:
	act 'Show him your lip tattoo':
		cls
		fatherTatLip=1
		minut+=1
		gs'stat'
		'<center><img src="images/body/tatlip<<tatlip>>.jpg" ></center>'
		if tatlip = 2: $textFatherTatLip = 'A tattoo on the inside of your lip?! That must''ve hurt like hell... it is cute, though.'
		if tatlip ! 2: $textFatherTatLip = 'That''s crazy! Why would you get something like that written on you, much less there?! No one would even think of doing that when I was your age.'
		'You pull your lip down, showing your stepfather the tattoo on the inside of your lip. Your stepfather looks surprised: "<<$textFatherTatLip>>"'
		act 'Move away':gt $locM, $metkaM
	end
end

if $clothingworntype = 'nude' and pregTalkFather = 0 and pregtime > 90:
	'Your stepfather looks at your belly with a surprised look on his face: "<<$name>>, you''re pregnant?"'
	if hadsexwithfather=1:
		'You see him hesitate for a second, as if weighing whether he should say something or not. He finally does: "It''s not mine, I hope?"'
	end
	pregTalkFather = 1
	
elseif $clothingworntype ! 'nude' and pregTalkFather = 0 and pregtime > 150:
	'Your stepfather looks at your belly with a surprised look on his face: "<<$name>>, you''re pregnant?"'
	if hadsexwithfather=1:
		'You see him hesitate for a second, as if weighing whether he should say something or not. He finally does: "It''s not mine, I hope?"'
	end
	pregTalkFather = 1
end

!!----------------------------------------end reaction block---------------------------------------------

if week < 6:
	if father_go = 0 and hour >= 21:
		'"Alright <<$nickname>>. It''s getting late, I need to get to bed. See you tomorrow."'
		cla
		act '<B>Continue</B>':gt $loc, $metka
		exit
	end
end
if week >= 6:
	if father_go = 0 and hour >= 21:
		'"Alright <<$nickname>>. It''s getting late, I need to get to bed. See you tomorrow."'
		cla
		act '<B>Continue</B>':gt $loc, $metka
		exit
	elseif father_go = 1 and hour >= 16:
		'Sorry <<$nickname>>, I have to go to the garage.'
		cla
		act '<B>Continue</B>':gt $loc, $metka
		exit
	end
end

act 'Chat':
	cls
	minut+=rand(10,15)
	father+=1
	gs 'stat'
	'<center><B><font color=maroon>Your stepfather, Vladimir Mikhailovich Scriabin</font></B></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	'Your stepfather sits on the couch in the living room, he is watching TV. You decide to join for a while and sit down next to him.'

	dynamic $fatherRep
	act 'Move away':gt $locM, $metkaM

	if father<20:
		dynamic $d_father[1]
		exit
	elseif father>=20 and father<40:
		dynamic $d_father[2]
		exit
	elseif father >=40:
		if vnesh<=15:
			dynamic $d_father[3]
			exit
		end
		if vnesh>15:
			dynamic $d_father[4]
			exit
		end
	end
end

act 'Ask for money':
	cls
	minut += 5
	gs 'stat'
	'<center><b><font color="maroon">Your stepfather, Vladimir Mikhailovich Scriabin</font></b></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	'Your stepfather is sitting on the couch, watching some boring TV show. You sit down next to him.'
	dynamic $fatherRep
	act 'Move away':gt $loc, $metka

	if fatherPayDay = daystart:
		'Trying your cutest face on your stepfather, you ask him for some money. He just shook his head wearily: "No, <<$nickname>>. I''ve already given you some money today, don''t be greedy."'
	else
		if father<=40 and TouchedByFather=0 or vnesh<=15 and TouchedByFather=0:
			'You ask your stepfather for some money, but he shakes his head in response: "I already gave my salary to your mother, go ask her."'
		else
			'You ask your stepfather for some money, but he shakes his head in response: "I already gave my salary to your mother, go ask her."'
			if father>=40 and father<60 and fatherPayDay ! daystart:FatherMolest = RAND (1,4)
			if father>=60 and father<80 and fatherPayDay ! daystart:FatherMolest = RAND (1,2)
			if father>=80 and fatherPayDay ! daystart:FatherMolest = 1
			if FatherMolest=1:
				'While you''re talking with him, you notice your stepfather is in a good mood today. Perhaps you can persuade him...'
				act '"Oh please?"':
					cla
					minut+=5
					gs 'stat'
					horny+=RAND(1,5)
					'"Oh come on, please Daddy?" you beg him, batting your eyelids and trying to look as cute as possible.'
					if vnesh < 15:fatherLust = 2
					if vnesh >= 15 and vnesh < 30:fatherLust = Rand (1,4)
					if vnesh >= 30 and vnesh < 50:fatherLust = Rand (1,2)
					if vnesh >= 50:fatherLust = 1
					if fatherLust>=2:
						'"Sorry <<$nickname>>, like I said, I have no money. Go ask your mother."'
					end

					if fatherLust=1:
						money+=rand(200,300)
						fatherPayDay = daystart
						gs 'stat'
						'"Oh, alright then. Just don''t tell your mother!" he says in a tired voice, keeping his eyes on the doorway as he gives you the money. You act genuinely happy as you take the money from him, giving him a kiss on the cheek as you go.'
					end
					act 'Move away':gt $loc, $metka
				end

				if hadsexwithfather=1:
					act 'Blackmail him':
						cla
						minut+=5
						money+=rand(300,500)
						fatherPayDay = daystart
						gs 'stat'
						'"Aren''t you forgetting something?" you say in a serious voice, referring to your recent intimate moments with him.'
						'Your stepfather is clearly embarrassed by this, and surprised at you for bringing it up. He clears his throat loudly, and the following silence makes you feel slightly uncomfortable. Nevertheless, he reaches for his wallet.'
						'"Here you go, you leech! You know you shouldn''t be taking advantage of me like that! Here, take your money and get out of here. Just keep your mouth shut!" You nod and pocket the money, feeling slightly worse for blackmailing him for it.'
						act 'Move away':gt $locM, $metkaM
					end
				end
			end

			if FatherMolest>1:
				'"<<$nickname>>, I already said no."'
			end
		end
	end
end

act 'Ask about him and your mother':
	cls
	minut += 5
	gs 'stat'
	'<center><b><font color="maroon">Your stepfather, Vladimir Mikhailovich Scriabin</font></b></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	'You ask your stepfather how he met your mother. Your stepfather scratches his slightly balding head and replies: "I used to work as a taxi driver back then, drove your mother to places from time to time and we got along very well. Natasha is a strong and independant woman, raising you and Anya all by herself after she left your father. I didn''t know him very well, I''ve only met him a couple of times. All I know is that his name is Mikhail Kuznetsov, and that he moved to a different city soon after he and your mother divorced."'

	act 'Move away':gt $loc, $metka
end

act 'Ask about his work':
	cls
	minut += 5
	gs 'stat'
	'<center><b><font color="maroon">Your stepfather, Vladimir Mikhailovich Scriabin</font></b></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	'You ask your stepfather about his work. Your stepfather reaches for his aching joints, with a grimace on his face. "I used to be a taxi driver. It was hard work though, often working many hours a day, and it could be dangerous too. With some money we saved up I bought a Gazelle pickup truck, and now I drive that selling goods and making deliveries. As you know I have two garages; one for our Volga, and the other one for the Gazelle."'

	act 'Move away':gt $loc, $metka
end

if motherKnowSpravka = 1 and hadsexwithfather=0:
	cla
	father = 0
	motherKnowSpravka = 2
	'Your stepfather angrily walked up to you: "Damnit <<$name>>, your mother told me you''re sexually active already! You''re still so young! I know I can''t stop you, but at least have some decency and don''t bring them to our house." His facial expression then softens, and he adds in a milder voice: "And please... be careful, okay?" You can tell your stepfather is very disappointed in you.'

	act 'Move away':gt $loc, $metka
	exit
end

if alko > 0 and alkoday ! daystart:
	alkoday = daystart
	father -= 1
	'Your stepfather notices the sharp smell of booze on you and looks disappointed. "<<$name>>, you smell like alcohol, what are you getting yourself into? You''re too young to drink that!"'
end

if TorgVokzalTimes>0 and YouCanGar=0:
	act 'Ask him permission to store things in his garage':
		cls
		minut+=5
		gs'stat'
		'<center><B><font color=maroon>Your stepfather, Vladimir Mikhailovich Scriabin</font></B></center>'
		'<center><img src="images/qwest/alter/father.jpg" ></center>'
		'You ask your stepfather if it is okay for you to store some things in his garage.'
		if father+dom>=80 or TouchedByFather=1:
			YouCanGar=1
			'Your stepfather reluctantly agrees to let you store some things in his garage. You can hide some of your trinkets there now.'
		else
			'Your stepfather shook his head and said "Sorry <<$nickname>>. The garage already has more stuff in it than I would like, there''s simply no room."'
		end
	act 'Move away':gt $locM, $metkaM
	
	end
end

act 'Ask him what he thinks of your outfit':
	cls
	minut+=rand(10,15)
	gs 'stat'
	'<center><B><font color=maroon>Your stepfather, Vladimir Mikhailovich Scriabin</font></B></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	'Your stepfather is sitting on the couch, watching TV. You sat next to him on the sofa.'

	dynamic $fatherRep
	act 'Move away':gt $locM, $metkaM

	''
	if father<=40 or vnesh<=15:
		'You ask your stepfather what he thinks of your outfit. It looks like he''s not in the mood to indulge you, and impatiently waves you away with his hand: "Don''t bother me <<$nickname>>, I''m busy.'
	else
		'You move slightly closer towards your stepfather, flirtatiously smile at him and ask him what he thinks of your outfit.'
		dynamic $din_father_hapri

		if father>=40 and father<60:FatherMolest = RAND (1,8)
		if father>=60 and father<80:FatherMolest = RAND (1,4)
		if father>=80:FatherMolest = RAND (1,2)

		if FatherMolest=1:
			horny+=RAND(1,5)
			gs 'stat'
			'He seems happy to oblige, and asks you to twirl your body so he can look at your outfit from all angles. The approving nod and hungry look in his eyes that follow, show that he would like to do more than just watch.'
			if horny>=50 or TouchedByFather=1:
				dynamic $din_vertetzopa
				if fatherLust=1:'"Even though he did not expect this from you, you can tell by the spark in his eyes and his heavier breathing that he is, at least, pleasantly surprised by your request.'
			end

			if horny>=90 or TouchedByFather=1:
				dynamic $din_popravit
				dynamic $din_potr
			end
		end

		if FatherMolest>1:
			'You twirl on the spot, showing off your outfit from all sides.'
			cla
			act 'Move away':gt $locM, $metkaM
		end
	end
end

act 'Watch TV together':
	cls
	minut+=60
	willpower+=RAND(5,10)
	manna+=RAND(10,50)
	gs 'stat'
	'<center><B><font color=maroon>Your stepfather, Vladimir Mikhailovich Scriabin</font></B></center>'
	'<center><img src="images/pics/telek.jpg"></center>'

	dynamic $fatherRep
	act 'Move away':gt $locM, $metkaM

	if father<20:
		dynamic $d_watchtv[1]
		exit
	elseif father>=20 and father<40:
		dynamic $d_watchtv[2]
		exit
	elseif father >=40:
		if vnesh<=15:
			dynamic $d_watchtv[3]
			exit
		end
		if vnesh>15:
			dynamic $d_watchtv[4]
			exit
		end
	end
end

if mother_go = 1 and hour = 16 and TouchedByFather = 1 and horny >= 50 or hadsexwithfather=1 and mother_go = 1 and hour = 16:
	act 'Seduce him':
		if father<20:
			'Your stepfather seems to misunderstand your advances.'
			dynamic $d_father[1]
			exit
		elseif father>=20 and father<40:
			'Your stepfather seems to misunderstand your advances.'		
			dynamic $d_father[2]
			exit
		elseif father >=40:
			if vnesh<=15:
				'Your stepfather clears his throat and changes the subject.'
				dynamic $d_father[3]
				exit
			end
			if vnesh>15:
				dynamic $d_fathersex[1]
				exit
			end
		end
	end
end

! WD: Fix old nude variable
if $clothingworntype = 'nude':
	if tatvag>0 and Putsawtatvag=0 and tanga=0:
		if father>=70:
			'With your naked body on display, your pubic tattoo catches your stepfather''s eye. "Wow, that is one hard to miss tattoo. Obviously you didn''t get that one just for yourself", he says as he moves towards you and has a closer look.'
			father += 1
			Putsawtatvag=1
			act 'Move away':gt $loc, $metka
			exit
		else
			'"Damn tattoo, I can''t believe you would soil your body like that." Your stepfather obviously doesn''t like the tattoo you have in your pubic area. '
			father -= 1
			Putsawtatvag=1
			act 'Move away':gt $loc, $metka
			exit
		end
	end
	if pirsF>0 and PutsawpirsF=0:
		if hadsexwithfather ! 1:
			if father>=60:
				'"Hey, those look nice on you!" your stepfather says, nodding at your nipple piercings. He seems to like them.'
				father += 3
				PutsawpirsF=1
				act 'Move away':gt $loc, $metka
				exit
			else
				'"Why would you do that... you should probably take those out", your stepfather says as he shakes his head, pointing at the piercings in your nipples.'
				father -= 1
				act 'Move away':gt $loc, $metka
				exit
			end
		else
			if hour >= 18 and hour < 21:	
				'"Hey, those look nice on you!" your stepfather says, nodding at your nipple piercings. He seems to like them.'
				father += 3
				PutsawpirsF=1
				act 'Move away':gt $loc, $metka
				exit
			else
				'"Wow, those are sexy!" your stepfather says, as he reaches out and touches your nipple piercings. You can''t help but let out an involuntary groan. He admires them for a moment, rubbing his fingers on your nipples and lightly tugging at them before he lets you go.'
				father += 3
				PutsawpirsF=1
				horny+=10
				act 'Move away':gt $loc, $metka
				exit
			end
		end
	end

	'Your stepfather stares at your naked body, you can''t help but notice the admiring look on his face mixed with the torment of him knowing that he should know better. "<<$nickname>>, have you no shame? You shouldn''t be flaunting your naked body like that, you have no idea what kind of effect it has on men!"'
	if hadsexwithfather ! 1:
		if father>=40:
			'He reluctantly turned his face away from you, and added: "Your body does look fantastic... but you really should go put some clothes on, your mother is around the house somewhere. Who knows what she''ll do when she sees you walking around like this!"'
			father += 1
			act 'Move away':gt 'bedrPar'
			exit
		else
			'He decisively turns his face away from you, avoiding the looks of your naked body. "Don''t be such a whore <<$name>>, go put some clothes on right now."'
			father -= 1
			act 'Move away':gt 'bedrPar'
			exit
		end
	else
		if mother_go = 0:$mother_go_text = '"You really should go put some clothes on though, your mother is home."'
			'His eyes never stop looking at your naked body, you can tell he''s very much impressed by your beauty as you walk around the room.<<$mother_go_text>>"'
		if mother_go = 0:
			act 'Move away':gt 'bedrPar'
			exit
		end
	end
end

if hadsexwithfather =1 and pirsG=1 and PutsawpirsG=0 and (hour <= 18 or hour > 21):
	act 'Show off your labia piercing':
		cls
		gs'stat'
		minut+=5
		horny+=15
		PutsawpirsG=1
		if lobok > 2:
			'<center><img src="images/picb/pirsGLob.jpg" ></center>'
			'Moving closer to your stepfather, you tease him by showing him your somewhat hairy pussy. He doesn''t know you have a piercing in your labia yet, and you plan on showing it off today. As you absently rub your fingers through your bush, the glint of metal catches his eye and he pushes your hand away before you get a chance to tell him about it. "Well well, what do we have here?" he mutters, gently rubbing his fingers over the shiny new piercing. "<<$nickname>>, you are such a horny devil... I love it, we''re going to have a lot of fun with this." He spends the next few minutes rubbing and tugging on your new piercing, making lewd comments the whole time. After that he reluctantly lets you go, leaving you hornier than before.'
			act 'Move away':gt $loc, $metka
		else
			'<center><img src="images/picb/pirsG.jpg" ></center>'
			'Moving closer to your stepfather, you tease him by showing him your smooth pussy. He doesn''t know you have a piercing in your labia yet, and you plan on showing it off today. As you absently rub your fingers over your labia, the glint of metal catches his eye and he pushes your hand away before you get a chance to tell him about it. "Well well, what do we have here?" he mutters, gently rubbing his fingers over the shiny new piercing. "<<$nickname>>, you are such a horny devil... I love it, we''re going to have a lot of fun with this." He spends the next few minutes rubbing and tugging on your new piercing, making lewd comments the whole time. After that he reluctantly lets you go, leaving you hornier than before.'
			act 'Move away':gt $loc, $metka
		end
	end
end

if hadsexwithfather=1:fatherwantssex=rand(1,2)
! WD: Fix old nude variable
! if nude = 1:
if $clothingworntype = 'nude': fatherwantssex=1
if hadsexwithfather=1 and fatherwantssex=1 and mother_go = 1 and hour = 16 and father>60:
	cls
	gs 'stat'
	'<center><B><font color=maroon>Your stepfather, Vladimir Mikhailovich Scriabin</font></B></center>'
	'<center><img src="images/qwest/alter/father.jpg"></center>'
	! WD: Fix old nude variable
	if $clothingworntype = 'nude': 'You give your stepfather a lewd smile. "I figured my clothes would only get in the way Daddy, so I took them off for you."'
	'Knowing words are not neccessary, your stepfather simply unbuttons his pants revealing his stiff cock: "<<$nickname>>, help your old man out will you?"'
	act 'Change your mind and leave':gt $loc, $metka
	act 'Get down on your knees':dynamic $d_fatherblow_act[4]
end


$fatherRep = {
	if father<20:'You and your stepfather can''t stand each other, and spend as little time in the same room as possible.'
	if father>=20 and father<40:'You and your stepfather don''t get along very well.'
	if father>=40 and father<60:'You have a normal relationship with your stepfather.'
	if father>=60 and father<80:'You have a good relationship with your stepfather.'
	if father>=80:'You have a great relationship with your stepfather.'
	''
}

--- father ---------------------------------

