# gschool_grounds

!Outside school building
if $ARGS[0] = 'main':
	CLOSE ALL
	$loc = 'gschool_grounds'
	$loc_arg = 'main'
	$location_type = 'public_outdoors'
	$menu_loc = 'gschool_grounds'
	$menu_arg = 'main'
	menu_off = 0
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">Outside School</font></b></center>'
	if month >= 11 or month <= 3:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/schoolwinter.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool.jpg"></center>'
	end
	'Pavlovsk''s only school, where all the local children go for their education. Next to the school is the athletic field and a parking lot for the teachers, with a large grove of trees just beyond them.' 
	'Hidden behind these trees is the old school building, which was supposed to be renovated and used for advance classes after the new one was built, but budget cuts meant the project was cancelled and now the old building stands empty and abandoned. It''s a popular spot for students to skip class or hang out outside of school.'

	if ((hour = 6 and minut >= 30) or (hour >= 7 and hour < 18)) and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0 and suspended['on'] ! 1:
		'With the school open you can go to the <a href="exec: minut += 1 & gt ''gschool_grounds'', ''change_room''">changing rooms</a> by the gym and change your outfit.'
	end

	if day = nyp_day and month = 12 and StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0:
		if hour >= 14 and hour < 17:
			'All of the boys and girls are standing near the school. Everyone did their best to look good today, wearing their nicest clothes. Someone calls out to you. "Hey <<$pcs_nickname>>, are you coming to the New Year''s Eve party or not? You haven''t paid the 1000 <b>₽</b> entry fee yet!"'

			if money >= 1000:act 'Go to the party (1,000 <b>₽</b>)':money -= 1000 & gt 'nyp_school', 'start'
		end
	end
	
	if InvitationToDimkaNYparty = 1 and day = 31 and day = nyp_day: act 'Follow Dimka to the party at his house': killvar 'InvitationToDimkaNYparty' & minut += 30 & gt 'sny', '3'

	if hour = 14 and week < 6 and kanikuli = 0:
		gs 'gschool_socialchg', 'group_student_rel_change'
		if NikoDate_Day ! daystart:
			if week = 1 and NikoIntro = 0:
				gt 'NikoEv', 'Intro'			
			elseif NikoVolkovQW = 5 and NikoEv = 6 and NikoDate = 0:
				gt 'NikoDates', 'After School'
			elseif NikoVolkovQW = 5 and NikoEv = 8 and NikoDate = 0:
				gt 'NikoDates', 'After School 2'
			elseif NikoVolkovQW = 10 and NikoEv = 22:
				gt 'NikoEv2', 'Recovery'
			end
		end
	end	
	
	if week < 6 and hour = 14 and kanikuli = 0 and afterschool_day ! daystart and suspended['on'] ! 1:
		afterschool_day = daystart
		if rand(0,3) = 0:
			$eventtype = 'after_school'
			gt 'gschool_events', 'break_events'
		else
			gt 'gschool_events', 'afterschool_events'
		end
	end
	
	!! Cheerleading tryouts if Sveta decided to attend
	if hour = 14 and cheerleadingQW['quest_stage'] = 1:
		if pregtime < 90: gt 'gschool_cheerleading', 'tryouts'
	!! Sveta attends training after school every Tuesday and Thursday.
	elseif hour = 14 and cheerleadingQW['day'] ! daystart and (week = 2 or week = 4) and kanikuli = 0 and cheerleadingQW['quest_stage'] > 1 and suspended['on'] ! 1:
		if npc_pregtalk['A23'] = 0: gt 'gschool_cheerleading', 'afterschool_practice'
	!! A football game is played once every 2nd Friday and the cheerleaders perform out on the field. 
	elseif hour = 14 and cheerleadingQW['day'] ! daystart and week = 5 and (daystart - (daystart mod 7)) mod 2 = 1 and kanikuli = 0 and (cheerleadingQW['quest_stage'] >= 3 or cheerleadingQW['backup_game'] = 1) and suspended['on'] ! 1:
		if npc_pregtalk['A23'] = 0: gt 'gschool_cheerleading', 'afterschool_game'
	end
	
	if hour <= 8 and kanikuli = 6:
		'Today is graduation day! The school prom is tonight as well.'

		act 'Go to your graduation':
			*clr & cla
			menu_off = 1
			hour = 10
			minut = 0
			SchoolAtestat = 1
			kanikuli = 7
			gs 'stat'

			if home_owned[2] = 0:
				'Everyone from your year is assembled in the auditorium of the school. Your teachers are standing on a platform in front of several rows of chairs where you and your classmates are sitting. Some parents and siblings are sitting in the back rows. Nobody from your family showed up.'
			else
				'Everyone from your year is assembled in the auditorium of the school. Your teachers are standing on a platform in front of several rows of chairs where you and your classmates are sitting. Some parents and siblings are sitting in the back rows. You turn your head and see your mother and your brother sitting among them.'
			end

			'Your Principial begins calling your classmates in alphabetical order. Finally it''s your turn.'

			if pcs_grades >= 90:
				'You go up to the podium. As the Principal shakes your hand she announces "As every year I''d like to honor our best student of the year. Miss <<$pcs_lastname>> here performed exceptionally well in her studies and I think that deserves some recognition." She starts applauding and the auditorium quickly joins in.'
				if home_owned[2] = 1: 'As you return to your place you see your mother smiling brightly.'
			elseif pcs_grades >= 70:
				'You go up to the podium. The Principal shakes your hand and gives you your diploma. "Well done, Miss <<$pcs_lastname>>!" she says before you return to your place.'
			else
				'You go up to the podium. The Principal shakes your hand and gives you your diploma and you return to your place.'
			end

			act 'Return to town': minut += 2 & gt 'pav_residential'

			act 'Go to the prom party':
				*clr & cla
				minut += 540
				pcs_energy += 70
				gs 'drugs', 'alcohol', 'vodka', rand(2,3)
				gs 'clothing', 'strip_all'
				gs 'stat'
				'After the ceremony, everyone gets hammered at the prom party. When you wake up, your mouth is dry and you can taste and smell the alcohol on your breath as you feel a headache coming on. Looking around, you see that you''re somewhere in the town center and that you''re completely naked, with twigs and leaves stuck in your hair. You are a little alarmed when you notice that your clothes are missing.'
				'You''re unable to find your clothes, so you cover yourself with your hands and stick to poorly lit areas as you make your way home. You try as hard as you can, but all you can remember are vague flashbacks of skinny dipping in the town fountain with a group of other students.'

				act 'Go home': gt 'korrPar'
			end
		end
		exit
	end

	if SchoolAtestat = -1 and week < 6 and hour >= 6 and hour < 14:
		act 'Get your diploma':
			*clr & cla
			menu_off = 1
			minut += 20
			SchoolAtestat = 1
			kanikuli = 7
			gs 'stat'
			if pcs_grades >= 70:
				'The Principal hands you your diploma. "Well done, Miss <<$pcs_lastname>>! Good luck in your future!" she says as she shakes your hand.'
			else
				'The Principal hands you your diploma. "Good luck in your future!" she says as she shakes your hand.'
			end

			act 'Return to town': minut += 2 & gt 'pav_residential'
		end
	end

!!	act 'Go to old school building': gt

!!	act 'Go to the Principals Office': gt

	if week < 6 and hour >= 6 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0 and SchoolBlock = 0 and suspended['on'] ! 1:
		if PCloStyle2 = 4 or Enable_sforma = 1:
			gs 'gschool_socialchg', 'group_student_rel_change_2'
			if hour = 6:
				if minut < 30:
					'It''s too early for school now.'
				else
					if NikoEv = 10: gt 'NikoWhore', 'Walk Of Shame'
					act 'Wait for class': gt 'gschool_grounds', 'bulding'
				end
			elseif hour = 7:
				if NikoEv = 10:
					gt 'NikoWhore', 'Walk Of Shame'
				elseif minut <= 45:
					act '<b>Go to class</b>': gt 'gschool_grounds', 'bulding'
				else
					'You are late for school! You could rush to class or just skip until the second period.'
					act '<b>Rush to class</b>':
						demerit += 5
						gt 'gschool_grounds', 'bulding'
					end
					act 'Skip class':
						*clr & cla
						menu_off = 1
						'<center><b><font color="maroon">School Hallway</font></b></center>'
						'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<rand(0,2)>>.jpg"></center>'
						'You don''t feel like attending all of your classes this morning and so make your way to the bathroom.'
						act 'Go to girl''s bathroom': gt 'gschool_girls_bathroom', 'start'
						act 'Go to boy''s bathroom': gt 'gschool_boys_bathroom', 'start'
					end
				end
			elseif hour = 8:
				if NikoEv = 10: gt 'NikoWhore', 'Walk Of Shame'
				'You missed your first class. You hope that no one noticed.'
				act '<b>Rush to class</b>':	demerit += 10 & gt 'gschool_grounds', 'bulding'
			elseif hour < 14:
				'It''s too late for you to attend school now. You have been marked as absent today.'
			end
		else
			'You''re not allowed to attend any classes at school if you''re not wearing your school uniform.'
		end
	end

	gs 'camera', 'check_location'

	act 'Return to town': minut += 2 & gt 'pav_residential'

	act 'Go to the sports field': minut += 1 & gt 'gschool_grounds', 'sports'

	if joint > 0 and jointhigh = 0: act 'Smoke a joint behind the school building': gs 'drugs', 'joint' & gt $loc, $loc_arg

	if week = 6 and hour = 8 and StoryLine > 0 and SchoolAtestat = 0 and detention_set = 1 and SchoolBlock = 0: act 'Go to detention': gt 'gschool_detention', 'start'

	if christinaQW['dompath'] = 4: gt 'gschool_grounds', 'KristinaFight'

	if suspended['on'] ! 1 and kanikuli = 0:
		if week < 6 and hour = 14 and dimaRevenge = 5 and dimaRevChoice = 0:
			gt 'dimaRevenge', dimaRevenge
		elseif week < 6 and hour = 14 and dimaRevenge = 4 and dimaRevChoice = 1:
			gt 'dimaRevenge', dimaRevenge
		elseif week < 6 and hour = 7 and dimaRevenge = 5 and dimaRevChoice = 1:
			gt 'dimaRevenge', 5, 'before'
		elseif week < 6 and hour = 7 and dimaRevenge = 6 and dimaRevChoice = 1:
			gt 'dimaRevenge', 6, 'before'
		elseif week < 6 and hour = 14 and dimaRevenge = 7 and dimaRevChoice = 1:
			gt 'dimaRevenge', dimaRevenge
		elseif week < 6 and hour = 7 and dimaRevenge = 4 and dimaRevChoice = 2:
			gt 'dimaRevenge', dimaRevenge, 'day4jockbefore'
		elseif week < 6 and hour = 7 and dimaRevenge = 5 and dimaRevChoice = 2:
			gt 'dimaRevenge', dimaRevenge, 'day5jockbefore'
		elseif week < 6 and hour = 14 and dimaRevenge = 6 and dimaRevChoice = 2:
			gt 'dimaRevenge', 5, 'day5jockafter'
		!elseif week < 6 and hour = 11 and dimaRevenge = 4 and	dimaRevChoice = 3:
		!	 gt 'gschool_lessons', 'short_break'
		elseif week < 6 and hour = 7 and dimaRevenge = 5 and  dimaRevChoice = 3:
			gt 'dimaRevenge', dimaRevenge
		elseif week < 6 and hour = 14 and dimaRevenge = 4 and dimaRevChoice = 4:
			gt 'dimaRevenge', dimaRevenge, 'after'
		elseif week < 6 and hour = 7 and dimaRevenge = 5 and dimaRevChoice = 4:
			gt 'dimaRevenge', dimaRevenge,	'before'
		elseif week < 6 and hour = 14 and dimaRevenge = 6 and dimaRevChoice = 4:
			gt 'dimaRevenge', 5, 'after'
		elseif week < 6 and hour = 7 and dimaRevenge = 7 and dimaRevChoice = 4 and dimaRevengedaystart >= daystart:
			gt 'dimaRevenge', 6
		elseif week < 6 and hour = 7 and dimaRevenge = 5 and dimaRevChoice = 5:
			gt 'dimaRevenge', dimaRevenge, 'before'
		elseif week < 6 and hour = 7 and dimaRevenge = 4 and dimaRevChoice = 5:
			gt 'dimaRevenge', 5
		elseif week < 6 and hour = 7 and dimaRevenge = 4 and dimaRevChoice = 6:
			gt 'dimaRevenge', dimaRevenge
		end
	end

	if hour >= 14 and pav_slut >= 250 and SchoolGossip > 0 and afterschoolday ! daystart: gt 'gschool_sex', 'suck'

	if week = 1 and kanikuli = 0 and arthakQW = 10 and apmeetday ! daystart and hour >= 14: gt 'artgar'

	if hour = 14 and week < 6 and kanikuli = 0:

		if ivan_afterschool = 1: act 'Meet Ivan after school': gt 'gschool_sex', 'afterschool'

		if Enable_tablemap = 0:
			if anushkaFirstvisit = 0 and meet_after_school = 1: '<a href="exec: gt ''anushka'', ''meet_after_school''">Anushka</a> is leaning against a tree just in front of the school.'
			if LariskaQW['story'] >= 5 and cheerleaders_on = 0: '<a href="exec:minut += 1 & gt ''LariskaGo''">Lariska</a> impatiently walks back and fourth across the courtyard, as if she''s waiting for someone.'
			if JuliaMilLernQW >= 1: '<a href="exec:minut += 1 & gt ''JuliaMilGo''">Julia</a> is sitting out on the courtyard, reading a book.'
			if NatbelQW['QWstage'] > 0: '<a href="exec:minut += 1 & gt ''NatBelGo''">Natasha</a> is standing in a corner.'
			if artemLernQW >= 1 and artemVitokQW ! 2 and artemblok = 0 and artlie ! 2 and artembrokenheart ! 1: '<a href="exec:minut += 15 & gt ''artem_events'', ''start''">Artem</a> is waiting for you near the school entrance.'
			if npc_QW['A231'] = 1 or npc_QW['A231'] = 3 or npc_QW['A231'] >= 5: '<a href="exec:minut += 1 & gt ''AleksandraGo'', ''Start''"><<$npc_firstname[''A231'']>></a> is waiting for you at the end of the school courtyard, listening to music.'

			if katjaMeynoldday ! daystart and meynoldQW >= 1: '<a href="exec:minut += 15 & gt ''katjaEv''">Katja</a> is walking across the school courtyard, getting ready to go home.'
			if DimaBeHomeOnce = 1 and DimaRudeBlock = 0:
				'<a href="exec:minut += 15 & gt ''dimaEv'', ''after_school''">Dimka</a> is deep in thought, as he leans against a lamp post.'
			elseif DimaRudeQW > 0 and dimarudeday ! daystart and DimaRudeBlock = 0:
				if rand(0,4) = 0: gt 'dimaEv', 'after_school'
				'<a href="exec:minut += 15 & gt ''dimaEv'', ''after_school''">Dimka</a> is deep in thought, as he leans against a lamp post.'
			end
			if IgorLoverQW > 0 and igorkruglovday ! daystart and IdorBlock = 0: '<a href="exec:minut += 15 & gt ''igorhome'', ''entrance''">Igor</a> is talking to some of his friends on the courtyard.'
			if fedorKozlovQW >= 10 and kotovVSkozlov = 1:
				'You can see <a href="exec: gt ''FedorEv'', ''Strela Chat''">Fedor</a> sitting in the corner of the schoolyard by himself.'
			elseif fedorKozlovQW >= 10 and fedorkozqw = 2 and FedorLuv < -4:
				'You can see <a href="exec: gt ''FedorEv2'', ''Fedor Breakup''">Fedor</a> standing by himself with a troubled look on his face.'
			elseif fedorKozlovQW >= 10:
				'You can see <a href="exec: gt ''FedorEv'', ''Start''">Fedor</a> standing by himself, his backpack around his arm.'
			end
			if NikoVolkovQW >= 5 and NikoDate = 1 and NikoDate_Day ! daystart or NikoEv = 11:
				'You can see <a href="exec:gt ''NikoEv'', ''Niko Dates''">Nikolai Volkov</a> smoking outside of the school.'
			elseif NikoVolkovQW >= 10 and NikoEv >= 17 and NikoEv <= 20 and NikoDate_Day ! daystart:
				'<a href="exec:gt ''NikoEv2'', ''Events''">Nikolai</a> is smoking outside of the school.'	
			end
		else
			!display friends in table if switch is on.
			st_count=0 & $show_table=''
			if anushkaFirstvisit = 0 and meet_after_school = 1: gs 'show_table', '<a href="exec: gt ''anushka'', ''meet_after_school''">Anushka Konstantinov</a>', '5'
			if LariskaQW['story'] >= 5 and cheerleaders_on = 0: gs 'show_table', '<a href="exec:minut += 1 & gt ''LariskaGo''">Lariska Gruzdev</a>', '5'
			if JuliaMilLernQW >= 1: gs 'show_table', '<a href="exec:minut += 1 & gt ''JuliaMilGo''">Julia Milova</a>', '5'
			if NatbelQW['QWstage'] > 0: gs 'show_table', '<a href="exec:minut += 1 & gt ''NatBelGo''">Natasha Belova</a>', '5'
			if artemLernQW >= 1 and artemVitokQW ! 2 and artemblok = 0 and artlie ! 2 and artembrokenheart ! 1: gs 'show_table', '<a href="exec:minut += 15 & gt ''artem_events'', ''start''">Artem Chebotarev</a>', '5'
			if npc_QW['A231'] = 1 or npc_QW['A231'] = 3 or npc_QW['A231'] >= 5: gs 'show_table', '<a href="exec:minut += 1 & gt ''AleksandraGo'', ''Start''"><<$npc_firstname[''A231'']>> <<$npc_lastname[''A231'']>></a>', '5'
			if katjaMeynoldday ! daystart and meynoldQW >= 1: gs 'show_table', '<a href="exec:minut += 15 & gt ''katjaEv''">Katja Meynold</a>', '5'
			if DimaBeHomeOnce = 1 and DimaRudeBlock = 0:
				gs 'show_table', '<a href="exec:minut += 15 & gt ''dimaHome'', ''home''">Dimka Nosov</a>', '5'
			elseif DimaRudeQW > 0 and dimarudeday ! daystart and DimaRudeBlock = 0:
				if rand(0,4) = 0: gt 'dimaEv', 'after_school'
				gs 'show_table', '<a href="exec:minut += 15 & gt ''dimaEv'', ''after_school''">Dimka Nosov</a>', '5'
			end
			if IgorLoverQW > 0 and igorkruglovday ! daystart and IdorBlock = 0: gs 'show_table', '<a href="exec:minut += 15 & gt ''igorhome'', ''entrance''">Igor Kruglov</a>', '5'
			if fedorKozlovQW >= 10 and kotovVSkozlov = 1:
				gs 'show_table', '<a href="exec: gt ''FedorEv'', ''Strela Chat''">Fedor</a>', '5'
			elseif fedorKozlovQW >= 10 and fedorkozqw = 2 and FedorLuv < -4:
				gs 'show_table', '<a href="exec: gt ''FedorEv2'', ''Fedor Breakup''">Fedor</a>', '5'
			elseif fedorKozlovQW >= 10:
				gs 'show_table', '<a href="exec: gt ''FedorEv'', ''Start''">Fedor</a>', '5'
			end
			if NikoVolkovQW = 5 and NikoDate = 1 and NikoDate_Day ! daystart or NikoEv = 11:
				gs 'show_table', '<a href="exec: gt ''NikoEv'', ''Niko Dates''">Nikolai</a>', '5'
			elseif NikoVolkovQW = 10 and NikoEv >= 17 and NikoEv <= 20 and NikoDate_Day ! daystart:
				gs 'show_table', '<a href="exec: gt ''NikoEv2'', ''Events''">Nikolai</a>', '5'
			end
			if st_count > 0:
				'Near school yard, you see: '
				'<TABLE BORDER=1><<$show_table>></TR></TABLE>'
			end
		end
		if AlbinaQW['albride'] = 0 and AlbinaQW['RideDaystart'] <= daystart and AlbinaQW['Chernov'] > 0: 
			'<a href="exec:minut += 15 & gt ''albinaRide''">Albina</a> is waiting behind the wheel of an SUV parked near the school.'
		end

		! meynold`s events
		if hour = 14 and vicky_afterschool ! daystart: gs 'mey_vika_events', 'near_school'
		if mey_vika['mey_vika_qw'] = 35 or mey_vika['mey_vika_qw'] = 36 and minut < 45:
			act 'Study with Tamara':
				if mey_vika['mey_vika_qw'] = 36: gt 'mey_tamara_events', 'rebuke'
				gt 'mey_tamara_events', 'learning0'
			end
		end
	end

	if strelaDay ! daystart and strelaQW = 1 and hour = 14:
		*clr & cla
		'A Mitsubishi Pajero and a BMW drive up to the school gates and strong, muscular guys in leather jackets and tracksuits get out of the cars. They have come to meet Vitek, Dan and Vasily. You watch them talk about something, although you''re not close enough to hear what it''s about. Then you notice Fedor leaving the school, his face swollen and bruised, and he waves at you. "<<$pcs_nickname>>! Come here, you''re my witness!"'

		act 'Go and see what he needs': gt 'strela'
	end
end

!Outside school sports field
if $ARGS[0] = 'sports':

	$loc = 'gschool_grounds'
	$location_type = 'public_outdoors'
	$loc_arg = 'sports'
	$menu_loc = 'gschool_grounds'
	$menu_arg = 'sports'
	menu_off = 0
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	if month > 3 and month < 11:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/field.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/fieldwinter.jpg"></center>'
	end
	'There is a football field near the school with a large track circling around it. Students often use the field and track before and after school.'

	if week = 5 and hour >= 16 and hour <= 18: 'As you pass by the sports field, you hear lots of cheering and yelling coming from the younger kids on the field. As you approach, you see '+iif(socialchg['tLazar_day'] ! daystart, '<a href="exec: gt ''gschool_socialchg1'', ''tLazar''">Lazar</a>', 'Lazar')+' on the football field showing off his skills.'

	if (week = 1 or week = 3) and hour >= 16 and hour <= 18: 'Looking toward what you can barely describe as a running track, you notice '+iif(socialchg['tSvyatoslav_day'] ! daystart, '<a href="exec: gt ''gschool_socialchg1'', ''tSvyatoslav''">Svyatoslav</a>', 'Svyatoslav')+' doing sprints by the football field.'

	act 'Leave the sports field': minut += 1 & gt 'gschool_grounds', 'main'

	if schoolrunday ! daystart and $clothingworntype = 'danilovich' and $shoeworntype = 'danilovich':
		act 'Go for a run (1 hour)':
			*clr & cla
			menu_off = 1
			minut += 60
			schoolrunday = daystart
			fat -= rand(2, 7)
			run_exp += rand(0, 2)
			gs 'sweat', 'add', 20
			gs 'stat'
			if month > 2 and month < 9:
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/runsummer.jpg"></center>'
			else
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/runwinter.jpg"></center>'
			end
			'You spend an hour running on the track.'

			act 'Continue': gt 'gschool_grounds', 'sports'
		end
	elseif schoolrunday ! daystart and ($clothingworntype ! 'danilovich' or $shoeworntype ! 'danilovich'):
		'You could go for a run here if you were wearing sports clothes and shoes.'
	end

	if hour >= 9 and hour < 18 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0: 'With the school open, you can go to the <a href="exec: minut += 1 & gt ''gschool_grounds'', ''change_room''">changing rooms</a> by the gym and change your outfit.'

	if week = 6 and SchoolSorev = 2 and hour = 10 and SchoolBlock = 0:
		*nl
		'Athletic students of your school and other schools in the area have gathered at the athletics track near your school. Everyone is getting ready for the running competition.'
		if $clothingworntype = 'danilovich' and $shoeworntype = 'danilovich':
			act 'Participate in the 400m sprint event': gt 'gschool_grounds', 'SchoolRun2'
		else
			'You need to wear exercise clothes to participate.'
		end
	end

	if week = 6 and SchoolSorev = 1 and hour = 10 and SchoolBlock = 0:
		*nl
		'Athletic students of your school and other schools in the area have gathered at the athletics track near your school. Everyone is getting ready for the running competition.'

		if $clothingworntype = 'danilovich' and $shoeworntype = 'danilovich':
			act 'Participate in the 400m sprint race': gt 'gschool_grounds', 'SchoolRun'
		else
			'You need to wear exercise clothes to participate.'
		end
	end

	gs 'family'
	
	! Giving football preference, since there''s no indoor option for it.
	if temper => 15 and sunWeather = 1 and hour > 8 and hour < 12 and week = 6 and fbpickupDay + 14 <= daystart and bbpickupDay ! daystart:
		*nl
		'As you pass by the sports field, you notice that most of the jocks have gathered around the <a href="exec: gt ''fbgameout'', ''start''">football field</a>.'
	elseif temper => 15 and sunWeather = 1 and hour > 8 and hour < 12 and week = 6 and bbpickupDay + 14 <= daystart and fbpickupDay ! daystart:
		*nl
		'As you pass by the sports field, you notice that most of the jocks have gathered around the <a href="exec: gt ''bbgameout'', ''start''">basketball court</a>.'
	end
end

!!School building
if $ARGS[0] = 'bulding':
	$loc = 'gschool_grounds'
	$loc_arg = 'bulding'
	$location_type = 'public_indoors'
	$menu_loc = 'gschool_grounds'
	$menu_arg = 'bulding'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Inside School</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<rand(0,2)>>.jpg"></center>'
	'The hallways of the school building are painted with a cheap green paint, which you think may be toxic. Budget cuts.'
	act 'Continue':
		if hour = 8: late = 1
		gs 'stat'
		BeInSchool += 1
		if rand(0,1) = 0:
			gt 'gschool_lessons', 'morning'
		else
			if rand(0,3) = 0:
				gt 'gschool_events', 'teacher_greet'
			else
				$eventtype = 'before_school'
				gt 'gschool_events', 'break_events'
			end
		end
	end
	act 'Return to town': minut += 2 & gt 'pav_residential'
	if dimaRevenge = 1:
		menu_off = 1
		'You''re in a good mood today as you enter the school. That changes when Dimka stops you in the hall on the way to class.
		"I know what you did, bitch. Did you really thing I wouldn''t figure out it was you that wiped my hard drive?"
		Before you can deny anything, he continues: "I don''t even need that video to ruin your life. Just you wait. Soon the whole school will know what a whore you are."
		His words fill you with dread as you make your way to class.'
	end

	if starlets_missed = 1:
		menu_off = 1
		starlets_missed = 0
		starlet_practice_skipped += 1
		if starlet_practice_skipped = 1:
		   gs 'npc_relationship', 'modify', 'A23', -10
		    'As you walk down the hallway, you see an annoyed Albina waiting for you, her arms crossed as she glares at you. "Where the fuck were you <<$pcs_nickname>>?! And don''t fucking lie to me!"'
		    '"Sorry, Albina. I... had a family thing I had to deal with and I didn''t have time to tell you." you meekly reply.'
		    'She doesn''t look completely convinced. "Right... Well if you care about your place on the team, then you''ll be at the next training session on time. No excuses."'
		    'You nod at her and she gives you a pat on the head before walking away. It looks like you got off lightly this time.'
		elseif starlet_practice_skipped = 2:
		   gs 'npc_relationship', 'modify', 'A23', -20
			'You notice Albina angrily watching you walk down the hallway towards her and she drags you to the side when you reach her.'
		    '"Skipping training <i>again</i>, <<$pcs_nickname>>?! I thought I told you that you were to be there on time <b>no excuses</b>?! I don''t care about what miserable story you have to defend yourself with. You either commit to the squad or you''re out. This is your final warning <<$pcs_nickname>>, don''t fucking test me!"'
		    'You don''t have a chance to speak before she storms off. Maybe it''s best for both of you if you stopped pissing her off?'
		else
		  AlbinaQW['StarletsJoined'] = -1
		  gs 'npc_relationship', 'set', 'A23', 10
		    'You are walking down the hallway and freeze when you clock a furious Albina heading straight for you. You barely have time to react before she grabs your shirt and forcefully slams you into the lockers, silencing the hall as everyone looks at you.'
		    'Albina glares directly into your eyes. "ARE YOU DOING THIS TO MOCK ME YOU FUCKING BITCH?! I GAVE YOU ONE. LAST. FUCKING. CHANCE AND YOU THROW IT BACK IN MY FUCKING FACE?!" She tightens her grips as she gets up in your face. "You''re done with the Starlets. If I <i>ever</i> see you anywhere near us, you better hope that I''m in a good fucking mood, bitch!"'
		    'She releases you and angrily storms off, shoving other students out of her way. You compose yourself and continue on your way, trying to ignore the stares.'
		end
	end

	if week <= 5 and christinaQW['dompath'] = 6:
		menu_off = 1
		npc_rel['A18'] = 0
		christinaQW['dompath'] = 7
		if LariskaHomeIn < 1:LariskaHomeIn = 1
		'Lariska tells you that you crushed Christina''s spirit when you beat her, and that there''s no fight left in her now. It sounds like Coach Pavlovich took your threat seriously too, as she was kicked from both the cheerleading squad and the track team.'
		
	elseif (week = 1 or week = 3 or week = 5) and christinaQW['fight'] = -1 and christinaQW['subpath'] = 3:
		menu_off = 1
		npc_rel['A18'] = 0
		'Christina approaches you. "Have you recovered from your ass-kicking yet? At least you understand your place now. I''m looking forward to after P.E. class today!"'
		'"W-What happens after P.E. class?" you meekly ask.'
		'Without answering, she simply smirks at you and walks away.'
	end
end

if $ARGS[0] = 'KristinaFight':
	*clr & cla
	menu_off = 1
	$location_type = 'event_outdoors'
	christinaQW['dompath'] = 5
	gs 'stat'
	'<center><video autoplay loop src="images/characters/pavlovsk/school/girl/christina/school/fight.mp4"></video></center>'
	'As you cross the school yard after school, you are surprised by something heavy slamming into your back. Falling to your hands and knees, you look behind you and see Christina, her face contorted with rage. "You fucking bitch! How dare you spread shit about me? I''ll smash your fucking face in!"'
	act 'Fight her':
		gs 'fight', 'initFight'
		gs 'fight_npcdata', 'christina'
		gt 'fight', 'start'
	end
end

if $ARGS[0] = 'SchoolRun':
	$location_type = 'event_outdoors'
	*clr & cla
	menu_off = 1
	minut += 30
	gs 'stat'
	'You think you might be able to win, and sign up to participate in the competition. After quickly changing into your running gear you return to the track. The guys are having their race first, and you watch them as you prepare. As expected, Ivan wins. When the guys have finished, it is the girls'' turn.'

	act 'Go to the start':
		*clr & cla
		minut += 30
		SchoolSorev = 0
		kolsorev += 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/begsor.jpg"></center>'
		'The 400m sprint event for girls is up next. You take off your jacket and go the starting line, mentally preparing yourself for the race.'
		wait 725
		'On your marks...'
		wait 725
		'Get set...'
		wait 725
		'Go!'
		wait 725

		begminus = 0
		if bmi_calc > 25: 
			begminus = ((bmi_calc - 25)*4)
		elseif bmi_calc < 18:
			begminus = ((25 - bmi_calc)*6)
		end
        begresult = pcs_run - begminus
		
		act 'Run': gs 'gschool_grounds', 'SchoolRun_result'
		gs 'willpower', 'skill', 'run_lvl', 'hard'
		if will_cost <= pcs_willpwr:
			act 'Give it your best (<<will_cost>> Willpower)':
				begresult += 10
				gs 'willpower', 'pay', 'self'
				gs 'gschool_grounds', 'SchoolRun_result'
			end
		else
			act 'Give it your best (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
	end
end

if $ARGS[0] = 'SchoolRun_result':
	cla
	if begresult < 10:
		grupvalue[2] -= 2
		'You barely manage to reach the finish line at all. There''s no point in checking where you finished, you already know you were last by quite a large margin.'
	elseif begresult < 15:
		grupvalue[2] -= 1
		'You struggle to keep up with the other girls, and finish in last place.'
	elseif begresult < 20:
		'You run as fast as you can, but only manage to finish second to last.'
	elseif begresult < 25:
		'You run as fast as you can, but only manage to take 6th place.'
	elseif begresult < 30:
		grupvalue[2] += 1
		'You run as fast as you can, but only manage to take 5th place.'
	elseif begresult < 35:
		grupvalue[2] += 1
		'You run as fast as you can, but only manage to take 4th place.'
	elseif begresult < 45:
		grupvalue[2] += 2
		'You run as fast as you can, and manage to take 3rd place. Mr. Pavlovich, the P.E. teacher at your school, congratulates you.'
	elseif begresult < 50:
		grupvalue[2] += 3
		'You run as fast as you can, and manage to take 2nd place. Mr. Pavlovich, the P.E. teacher at your school, congratulates you on your achievement.'			
	else
		swinbeg += 1
		grupvalue[2] += 5
		'You run as fast as you can, and manage to reach the finish line first. Mr. Pavlovich, the P.E. teacher at your school, congratulates you on winning the competition as you walk around with your arms raised in triumph.'
	end

	run_exp += 1
	fat -= 2

	if swinbeg = 3 and LocalRun = 0:
		LocalRun = 1
		if razradbeg < 1:
			'You have won the school running competition three times now and Mr. Pavlovich, the P.E. teacher at your school, announces that your success has not gone unnoticed. He smiles and awards you the third tier sports trophy in running, which you proudly accept and show to everyone.'
			razradbeg = 1
		else
			'You have won the school running competition three times now and Mr. Pavlovich, the P.E. teacher at your school, smiles and awards you a local competition trophy which you proudly accept and show to everyone.'
		end
	end

	gs 'stat'
	
	act 'Return to town': gt 'gschool_grounds', 'main'
end

if $ARGS[0] = 'SchoolRun2':
	$location_type = 'event_outdoors'
	*clr & cla
	menu_off = 1
	minut += 30
	gs 'stat'
	'You think you might be able to win, and sign up to participate in the competition. After quickly changing into your running gear you return to the track. The guys are having their race first, and you watch them as you prepare. As expected, Ivan Prokhorov wins. When the guys have finished, it is the girls'' turn.'

	if christinaQW['dompath'] >= 6:
		if swinbeggor = 0:
			'You find Lina at the starting line with a smile on her face. Seeing you she waves and calls out cheerfully "Good luck, <<$pcs_nickname>>!"'
		elseif swinbeggor = 1:
			'You find Lina at the starting line with a smile on her face. Seeing you she waves and calls out cheerfully "I am going to get you this time, <<$pcs_nickname>>!"'
		elseif swinbeggor = 2:
			'You find Lina at the starting line with a smile on her face. Seeing you she waves and calls out cheerfully "Not going to get me a third time, <<$pcs_nickname>>!"'
		end
	else
		if swinbeggor = 0:
			'Getting ready at the starting line is Christina, one of the girls in your class. She just scoffs at you. "You''ll never beat me!"'
		elseif swinbeggor = 1:
			'Getting ready at the starting line is Christina, one of the girls in your class. She looks at you angrily and says "You beat me once, but never again!"'
		elseif swinbeggor = 2:
			'Getting ready at the starting line is Christina, one of the girls in your class. She looks at you with pure hatred. "You again, bitch? You''re going down this time, I promise you!"'
		end
	end

	act 'Go to the start':
		*clr & cla
		kolsorev += 1
		minut += 30
		SchoolSorev = 0
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/grounds/begsor.jpg"></center>'
		'The 400m sprint event for girls is up next. You take off your jacket and go the starting line, mentally preparing yourself for the race.'
		wait 725
		'On your marks...'
		wait 725
		'Get set...'
		wait 725
		'Go!'
		wait 725

		begminus = 0
		if bmi_calc > 25: 
			begminus = ((bmi_calc - 25)*4)
		elseif bmi_calc < 18:
			begminus = ((25 - bmi_calc)*6)
		end
        begresult = pcs_run - begminus
		
		act 'Run': gs 'gschool_grounds', 'SchoolRun2_result'
		gs 'willpower', 'skill', 'run_lvl', 'hard'
		if will_cost <= pcs_willpwr:
			act 'Give it your best (<<will_cost>> Willpower)':
				begresult += 10
				gs 'willpower', 'pay', 'self'
				gs 'gschool_grounds', 'SchoolRun2_result'
			end
		else
			act 'Give it your best (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end		
	end
end

if $ARGS[0] = 'SchoolRun2_result':
	cla
	if begresult < 10:
		grupvalue[2] -= 2
		'You barely manage to reach the finish line at all. There''s no point in checking where you finished, you already know you were last by quite a large margin.'
	elseif begresult < 15:
		grupvalue[2] -= 1
		'You struggle to keep up with the other girls, and finish in last place.'
	elseif begresult < 20:
		'You run as fast as you can, but only manage to finish second to last.'
	elseif begresult < 25:
		'You run as fast as you can, but only manage to take 6th place.'
	elseif begresult < 30:
		grupvalue[2] += 1
		'You run as fast as you can, but only manage to take 5th place.'
	elseif begresult < 35:
		grupvalue[2] += 1
		'You run as fast as you can, but only manage to take 4th place.'
	elseif begresult < 40:
		grupvalue[2] += 2
		'You run as fast as you can, and manage to take 3rd place. Mr. Pavlovich, the P.E. teacher at your school, congratulates you.'
	elseif begresult < 50:
		grupvalue[2] += 3
		'You run as fast as you can, and manage to take 2nd place. Mr. Pavlovich, the P.E. teacher at your school, congratulates you on your achievement.'
	else
		swinbeggor += 1
		grupvalue[2] += 5
		'You run as fast as you can, and manage to reach the finish line first. Mr. Pavlovich, the P.E. teacher at your school, congratulates you on winning the competition yet again while you walk around with your arms raised in triumph.'
	end

	run_exp += 1
	fat -= 2

	if swinbeggor = 3 and LocalRun = 1:
		LocalRun = 2
		if razradbeg < 2:
			'You have won the competition three more times and Mr. Pavlovich awards you the second tier sports trophy in running.'
			razradbeg = 2
		else
			'You have won the competition three more times and Mr. Pavlovich awards you another trophy.'
		end
		if christinaQW['dompath'] >= 6:
			npc_rel['A19'] += 5
			'Lina stands at at the finish line, gasping for breath. Even though you beat her again, she flashes you a bright smile in congratulations.'
		else
			'Christina stands nearby glaring at you the whole time, her hands balled into fists. She''s seething with rage, realizing she cannot beat you.'
		end
	elseif swinbeggor = 2 and begresult >= 130:
		if christinaQW['dompath'] >= 6:
			npc_rel['A19'] += 5
			'Lina stands at at the finish line, gasping for breath. Even though you beat her a second time, she flashes you a bright smile in congratulations.'
		else
			npc_rel['A18'] -= 10
			'Christina is gasping for breath at the finish line. She was unable to overtake you, and came second again. Christina avoids your gaze, not wanting to look you in the eye.'
		end
	elseif swinbeggor = 1 and begresult >= 130:
		if christinaQW['dompath'] >= 6:
			npc_rel['A19'] += 5
			'Lina stands at at the finish line, gasping for breath. Even though you beat her, she flashes you a bright smile in congratulations.'
		else
			npc_rel['A18'] -= 10
			'Christina is gasping for breath at the finish line. She was unable to overtake you, and came second yet again. You just flash her a toothy grin and continue your victory lap. Through clenched teeth Christina growls at you. "You bitch!"'
		end
	end

	gs 'stat'

	act 'Leave': gt 'gschool_grounds', 'main'
end

if $ARGS[0] = 'change_room':
	$loc_arg = 'change_room'
	$loc = 'gschool_grounds'
	$location_type = 'bathroom'
	$locclass = 'locker'
	$locM = 'gschool_grounds'
	$locM_arg = 'change_room'
	$menu_loc = 'gschool_grounds'
	$menu_arg = 'change_room'
	menu_off = 0
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/gym/lockerroom.jpg"></center>'

	act 'Leave':
		if $clothingworntype = 'nude':
			msg'<b><font color = red>You need to get dressed first!</font></b>'
			gt 'gschool_grounds', 'change_room'
		else
			minut += 1
			gt 'gschool_grounds', 'main'
		end
	end

	act 'Go to the sports field':
		if $clothingworntype = 'nude':
			msg'<b><font color = red>You need to get dressed first!</font></b>'
			gt 'gschool_grounds', 'change_room'
		else
			minut += 1
			gt 'gschool_grounds', 'sports'
		end
	end
	*nl
	'The locker room is accessed through the school gym, and is left unlocked until 18:30 each day for the use of students using the sports ground for training and events outside school hours. The room is cold, damp and filled with lockers, benches and not much else. You can <a href="exec: gt ''wardrobe'', ''start''">change your outfit</a> by the lockers here'+iif(pirsA + pirsB + pirsC + pirsD + pirsE + pirsF + pirsN + pirsG + pirsGL > 0, ', and <a href="exec:dynamic $pirManage">manage your piercings</a>', '.')+''
	*nl
	'You can access your <a href="exec: quick_dress = 1 & gt ''wardrobe'', ''default''">default clothing</a> options directly.'

	if (hour = 18 and minut >= 30) or hour > 18:
		'The school is closing for the night and you have to leave before you get locked in.'
		exit
	end

	gs 'wardrobe', 'sports_outfit'
	act 'Change your outfit':
		cla
		gt 'wardrobe', 'start'
	end
	dynamic $publicpan
	act 'Look in the mirror': gt 'mirror', 'start'
	if pcs_hairbsh = 0: act 'Brush your hair': gt 'mirror', 'brush'
	dynamic $quickwash
	dynamic $basin
	if shampoo > 0:
		dynamic $shower
	else
		'<font color="red">You''ve run out of shampoo and will have to buy some more before you can wash yourself.</font>'
	end
	dynamic $tampon
	gs 'din_van', 'pblc_pee'
end

--- gschool_grounds ---------------------------------

