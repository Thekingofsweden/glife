# npcgenext


! {Dick size and thickness generator. Outputs $npctempgn[4] for girth and npctempgn[5] for length. It takes a higher add number for a call that is supposed to be bigger than average.}
if $args[0] = 'dick':
	tempfindadd = 0
	if args[1] > 0: tempfindadd += args[1]

	penis_dist = rand(1,10000)

	if penis_dist <= 14:
		tempfindadd += rand(5,6)
	elseif penis_dist >= 15 and penis_dist <= 249:
		tempfindadd += rand(7,8)
	elseif penis_dist >= 250 and penis_dist <= 1599:
		tempfindadd += rand(9,10)
	elseif penis_dist >= 1600 and penis_dist <= 4999:
		tempfindadd += rand(11,13)
	elseif penis_dist >= 5000 and penis_dist <= 8399:
		tempfindadd += rand(13,15)
	elseif penis_dist >= 8400 and penis_dist <= 9749:
		tempfindadd += rand(16,17)
	elseif penis_dist >= 9750 and penis_dist <= 9984:
		tempfindadd += rand(18,19)
	elseif penis_dist >= 9985 and penis_dist <=9998:
		tempfindadd += rand(20,21)
	else
		tempfindadd += rand(22,30)
	end
	
	npctempgn[5] = tempfindadd

	! {It sets the girth based on the dick size}
	if tempfindadd < 9:
		tempfindgirth = 1
	elseif tempfindadd < 12:
		tempfindgirth = 2
	elseif tempfindadd < 15:
		tempfindgirth = 3
	elseif tempfindadd < 18:
		tempfindgirth = 4
	elseif tempfindadd < 21:
		tempfindgirth = 5
	elseif tempfindadd < 24:
		tempfindgirth = 6
	else
		tempfindgirth = 7
	end
	! {Then it gives random variance with girth. 1 or 2 drops a size. 3 or 4 stays the same. 5 or 6 goes up a size. A 1 or a 6 gives a chance to go up or down a size again, for a max of 2 changes from the base girth from size.}
	tempfinddick = rand(1,6)
	if tempfinddick < 3:
		tempfindgirth -= 1
		if tempfinddick = 1:
			tempfinddick = rand(1,3)
			if tempfinddick = 1:
				tempfindgirth -= 1
			elseif tempfinddick = 3:
				tempfindgirth += 1
			end
		end
	elseif tempfinddick > 4:
		tempfindgirth += 1
		if tempfinddick = 6:
			tempfinddick = rand(1,3)
			if tempfinddick = 1:
				tempfindgirth -= 1
			elseif tempfinddick = 3:
				tempfindgirth += 1
			end
		end
	end
	! {Then it returns the official girth.}
	if tempfindgirth < 2:
		$npctempgn[4] = 'skinny'
	elseif tempfindgirth = 2:
		$npctempgn[4] = 'slim'
	elseif tempfindgirth = 3:
		$npctempgn[4] = 'well proportioned'
	elseif tempfindgirth = 4:
		$npctempgn[4] = 'thicker than average'
	elseif tempfindgirth = 5:
		$npctempgn[4] = 'thick'
	elseif tempfindgirth = 6:
		$npctempgn[4] = 'massive'
	else
		$npctempgn[4] = 'monstrous'
	end

	! {cleanup}
	killvar 'tempfindadd'
	killvar 'tempfinddick'
	killvar 'tempfindgirth'
end

! {Personality Generator. Outputs $npctempgn[6]}
if $args[0] = 'pers':
	if rand(0,1) = 1:
		$npctempgn[6] = 'E'
	else
		$npctempgn[6] = 'I'
	end
	if rand(0,1) = 1:
		$npctempgn[6] += 'S'
	else
		$npctempgn[6] += 'N'
	end
	if rand(0,1) = 1:
		$npctempgn[6] += 'T'
	else
		$npctempgn[6] += 'F'
	end
	if rand(0,1) = 1:
		$npctempgn[6] += 'J'
	else
		$npctempgn[6] += 'P'
	end
end

! {Venerial Disease Generator. requires an index.}
if $args[0] = 'rstd':
	if $ARGS[1] = '': $ARGS[1] = 0
	if mid($ARGS[1],1,1) < 10: 
		$tempvan1 = 'A' + $ARGS[1]
	else
		$tempvan1 = $ARGS[1]
	end
	if rand(0,100) = 100:
		tempvenerial = rand(1,7)
		if tempvenerial = 1: npc_herpes[$tempvan1] = 1
		if tempvenerial = 2: npc_syth[$tempvan1] = 1
		if tempvenerial = 3: npc_gon[$tempvan1] = 1
		if tempvenerial = 4: npc_chlam[$tempvan1] = 1
		if tempvenerial = 6: npc_genwart[$tempvan1] = 1
		if tempvenerial = 7: npc_thrush[$tempvan1] = 1
	end
	killvar 'tempvenerial'
	killvar '$tempvan1'
end



--- npcgenext ---------------------------------

