# kikbox
if $ARGS[0] = 'start':
	cla
	if health <= 0:
		'<b><font color = red>You lost by TKO</font></b>'
		xgt 'enderKik','lossKO'
		exit
	elseif healthV <= 0:
		'<b><font color = green><<$nameV>> lost by TKO</font></b>'
		xgt 'enderKik','winKO'
		exit
	end

	if round >= 1:
		'<center><b>Round <<round>></b></center>'
		*nl
		if sparing = 1:
			'<center><img src="images/etogame/kik1.jpg"></center>'
		else
			'<center><img src="images/community/ring<<round>>.jpg"></center>'
		end
		cla
		minround += 1
		'<b>= = = = = = = = = = = = = (ROUND <<round>>) = = = = = = = = = = = = =</b>'
		if minround = 1:'<b>Start of the round</b>'
		if minround = 2:'<b>Time in round 0:30</b>'
		if minround = 3:'<b>Time in round 1:00</b>'
		if minround = 4:'<b>Time in round 1:30</b>'
		if minround = 5:'<b>Time in round 2:00</b>'
		if minround = 6:'<b>Time in round 2:30</b>'
		'<b><<$nameV>></b> Life <b><font color="red"><<healthV>></font></b>, Endurance <b><font color="green"><<staminaV>></font></b>'
		'Your life <b><font color="red"><<health>></font></b>, Endurance <b><font color="green"><<stamina>></font></b>'
		'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
		'Your points <<pointKik>>Points opponent <<pointKikV>>'
		'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
		gs 'initKik'

		if hod = 0:
			!!Player attacks
			'<font color="green">You can attack</font>'

			act 'Fast Jab':
				cla
				damage = rand(stren/12, stren/8)

				if damage <= 0:damage = 1

				koefatak = (speed + react + jab) * 2
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="green">You go for the quick jab and step forward.</font>'
				gs 'Kikatak', 'player'
				xgt 'kikbox', 'sta'
			end

			act 'Power punch':
				cla
				damkoef = stren * 20 / 100
				damage = rand(stren - damkoef, stren + damkoef)

				if damage <= 0:damage = 1

				koefatak = (speed + react + punch)
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="green">You attempt a powerful punch.</font>'
				gs 'Kikatak', 'player'
				xgt 'kikbox', 'sta'
			end

			act 'Big kick':
				cla
				damkoef = stren * 20 / 100
				damage = rand((stren - damkoef) * 3, (stren + damkoef) * 3)

				if damage <= 0:damage = 1

				koefatak = (speed + react + kik) / 2
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="green">You try a powerful kick.</font>'
				gs 'Kikatak', 'player'
				xgt 'kikbox', 'sta'
			end
		else
			!!Enemy attacks
			'<font color="red"><<$nameV>> attacks</font>'
			tiprand = rand(0, 2)

			if tiprand = 0:
				damage = rand(strenV / 12, strenV / 8)

				if damage <= 0:damage = 1

				koefatak = (speedV + reactV + JabV) * 2
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="red"><<$nameV>> deals with quick jab, stepping forward.</font>'
				gs 'Kikatak', 'enemy'
				xgt 'kikbox', 'sta'
			elseif tiprand = 1:
				damkoef = strenV * 20/100
				damage = rand(strenV - damkoef, strenV + damkoef)

				if damage <= 0:damage = 1

				koefatak = (speedV + reactV + punchV)
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="red"><<$nameV>> applies a power punch.</font>'
				gs 'Kikatak', 'enemy'
				xgt 'kikbox', 'sta'
			elseif tiprand = 2:
				damkoef = strenV * 20 / 100
				damage = rand((strenV - damkoef) * 3, (strenV + damkoef) * 3)

				if damage <= 0:damage = 1

				koefatak = (speedV + reactV + kikV) / 2
				koefpop = koefatak * 20 / 100
				popad = rand(koefatak - koefpop, koefatak + koefpop)
				'<font color="red"><<$nameV>> goes for a kick.</font>'
				gs 'Kikatak', 'enemy'
				xgt 'kikbox', 'sta'
			end
		end
	end
end

if $ARGS[0] = 'sta':
	cla
	ionce = 0
	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
	if minround = 1:'<b>Time in round 0:30</b>'
	if minround = 2:'<b>Time in round 1:00</b>'
	if minround = 3:'<b>Time in round 1:30</b>'
	if minround = 4:'<b>Time in round 2:00</b>'
	if minround = 5:'<b>Time in round 2:30</b>'
	if minround = 6:'<b>Time in round 3:00</b>'
	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
	'<b><<$nameV>></b> Life <b><font color="red"><<healthV>></font></b>, Endurance <b><font color="green"><<staminaV>></font></b>'
	'Your life <b><font color="red"><<health>></font></b>, Endurance <b><font color="green"><<stamina>></font></b>'
	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'

	act 'Further':gt 'kikbox', 'start'

	if minround = 6:
		cla
		minround = 0
		round += 1
		'The bell rings indicating the end of round <<round>>.'
		if round <= formula:
			act 'Back to the corner':
				cls
				'<center><img src="images/community/kickboxing_corner.jpg"></center>'
				'You try to rest as best as you can.'

				act 'Return to the ring':gt 'kikbox', 'start'
			end
		elseif round > formula and sparing = 0:
			act 'Go to the middle of the ring for the announcement':
				cla

				if pointKik > pointKikV:
					gt 'enderKik', 'win'
				elseif pointKik < pointKikV:
					gt 'enderKik', 'loss'
				elseif pointKik = pointKikV:
					gt 'enderKik', 'draw'
				end
			end
		elseif round > formula and sparing = 1:
			if pointKik > pointKikV:
				gt 'enderKik', 'win'
			elseif pointKik < pointKikV:
				gt 'enderKik', 'loss'
			elseif pointKik = pointKikV:
				gt 'enderKik', 'draw'
			end
		end
	end
end

--- kikbox ---------------------------------

