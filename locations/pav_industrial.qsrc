# pav_industrial

CLOSE ALL

$loc_arg = ''
$loc = 'pav_industrial'
$location_type = 'public_outdoors'
$menu_loc = 'pav_industrial'
$menu_arg = ''
menu_off = 0

if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

*clr & cla
litltown = 1
alkoblock = 0
boynumBlock = 0

!call random events from [street_events], and display them before show location.
if streetevent_hour + 2 < hour:gt'street_event','pav_industrial', 'main'

'<center><h2>Pavlovsk Industrial District</h2></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/pav_factory.jpg"></center>'
*nl
'This is the Industrial region of Pavlovsk. It houses some factories and warehouses, along with the old houses that are small and run down.'
*nl
'This area is dominated by one of the biggest employers in Pavlovsk, the local textile factory. It''s owned by G&M, who opened up a store opposite.'

if hour >= 8 and hour < 20 and week >= 1 and week <= 5:
	'Behind the security checkpoint, you can see the <a href="exec:minut += 5 & gt ''pav_factory'', ''enter''">entrance</a> that every employee passes through.'
elseif hour < 8 or hour > 20 or week > 5:
	'The factory is currently closed.'
end
'Opening hours are Monday through Friday:'
'First shift - 08:00 to 16:00'
'Second shift - 16:00 to 20:00'
*nl
'In a smaller building right next to the factory is the local '+iif(week < 7 and hour >= 8 and hour < 18,'<a href="exec: gt ''pav_factory'', ''tailor''">tailor</a>', 'tailor')+' shop, a service offered by the factory. Opening hours are Monday to Saturday, between 08:00 to 17:00.'

gs'street_event','pav_industrial', 'extras'

if ((month = 9 and day > 16) or month >= 10) and (AlbinaQW['StarletsShutDown'] = 0 or AlbinaQW['Chernov'] = 0): 'You can see election posters on the walls all over town. They say: "Vote for Boris Barlovsky!" Wait a minute... isn''t that Albina''s father?'
if AlbinaQW['StarletsJoined'] > 0 and AlbinaQW['StarletsShutDown'] = 0 and npc_pregtalk['A23'] = 0 and AlbinaQW['Chernov'] = 0 and ((month = 11 and day > 18) or month = 12) and hour >= 14:gt 'albina_election_events'

if rand(1,5) >= 4 and fame['pav_slut'] >= 100:
	if fame['pav_slut'] < 150:
		'<br>You catch people staring at you intently, as if they''re supposed to know who you are but can''t quite remember why. You sincerely hope they don''t realize your promiscuous adventures are the reason why. The occasional person chuckling and pointing at you crushes those hopes, and you quickly keep walking before they get a chance to say anything.'
	elseif fame['pav_slut'] < 200:
		'<br>People look at you with lewd grins on their faces, some of them even making vulgar gestures. Looks like they know what you''ve been up to.'
	else
		'<br>Everywhere you go, people recognize you as a whore. Some even come over and slap you on the ass, claiming they know you like it when they do, or claiming they want to sample your goods. An elderly woman sitting on a bench calls out to you, loudly calling you a slut and a whore. You hide your face and run away as fast as you can, before she can draw too much attention to you.'
	end
end

gs 'stat'

if car > 0 and cardrive = 26:
	cardrive = 26
	'Your <a href="exec:gs ''carF'', ''start''"><<$car>></a> is parked in the street.'
end

if week < 6 and hour >= 8 and hour < 20:
	*pl 'The local <a href="exec:torg = 0 & gt ''budgetclothes'',''start''">G&M Store</a> is currently open.'
else
	*pl 'The local G&M store is currently closed. Opening hours are Monday to Friday, between 08:00 to 20:00.'
end

act 'Walk to the Five Eight estate (0:05)':minut += 5 & gt 'pav_complex', 'start'
act 'Walk to the Residential area (0:05)':minut += 5 & gt 'pav_residential'
act 'Walk to the Commercial area (0:05)':minut += 5 & gt 'pav_commercial'

if week < 7 and hour >= 8 and hour < 18: 
	act 'Visit the tailor': gt 'pav_factory', 'tailor'
else
	'<br>The tailor is closed right now.'
end

act 'Wait':gs 'obj_din', 'wait'

!!if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

if npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week = 7:
	menu_off = 1
	cla & *clr
	daybelisex = daystart
	minut += 5
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'As you walk through Pavlovsk, Vadim Belys Mitsubishi Pajero stops near you.'

	act 'Go to the jeep': gt 'belgang', 'payday' 	
elseif npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week ! 7 and belgangPayWeek >= 300 and belgangProstitute = 1 and rand (0,2) = 0:
	menu_off = 1
	cla & *clr
	daybelisex = daystart
	minut += 5
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'You see the by now familiar old Mitsubishi Pajero driving through the streets with Vadim Bely and his gang. Your heart skips a beat as you notice one of them pointing at you, and soon they pull over.'
		
	act 'Go to the jeep': gt 'belgang', 'workofdebt' 	
end

--- pav_industrial ---------------------------------

