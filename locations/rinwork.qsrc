# rinwork
$location_type = 'event_outdoors'

! Variables used
! rinvesh = 	sold Items of clothing
! rinmon =		Total cash taken
! arturplan =	Amount Arthur expects min	
! arturnoo, arturyes = Times failed, reached sales
! workrin 		working in the market
! repa   ???????????
! pokupatel = 	customer interested
! prodpayum =	average price
! pokpay =		amount customer is willing to pay
! pcs_apprnc =	Attractiveness
! rinslut 		Arthur only keeps quiet if you do what he says
! palevorin =	0:Never had Sex w Arthur
! 				1:Had Sex w Arthur
!				2:Declined GB
!				3:Declined GB talked Arthur
!				4:Agreed GB
!				5:Agreed GB talked Arthur
!tetaKataQW		?
!annaQW 		?
!NadiaQW 		?
!

if $ARGS[0] = 'start':
	$metka = 'start'
	$loc = 'rinwork'
	cla & *clr
	$menu_loc = 'rinwork'
	$menu_arg = 'start'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Marketplace</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/market/rinok.jpg"></center>'
	'You stand at your clothes stall waiting for buyers.'
	'Cash taken <<rinmon>> <b>₽</b>, You have sold <<rinvesh>> items.'
	'The average price on the market <<prodpayum>>'

	if hour = 14:
		'Arthur comes to you and you start to calculate how much you should give him.'
		act 'Give money to Arthur':
			cla
			*clr
			rinart = rinvesh * 1000
			rinmon -= rinart
			money += rinmon
			gs 'stat'
			'You gave Arthur <<rinart>> per sold <<rinvesh>> things.'
			'Today you have earned <<rinmon>> <b>₽</b>'

			rinmon = 0
			rinvesh = 0
			
			if rinart < arturplan:
				arturnoo += 1
				'Uh, <<$pcs_nickname>>, this is bad, you haven''t made enough money. You will completely ruin me.'
			else
				arturyes += 1
				'Well done <<$pcs_nickname>>, good job.'
			end

			if arturnoo > arturyes:arturnoo = arturnoo - arturyes & arturyes = 0
			if arturyes > arturnoo:arturyes = arturyes - arturnoo & arturnoo = 0
			if arturnoo = arturyes:arturnoo = 0 & arturyes = 0

			if arturnoo > 10:
				arturnoo = 0
				work = 0
				workrin = 0
				'Uh, you''re fucking useless. Here''s your papers. Hit the road, I do not want to see you ever again!'

				act 'Leave':gt 'street'

				exit
			end

			rinrand = rand(1, 400)

			if rinrand < pcs_apprnc:
				if palevorin <= 1 or palevorin = 3:
					if palevorin = 0:
						'"Hey <<$pcs_nickname>>, you and I have not even met properly. Come into the warehouse, eat barbecue, drink and get acquainted.'
					elseif palevorin = 1:
						'"Hey <<$pcs_nickname>>, want to Come to the warehouse again, eat barbecue, drink and have some fun.'
					else
						'"Hey <<$pcs_nickname>>. Come into the warehouse, eat barbecue, drink, rest." He walks right up to you and slaps you on the ass.'
					end

					act 'Refuse':
						cla
						'You start making excuses. Arthur says to you, "Hey, don''t insult me, your stall is dirty, it has dust and dirt all over it. I should fine you, but if you''re nice I will let you off.'

						act 'Go to warehouse':gt 'RynokHoz', 'the_beginning'

						act 'No':
							cla
							arturplan += 1000

							if money >= 1000:
								money -= 1000
								'"Hey, your stall is dirty, you owe a thousand <b>₽</b>, in fine." Arthur takes the money.'
							else
								'Go with him, before it gets worse'
							end

							act 'Leave':gt 'street'
						end
					end

					act 'Go with him':gt 'RynokHoz', 'the_beginning'
					
				elseif palevorin = 2:
					'Artur goes behind you into the stall carrying some stuff, then he comes up behind you are working and gives your ass a slap and squeeze. "I am glad you turned Abdul and Hasan down. I like having you all to myself."'
					'You turn your head to look at him and smile. "I like that too, but maybe from now on you can lock the door first?"'
					'He laughs and nods. "Of course of course, anything for you my sweet. If you wish the door to be locked from now on the door will be locked from now on. Speaking of which, come let us go try locking the door to make sure it works and have lunch."'
					act 'Go with Artur':palevorin = 3 & gt 'RynokHoz', 'the_beginning'
					
				elseif palevorin = 4:
					'Artur goes behind you into the stall carrying some stuff. He comes up behind you are working and gives your ass a slap and squeeze, with one hand while the other one snakes around and roughly grabs your tit, as he whispers in your ear. "So you like cock, I didn''t think you were a little slut but that''s ok, I like dirty sluts."'
					'He cuts you off before you can make any protest about being slut. "No it is good, I can use this. Now come let us go to the warehouse so I can fuck you."'
					
					act 'Go with him ':palevorin = 5 & gt 'RynokHoz', 'sex'
					
					gs 'willpower', 'sex', 'resist', 'hard'
					if will_cost <= pcs_willpwr:
						act 'Refuse (<<will_cost>> Willpower)':
							cla
							gs 'willpower', 'pay', 'resist'
							gs 'stat'
							'You shake your head and pull out of his grasp. "I am not a slut Artur and I am not going with you."'
							'You see the anger flare up in his eyes. "So you think you are to good for me whore? I gave you a job, I treated you nice and then you fuck those two and now I am not good enough?" He shakes his head. "No get your ass in the warehouse now or you are fired and I will tell Abdul and Hasan they are free to tell everyone about how you greedily took their cocks like the little slut you are."'
							'You are a bit shocked at this level of anger and the ultimatum. You are unsure what to do, go with him, keep your job, and your rep. Or refuse and find a new job and hope no one believes them.'

							act 'Go to warehouse':palevorin = 5 & gt 'RynokHoz', 'sex'

							gs 'willpower', 'sex', 'resist', 'hard'
							if will_cost <= pcs_willpwr:
								act 'Get fired (<<will_cost>> Willpower)':
									cla
									gs 'pain', 5, 'cheeks', 'slap'
									gs 'willpower', 'pay', 'resist'
									gs 'stat'
									'You shake your head no. "No I won''t go with you."'
									'Before you notice him raising his hand, you feel the burning on your cheek as he slaps you. "Then get out of her whore, your fired and I don''t want to see you back here!" He says very loudly as he pushes you out from behind his stall, loud enough that many people are looking your direction now at what is going on.'
									'With shame and a burning cheek you walk away.'
									if rinslut = 0:rinslut = 1
									act 'Leave':gt 'street'
								end
							else
								act 'Get fired (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
							end	
						end
					else
						act 'Refuse (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
					end	
					
				elseif palevorin = 5:
					sexgrouprand = rand(0,1)
					
					if sexgrouprand = 1:
						'Artur walks up and slaps your ass hard and grabs you roughly by one arm. "Hey bitch, how would you like to make some extra money? My friends need company."'
						
						act 'Go with him':gt 'RynokHoz', 'group'
					else
						'Artur walks up and slaps your ass hard and grabs you roughly by one arm. "Bitch get your fucking ass in the warehouse."'
						
						act 'Go with him':gt 'RynokHoz', 'sex'
					end
					
					gs 'willpower', 'sex', 'resist', 'hard'
					if will_cost <= pcs_willpwr:
						act 'Refuse (<<will_cost>> Willpower)':
							cla
							gs 'willpower', 'pay', 'resist'
							gs 'stat'
							'You start making excuses. Arthur says to you, "Hey, don''t insult me you fucking slut, I was there. I know what a whore you are. Now get your fucking ass in the warehouse or the whole town will know what a whore you are."'
							if sexgrouprand = 1:
								act 'Go to warehouse':gt 'RynokHoz', 'group'
							else
								act 'Go to warehouse':gt 'RynokHoz', 'sex'
							end
							
							gs 'willpower', 'sex', 'resist', 'hard'
							if will_cost <= pcs_willpwr:
								act 'No (<<will_cost>> Willpower)':
									cla
									arturnoo = 0
									work = 0
									workrin = 0
									gs 'willpower', 'pay', 'resist'
									gs 'stat'
									if rinslut = 0:rinslut = 1
									if repa < 6:repa = 6
									'Then fuck you, you little fucking whore, your fired and by tomorrow the whole town will know what a whore you are."'

									act 'Leave':gt 'street'
								end
							else
								act 'No (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
							end	
						end
					else
						act 'Refuse (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
					end	
				else
					act 'Leave work':gt 'street'
				end
			else
				act 'Leave work':gt 'street'
			end	
		end
		exit
	elseif hour < 14:
		act 'Laze around (0:15)':
			minut += 15
			gt 'rinwork', 'start'
		end

		if money >= 200:
			act 'Buy a snack (200 <b>₽</b>) (0:15)':
				tetaKataQW += 1
				money -= 200
				gs 'kit_din'
				gs 'food', 'snack'
			end
		end
		
		rintakrand = rand(0, 5)
		talkrand = rand(0, 2)
		if hour = 9:
			if rintakrand = 0:
				$rintak = ' Hassan unloads clothes in front of saleswoman Nadias stall.'

				act 'See Hassan and Nadia':
					cla
					minut += 15
					
					if talkrand = 0:'Hassan utters to Nadia. "Today you will sell more, and to the rich ladies, I need the money."'
					if talkrand = 1:'Hassan opens trunks and puts clothes on the counter.'
					if talkrand >= 2:'Hassan drags trunks to his stall.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 1:
				$rintak = 'On the next stall Abdul is talking loudly and giving clothes to saleswoman Anna.'

				act 'Look at Abdul and Anna':
					cla
					minut += 15

					if talkrand = 0:'Abdul swears. "We''re fucked, you can not sell anything!" Anna retorts, "You give me this rotten shit to sell, take it nobody wants it."'
					if talkrand = 1:'Abdul slaps Annas ass, she pretends that nothing happened.'
					if talkrand >= 2:'Abdul drags trunks to his stall.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 2:
				$rintak = 'Abdul and Hassan stand beside your stall and saying something guttural.'

				act 'View the Armenians':
					cla
					minut += 15

					if talkrand = 0:'Abdul and Hassan continue to talk about something, you do not know the language, and can not understand what they are saying.'
					if talkrand = 1:'Abdul and Hassan ignore you and discuss what sounds like clothes prices.'
					if talkrand >= 2:'Abdul looks at you and says to Hassan, "Look at that a girl, she looks like a good fuck, yeah."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 3:
				$rintak = 'Anna hangs clothes on her stall shouting at Nadia.'

				act 'Chat with girls':
					cla
					annaQW += 1
					NadiaQW += 1
					minut += 15

					if talkrand = 0:
						'Anna tells you that her skirt needed altering, there is a great Jewish tailor, his shop is nearby.'
					else
						'Nadia tells her how some pervert molested her on the subway rubbing his dick on her ass.'
					end

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 4:
				$rintak = 'Arthur is talking about something with Abdullah.'

				act 'View the Armenians':
					cla
					minut += 15

					if talkrand = 0:'Abdul and Hassan continue to talk about something, you do not know the language, and can not understand what they are saying.'
					if talkrand = 1:'Abdul and Arthur ignore you and discuss what sounds like clothes prices.'
					if talkrand >= 2:'Abdul looks at you and says to Arthur, "Nice girl you got there, you fucking her?."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand >= 5:
				$rintak = 'Arthur, Abdul Hassan are talking loudly, about something in foreign language.'

				act 'View the Armenians':
					cla
					minut += 15

					if talkrand = 0:'Abdul and Hassan continue to talk about something, you do not know the language, and can not understand what they are saying.'
					if talkrand = 1:'Armenians ignore you and discuss what sounds like clothes prices.'
					if talkrand >= 2:'Abdul looks at you and says to the other Armenians, "Pretty little girl, can i have her?"'

					act 'Further':gt 'rinwork', 'start'
				end
			end

			'<<$rintak>>'
		elseif hour = 10:
			if rintakrand = 0:
				$rintak = 'Hassan sits on a chair dejectedly chewing food and talking with his mouth full to his saleswoman Nadia.'

				act 'Watch Hasan':
					cla
					minut += 15
					'Hassan says to Nadia "Nadia, why you refuse? Come here, have some wine."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 1:
				$rintak = 'At the stall in front of you sits Nadia doing crossword puzzles.'

				act 'Chat with Nadia':
					cla
					NadiaQW += 1
					minut += 15
					'Nadia leaves the crossword and willingly gossips with you.'
					
					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 2:
				$rintak = 'Nadias stall in front of you has many buyers and she fusses before them laying out goods.'

				act 'Chat with Nadia':
					cla
					NadiaQW -= 1
					minut += 15
					'Nadia is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 3:
				$rintak = 'At the next stall Anna is drinking coffee.'

				act 'Chat with Anna':
					cla
					annaQW += 1
					minut += 15
					'Anna is happy to chat with you about any old nonsense.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 4:
				$rintak = 'At the next stall Anna tries to interest buyers in her goods.'

				act 'Chat with Anna':
					cla
					annaQW -= 1
					minut += 15
					'Anya is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand >= 5:
				$rintak = 'Anna is gossiping with Aunt Katya.'

				act 'Chat with Anna and Aunt Katya':
					cla
					annaQW += 1
					tetaKataQW += 1
					minut += 15
					'Aunt Katya is talking about her pies and Anya complains that it because of her bloody pies that her ass is twice as big as it was.'

					act 'Further':gt 'rinwork', 'start'
				end
			end

			'<<$rintak>>'
		elseif hour = 11:
			if rintakrand = 0:
				$rintak = 'Nadia is approached by three Armenians, Aslan, Camille and Mukhtar and they begin to molest her, she swears at them and tries to dodge.'

				act 'Look at Nadia and Armenians.':
					cla
					minut += 15
					'The Armenians seem to be trying to persuade Nadia to have sex with them.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 1:
				$rintak = 'At the stall in front of you sits Nadia eating cake.'

				act 'Chat with Nadia':
					cla
					NadiaQW += 1
					minut += 15
					'Nadia finishes the cake and mumbles about the usual nonsense with you.'

					if clener = 0:
						clener = 1
						'Nadia tells you about an office building not too far away that is looking for a cleaner.'
					end

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 2:
				$rintak = 'Nadias stall in front of you has many buyers and she fusses before laying out goods.'

				act 'Chat with Nadia':
					cla
					NadiaQW -= 1
					minut += 15
					'Nadia is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 3:
				$rintak = 'At the next stall Anna is reading a newspaper.'

				act 'Chat with Anna':
					cla
					annaQW += 1
					minut += 15
					'Anna is happy to chat with you about any old nonsense.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 4:
				$rintak = 'At the next stall Anna tries to interest buyers in her goods.'

				act 'Chat with Anna':
					cla
					annaQW -= 1
					minut += 15
					'Anya is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand >= 5:
				$rintak = 'Nadia is approached by three Armenians, Aslan, Camille and Mukhtar and they begin to molest her, she swears at them and tries to dodge.'

				act 'Look at Nadia and Armenians.':
					cla
					minut += 15
					'The Armenians seem to be trying to persuade Nadia to have sex with them.'

					act 'Further':gt 'rinwork', 'start'
				end
			end

			'<<$rintak>>'
		elseif hour = 12:
			if rintakrand = 0:
				$rintak = 'Hasan has bought chebureki and is eating it while sitting behind Nadia in the stall.'

				act 'Chat with Nadia':
					cla
					NadiaQW -= 1
					minut += 15
					'Nadia shies away from talking in front of Hassan.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 1:
				$rintak = 'Nadia is surrounded by customers.'

				act 'Chat with Nadia':
					cla
					NadiaQW -= 1
					minut += 15
					'Nadia is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 2:
				$rintak = 'Nadia talks with Anna.'

				act 'Chat with girls':
					cla
					annaQW += 1
					NadiaQW += 1
					minut += 15

					if talkrand = 0:
						'Anna tells you that her friend used to clean an office building not far from here, they are looking for a new cleaner if you need to make some extra cash.'

						if clener = 0:clener = 1

					elseif talkrand = 1:
						'Nadia tells her how some pervert molested her on the subway rubbing his dick on her ass.'
					end

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 3:
				$rintak = 'Anna  is selling clothing to customers.'

				act 'Chat with Anna':
					cla
					annaQW -= 1
					minut += 15
					'Anna is not happy, "Shoo, no distractions, I have buyers."'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand = 4:
				$rintak = 'Anna is drinking coffee sitting at her stall.'

				act 'Chat with Anna':
					cla
					annaQW += 1
					minut += 15
					'Anna happily chats with you about nonsense.'

					act 'Further':gt 'rinwork', 'start'
				end
			elseif rintakrand >= 5:
				$rintak = 'Nadia is idly trying to catch flies.'

				act 'Chat with Nadia':
					cla
					NadiaQW += 2
					minut += 15
					'Nadia glad for the distraction and has fun chatting with you.'

					act 'Further':gt 'rinwork', 'start'
				end
			end

			'<<$rintak>>'
		elseif hour = 13:
			if rintakrand = 0:
				$rintak = 'Hassan begins to slowly collect goods from Nadia.'
			elseif rintakrand = 1:
				$rintak = 'Anya sits and calculates her profit for the day.'
			elseif rintakrand = 2:
				$rintak = 'Nadia calculates her days profit.'
			elseif rintakrand = 3:
				$rintak = 'Abdul begins collecting goods from Anna.'
			elseif rintakrand = 4:
				$rintak = 'Arthur walks past your stall.'
			else
				$rintak = 'Aunt Katya stops trading pies and leaves.'
			end

			'<<$rintak>>'
		end	
		
		pokrand = rand(0, 1)

		if pokrand = 0:
			$pokup = 'You have no customers.'
			pokupatel = 0
			''
			'Aunt Katya pushes her trolley through the market carrying cakes and coffee.'
		else
			pokupatel = 1
			poktiprand = rand(0, 7)

			if poktiprand = 0:$pokup = 'A middle-aged man approaches your stall.' & $poktip = 'Man' & pokti = 1
			if poktiprand = 1:$pokup = 'A young man approaches your stall.' & $poktip = 'Guy' & pokti = 2
			if poktiprand = 2:$pokup = 'A teenage boy approaches your stall.' & $poktip = 'Boy' & pokti = 3
			if poktiprand = 3:$pokup = 'A woman approaches your stall.' & $poktip = 'Woman' & pokti = 1
			if poktiprand = 4:$pokup = 'A girl approaches your stall.' & $poktip = 'Girl' & pokti = 2
			if poktiprand = 5:$pokup = 'A teen girl approaches your stall.' & $poktip = 'Teen girl' & pokti = 3
			if poktiprand = 6:$pokup = 'An old man approaches your stall.' & $poktip = 'Old man' & pokti = 0
			if poktiprand = 7:$pokup = 'A Grandma approaches your stall.' & $poktip = 'Old woman' & pokti = 0
		end

		'<b><<$pokup>></b>'

		if pokupatel = 1:
			act 'How can I help you?':
				cla
				minut += 15
				pokvnirand = rand(0, 2)

				if pokvnirand = 0:
					'<<$poktip>> with little interest examines your wares.'
				elseif pokvnirand = 1:
					'<<$poktip>> with interest looks through the clothes for sale.'
				elseif pokvnirand = 2:
					'<<$poktip>> picks up the item.'
				end

				act 'Now this is very fashionable':
					cla
					tipred = 3
					gs 'rinwork', 'answerrin'
				end

				act 'This item is very high quality and reliable':
					cla
					tipred = 1
					gs 'rinwork', 'answerrin'
				end

				act 'You''ll find this very reasonable':
					cla
					tipred = 0
					gs 'rinwork', 'answerrin'
				end

				act 'That is so you':
					cla
					tipred = 2
					gs 'rinwork', 'answerrin'
				end
			end
		end

	end
end

if $ARGS[0] = 'answerrin':
	if tipred = pokti:
		''
		if pokvnirand = 0:
			pokpay = rand(1000, 1200)
		elseif pokvnirand = 1:
			pokpay = rand(1100, 1300)
		elseif pokvnirand = 2:
			pokpay = rand(1300, 1500)
		end
		'<<$poktip>> asks you for a price'
		gs 'rinwork', 'tellprice'
	else
		'<<$poktip>> puts the item back and leaves.'
		act 'Further':gt 'rinwork', 'start'
	end
end

if $ARGS[0] = 'tellprice':
	act 'Name the price':
		cla
		prodpay = input ("Enter the selling price")

		if prodpay < 1000:prodpay = prodpayum
		poknorand = rand(1, 100 + pcs_apprnc/4)
		'You tell them <<prodpay>> <b>₽</b>'

		if prodpay <= pokpay:
			if poknorand >= 10:
				rinmon += prodpay
				rinvesh += 1
				'The buyer takes the item and pays <<prodpay>> <b>₽</b>.'

				act 'Sell it':gt 'rinwork', 'start'
			else
				'The buyer twirls clothing in their hands <<prodpay>> <b>₽</b> is too expensive.'
				gs 'rinwork', 'haggling'
			end
		elseif prodpay > pokpay and prodpay <= pokpay + 100:
			if poknorand >= 10:
				'The buyer twirls clothing in their hands <<prodpay>> <b>₽</b> is too expensive.'
				gs 'rinwork', 'haggling'
			else
				'Buyer puts the item back and leaves'

				act 'Return':gt 'rinwork', 'start'
			end
		elseif prodpay > pokpay + 100:
			'The buyer puts the item back and leaves.'

			act 'Return':gt 'rinwork', 'start'
		end
	end
end

if $ARGS[0] = 'haggling':
	if prodpay >= 1100:
		act 'Reduce the price by 100 <b>₽</b>':
			cla
			'You have reduced the price by 100 <b>₽</b>.'
			poknorand = rand(1, 100 + pcs_apprnc/4)

			if poknorand >= 10:
				rinmon += prodpay - 100
				rinvesh += 1
				'The buyer takes the item and pays <<prodpay - 100>> <b>₽</b>'
			else
				'The buyer puts the item back and leaves.'
			end
			
			act 'Haggle':gt 'rinwork', 'start'
		end
	elseif prodpay >= 1050:
		act 'Reduce the price by 50 <b>₽</b>':
			cla
			'You have reduced the price by 50 <b>₽</b>.'
			poknorand = rand(1, 100 + pcs_apprnc/4)

			if poknorand >= 30:
				rinmon += prodpay - 50
				rinvesh += 1
				'The buyer takes the item and pays <<prodpay - 50>> <b>₽</b>'
			else
				'The buyer puts the item back and leaves.'
			end
			
			act 'Haggle':gt 'rinwork', 'start'
		end
	end
	act 'You can not take less, take the item back':gt 'rinwork', 'start'
end


--- rinwork ---------------------------------

