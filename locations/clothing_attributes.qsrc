# clothing_attributes

!! **Clothing Organization
!! CloCategory: 1 Cheap, 2 Average, 3 Expensive, 4 Formal, 5 Office, 6 School, 7 Exercise, 8 Fetish, 9 Exhibit, 10 Uniform, 11 Coat, 12 Swimwear, 13 Burlesque
!!		Notes: 1-3 are general purpose clothing, 4-7 are self-explanatory, 8 is fetish wear, latex and leather. 9 is extreme clothing that exposes your body and likely to get you arrested . 10 is waitress, maid and stripper uniforms. 13 is for burlesque shows or a bordello
!! CloStyle: 1 Girlie, 2 Rebel, 3 Wealthy, 4 Prostitute, 5 is Loose fit
!!		Notes: 1 is clothing themed to give the impression of innocence of youth. 2 is for stereotypical punks; ripped clothing, leather jackets, etc. 3 gives the impression that theyre debutanttes or rich at first glance. 4 is stuff worn by prostitutes. 5 is formless clothing that doesn''t requiring refitting.
!! CloStyle2: 1 Maid, 2 Server, 3 Stripper, 4 School
!!		Notes: Used to distinguish between the main types of uniform.
!! CloInhibit: 0-100 Inhibition value required to wear the outfit
!!
!! **Clothing Variables
!! Scales use a 1-5 range. with 6 being a special case thats essentially nudity.
!!
!! CloQuality = 1-7 scale, based on the general expensiveness of the clothing. Higher is more expensive.
!!		Notes: 7 is reserved for ballroom gowns, and other clothing that might only be found on celebrities or millionaires at red carpet events.
!! 		Ex:		6-5 are the higher and lower levels of the expensive category
!!				4-3 are the same for average category
!!				2-1 are the same for cheap category.
!! CloThinness = 1-6 scale, with higher values being made of thinner material and/or being more tightly fitting.
!!		Notes:   Clothing with large cut-away sections or transparent sections should have a decreased CloThinness score
!!		Ex: 	6 intentionally reveals breast or pubic areas, with translucent or transparent areas and should trigger some sort of indecent exposure/arrest event if worn in public without underwear. 
!!				5 is thin enough to be seen through only in bright light, or is otherwise tight enough to show the body in detail if opaque.  
!!				4 is thin or tight enough to be flirty and fine in most places except the most conservative (church, school, etc). 
!!				3-2 is the range of clothing that is modest enough to be worn in all public places.
!!				1 does not follow the curves of the body in great detail, and makes it hard to see the body shape.
!!Top!!
!! CloTopCut = 1-4 scale* (with an additional +0-3 based on player breast sizes, for an effective 1-7 scale). System is based on based on how much skin on the torso is shown (including navel, not just above the breasts), with larger breasts implicitly making the outfit smaller and more revealing.
!!		Notes:   Need to add modifiers based on breast size, where larger breasts end up forcing the player to show more cleavage than those with smaller breasts. 
!!		Ex: 	4 reveals over half the skin on the torso area, 
!!				3 has a deep neckline that reaches the cleavage between the breasts, 
!!				2 is a cut high enough to be considered appropriate in most places (above the cleavage, or just below where it starts), 
!!				1 is a neckline that ends very near the neck or higher.
!! CloBra = 1 for on 2 for bare chest
!!		Ex:		2 Your chest is not covered at all by the outfit, if you wear a bra that will be your top
!!				1 the top is no more than a bra and wearing a bra underneath s pointless
!!Bottoms!!
!! CloSkirtShortness = 1-6 scale. Higher values are shorter.
!!		Notes: 	 Loose skirts sometimes rank higher than tight skirts of the same length, since they are more likely to flash others.
!!		Ex: 	6 is short enough to reveal pubic areas at all times 
!!				5 is ~20% thigh length or less (micro-skirt), and can reveal underwear frequently 
!!				4 is ~40% thigh length (miniskirt) and may show underwear if bending over 
!!				3 is up to ~60% thigh length (skirt) and normally never reveals underwear 
!!				2 is ending immediately above, at or below the knees (knee-length) 
!!				1 is ankle-length.
!! CloPantsShortness = 1-6 scale. Higher values are shorter.
!!		Notes: 	 Intentionally separate variable from skirt length to prevent wording confusion during events (e.g., lifting a dress or skirt versus pulling down pants or shorts)
!!		Ex: 	6 are high-cut shorts (legs end significantly above the crotch, usually reveals a large amount of the ass cheek)
!!				5 is mini shorts (legs do not reach below the crotch)
!!				4 are shorts (close to mid thigh)
!!				3 is knee-length shorts
!!				2 is calf length
!!				1 is ankle length
!! CloDress  =  1 for yes
!!		EX		1 This average quality dress. Unlike a skirt the whole thing must be removed
!! CloPanties = 1 for yes
!!		Ex:		1 The bottom of this outfit offers no more coverage than panties and it will replace your panties (if worn)
!! CloOnePiece = 1
!!		Notes:	 Flag for event writing; excludes skirted/dress outfits. Denotes that in order to have sex, the clothing must be pulled to the side, unlike shorts which can be pulled down, or skirts which may be lifted. 
!! CloBimbo = 1
!!		Notes:	 Flag for bimboification gain. All exceptionally slutty clothing (the kind almost never seen outside of porn, strip clubs, or on street hookers) should have this flag.
!!
!!	Figures for CloInhibit values, these are the maximums for each range. Currently all set to lowest figure in range but to add random element when time permits
!!0:	dress:1					pants:1		top:2			thickness:2
!!5:	dress:2					pants:3		top:2			thickness:3
!!10:	dress:3					pants:4		top:2			thickness:4
!!15:	dress:3					pants:4		top:3			thickness:4
!!20:	dress:4 				pants:4		top:3			thickness:4
!!25:	dress:4 and CloPanties	pants:5		top:4			thickness:5
!!30:	dress:5					pants:6		top: - CloBra	thickness:5
!!35:	dress:6					pants:6		top: - CloBra	thickness:6
!!40-60:	All but Exhibit
!!60+:	Can wear Exhibitionist clothing




CloQuality = 0
CloThinness = 0
CloTopCut = 0
CloBra = 0
CloPantsShortness = 0
CloSkirtShortness = 0
CloPanties = 0
CloDress = 0
CloOnePiece = 0
CloStyle = 0
CloStyle2 = 0
CloBimbo = 0
CloInhibit = 0

if $ARGS[0] = 'cheap':
	*P FUNC('$attributes_cheap', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'average':
	*P FUNC('$attributes_average', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'expensive':
	*P FUNC('$attributes_expensive', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'formal':
	*P FUNC('$attributes_formal', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'office':
	*P FUNC('$attributes_office', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'school':
	*P FUNC('$attributes_school', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'exercise':
	*P FUNC('$attributes_exercise', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'fetish':
	*P FUNC('$attributes_fetish', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'exhibit':
	*P FUNC('$attributes_exhibit', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'uniform':
	*P FUNC('$attributes_uniform', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'coat':
	*P FUNC('$attributes_coat', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'swimwear':
	*P FUNC('$attributes_swimwear', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'burlesque':
	*P FUNC('$attributes_burlesque', $ARGS[0], ARGS[1])
elseif $ARGS[0] = 'alternative':
	*P FUNC('$attributes_alternative', $ARGS[0], ARGS[1])
end

if $ARGS[0] ! 'swimwear' and $ARGS[0] ! 'coat' and $ARGS[0] ! 'exercise':
	if CloSkirtShortness = 6:
		CloInhibit = 35
	elseif CloSkirtShortness = 5 or CloPantsShortness = 6:
		if CloThinness = 6:
			CloInhibit = 35
		else
			CloInhibit = 30
		end
	elseif CloPanties = 1 or CloPantsShortness = 5:
		if CloThinness = 6:
			CloInhibit = 35
		elseif CloBra = 1:
			CloInhibit = 30
		else
			CloInhibit = 25
		end
	elseif CloSkirtShortness = 4:
		if CloThinness = 6:
			CloInhibit = 35
		elseif CloBra = 1:
			CloInhibit = 30
		elseif CloTopCut = 4 or CloThinness = 5:
			CloInhibit = 25
		else
			CloInhibit = 20
		end
	elseif CloSkirtShortness = 3:
		if CloThinness = 6:
			CloInhibit = 35
		elseif CloBra = 1:
			CloInhibit = 30
		elseif CloTopCut = 4 or CloThinness = 5:
			CloInhibit = 25
		elseif CloTopCut = 3:
			CloInhibit = 15
		else
			CloInhibit = 10
		end
	elseif CloSkirtShortness = 2 or CloPantsShortness = 3:
		if CloThinness = 6:
			CloInhibit = 35
		elseif CloBra = 1:
			CloInhibit = 30
		elseif CloTopCut = 4 or CloThinness = 5:
			CloInhibit = 25
		elseif CloTopCut = 3:
			CloInhibit = 15
		elseif CloThinness = 4:
			CloInhibit = 10
		else
			CloInhibit = 5
		end
	else
		if CloThinness = 6:
			CloInhibit = 35
		elseif CloBra = 1:
			CloInhibit = 30
		elseif CloTopCut = 4 or CloThinness = 5:
			CloInhibit = 25
		elseif CloTopCut = 3:
			CloInhibit = 15
		elseif CloThinness = 4:
			CloInhibit = 10
		elseif CloThinness = 3:
			CloInhibit = 5
		else
			CloInhibit = 0
		end
	end
end

--- clothing_attributes ---------------------------------

