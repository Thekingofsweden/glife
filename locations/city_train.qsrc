# city_train

if $ARGS[0] = 'start':
	$metka = 'start'
	$loc = 'city_train'
	$location_type = 'public_outdoors'
	$menu_loc = 'city_train'
	$menu_arg = 'start'
	menu_off = 0
	*clr & cla
	gs'stat'
	'<center><B>City center railway station</B></center>'
	if hour < 9 or hour > 19:
		'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_night.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_day.jpg"></center>'
	end
	'The main station for the city, it is always busy offering services to many destinations and the best place to park your car or hire a taxi.'

	if car > 0 and cardrive = 2:
		'In the parking lot is <a href="exec:GS ''carF'',''start''">your <<$car>></a>.'
	end

	act 'Go to the city center':minut += 5 & gt 'city_center'
	act 'Enter the station building': minut += 1 & gt 'city_train', 'inside'
end

if $ARGS[0] = 'inside':
	$metka = 'inside'
	$loc = 'city_train'
	$location_type = 'public_indoors'
	$menu_loc = 'city_train'
	$menu_arg = 'inside'
	menu_off = 0
	*clr & cla
	gs'stat'
	'<center><B>City center railway station</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/shared/trainstation/central_station_inside.jpg"></center>'
	'The grand old station is even more impressive on the inside and has a number of shops and bars as well as all the platforms and trains. '
	*nl
	act 'Leave the train station': minut += 1 & gt 'city_train', 'start'
	if Trainpass_day > daystart:
		'You have <<Trainpass_day - daystart>> days left on your train pass.'
		act 'Go to the platforms': gt 'city_train', 'platform'
	else
		act 'Go to the ticket office': gt 'city_train', 'tickets'
	end
end

if $ARGS[0] = 'tickets':
	$menu_loc = 'city_train'
	$menu_arg = 'tickets'
	menu_off = 0
	*clr & cla
	gs 'stat'
	'<center><h4>Ticket office</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/kassa.jpg" ></center>'

	act 'Leave the ticket office':minut += 1 & gt 'city_train', 'inside'
	
	act 'Buy a ticket':
		cla
		gs 'city_train', 'buy_ticket'
	end

	if money >= 1500 and Trainpass_day <= daystart:
		act 'Buy a weekly pass  (7 days - 1500 <b>₽</b>) ':
			cla
			menu_off = 1
			money -= 1500
			Trainpass_day = daystart + 7
			act 'Return': gt 'city_train', 'inside'
		end
	end

	if money >= 5500 and Trainpass_day <= daystart:
		act 'Buy a monthly pass (30day -  5500 <b>₽</b>)':
			cla
			menu_off = 1
			money -= 5500
			Trainpass_day = daystart + 30
			act 'Return': gt 'city_train', 'inside'
		end
	end	
end

if $ARGS[0] = 'buy_ticket':
	$menu_loc = 'city_train'
	$menu_arg = 'buy_ticket'
	menu_off = 0
	
	act 'Leave the ticket office': minut += 1 & gt 'city_train', 'inside'
	
	act 'Buy a ticket to the city industrial area (50 <b>₽</b>)':
		if money >= 50:
			money -= 50
			gt 'city_train', 'ind'
		else
			'You don''t have enough money for this ticket.'
		end
	end

	act 'Buy a ticket to the old platform by the communal village (75 <b>₽</b>)':
		if money >= 75:
			money -= 75
			gt 'city_train', 'communal'
		else
			'You don''t have enough money for this ticket.'
		end	
	end

	act 'Buy a ticket to the village of Gadukino (100 <b>₽</b>)':
		if money >= 100:
			money -= 100
			gt 'city_train', 'village'
		else
			'You don''t have enough money for this ticket.'	
		end
	end

	act 'Buy a ticket to Pavlovsk (150 <b>₽</b>)':
		if money >= 150:
			money -= 150
			gt 'city_train', 'pavlovsk'
		else
			'You don''t have enough money for this ticket.'		
		end
	end
end

if $ARGS[0] = 'platform':
	$location_type = 'public_outdoors'
	$menu_loc = 'city_train'
	$menu_arg = 'platform'
	menu_off = 0
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzalper.jpg" ></center>'
	'When you arrive at the platform, you can''t help but notice it bustling with activity. Lots of passengers are coming and going, all carrying shopping bags. It feels like it is always busy here, no matter at what time of the day it is.'

	act 'Return to the station hall':gt 'city_train', 'inside'
	
	act 'Take a train to the city industrial area': gt 'city_train', 'ind'

	act 'Take a train to the old platform by the communal village': gt 'city_train', 'communal'

	act 'Take a train to the village of Gadukino': gt 'city_train', 'village'

	act 'Take a train to Pavlovsk': gt 'city_train', 'pavlovsk'
end

if $ARGS[0] = 'ind':
	$loc = 'city_industrial_train'
	$metka = 'platform'
	menu_off = 1
	minut += 5
	gs 'stat'
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/indusplat.jpg" ></center>'
	'After 5 minutes, the train stops at the St. Petersburg city industrial area station.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'city_industrial_train', 'platform'
	end
end

if $ARGS[0] = 'communal':
	$loc = 'train'
	$metka = 'communal'
	menu_off = 1
	minut += 15
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/oldplat.jpg" ></center>'
	'After 15 minutes you arrive at an old railway platform near the communal village.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'communal'
	end
end

if $ARGS[0] = 'village':
	$loc = 'train'
	$metka = 'village'
	menu_off = 1
	minut += 25
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/gadplat.jpg" ></center>'
	'After 25 minutes, the train stops at the small station near the village of Gadukino.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'village'
	end
end

if $ARGS[0] = 'pavlovsk':
	$loc = 'pav_train_hall'
	$metka = 'platform'
	menu_off = 1
	minut += 40
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) < 30: gt 'train', 'Gopnik'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	if train_event = 0 and rand(1, 100) < 20: gt 'train', 'events'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/pavplat.jpg" ></center>'
	'After 40 minutes, you see the town of Pavlovsk approaching. Pavlovsk Castle is an impressive sight, even from a long distance. The train stops at its final destination, a platform in Pavlovsk station.'
	act 'Get off the train':
		if train_event = 0 and rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'pav_train_hall', 'platform'
	end
end

--- city_train ---------------------------------

