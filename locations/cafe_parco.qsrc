# cafe_parco

$location_type = 'public_indoors'

if $args[0]= 'start':
	$menu_loc = 'cafe_parco'
	$menu_arg = 'start'
	menu_off = 0
	'<center><h4><font color="maroon">Cafe "Del Parco"</font></h4></center>'
	'<center><img <<$set_imgh>> src="'+'images/locations/pavlovsk/park/cafe/caffe_del_parco'+iif(hour > 20 or hour < 7,'_night','')+'.jpg'+'"></center>'
	'The newly opened cafe "Del Parco", with its striking facade, there are lots of tables and a summer terrace. Opening hours between 14.00 to 21.00.'

	act 'Return to the square': gt 'gskver', 'start'
	if hour >= 14 and hour < 21: act 'Enter the cafe': gt 'cafe_parco', 'inner'
end

if $args[0]= 'inner':
	$menu_loc = 'cafe_parco'
	$menu_arg = 'inner'
	menu_off = 0
	$metka = 'inner'
	$loc = 'cafe_parco'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/park/cafe/caffe_del_parco_in.jpg"></center>'

	act 'Exit the cafe': gt 'gskver', 'start'

	act 'Order from the menu (0:05)':gs 'food_menu'
	if money >= 350:
		act 'Order take-out (0:20) (350 <b>â‚½</b>)':money -= 350 & gt 'food', 'f_food'
	end

	gs 'family'

	if (sunWeather = 0 or temper < 5) and week mod 2 = 0 and (hour >= 14 and hour < 20) and mey_vika_qw_day ! daystart:
		mey_vika_qw_day = daystart
		minut += rand(10,20)
		'You notice Vicky and Vanya sitting at a table.'

		if mey_vika['mey_vika_qw'] < 3:
			'You''re not really close with either of them, you don''t really know how to approach them.'

		elseif mey_vika['mey_vika_qw'] < 5:
			'While in passing of their table you greet them with a simple "Hi".'

			if npc_rel["A15"] < 20:
				'Vicky and Vanya only glance in your direction and turn away, having nothing more to say you quickly moved on...'
			else
				'Vicky and Vanya answer "Hi" back at the same time, you just wanted to greet them so you continued on your way.'
			end

		elseif mey_vika['mey_vika_qw'] >= 5:
			if npc_rel["A15"] < 50:
				'You approach the couple and greet them, but they don''t pay any attention to you.'
			else
				'You approach the couple and greet them, right away Vicky starts gossiping, as always, Vanya, mostly joked about the things you two were talking about. You chatted for a few minutes...'
			end

			if npc_rel["A15"] >= 60:
				if mey_vika['mey_vika_qw'] = 10:
					cla
					'Vicky and Vanya invite you to join them.'
					if money < 300:
						act 'Refuse':
							cla
							'You''re pretty much broke, and you don''t want to be impudent by asking them to pay for you, so you decide to thank them for the invite but that you have a thing elsewhere that you need to attend...'

							act 'Leave': gt 'park'
						end
					end
					act 'Agree':
						mey_vika['mey_vika_qw'] = 15
						gt 'mey_vika_events', 'cafe_qw'
					end
				elseif mey_vika['mey_vika_qw'] >= 7:
					'As you''re getting up from the table, you notice Vicky whispering something to Vanya. You notice Vanya''s hand stroking and squeezing Vicky''s butt, as they stare at you with lust in their eyes...'
				end
			end
		end
	else
		'You don''t see anyone familiar here.'
	end
end

--- cafe_parco ---------------------------------

