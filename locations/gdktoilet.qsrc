# gdktoilet

$location_type = 'public_indoors'
$menu_loc = 'gdktoilet'
$loc = 'gdktoilet'
gs 'themes', 'indoors'

if $ARGS[0] = 'start':
	$menu_arg = 'start'
	$metka = 'start'
	killvar '$locclass'
	menu_off = 0
	gs 'stat'
	*clr & cla
	minut += 1
	ghnow = 0

	'<center><b><font color="maroon">Community Center - Restrooms</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/bathroom/bathroom_entrance.jpg"></center>'

	'There is a <a href="exec: gt ''gdktoilet'', ''watercooler''">drinking fountain</a> near the doors to the bathrooms.'
	
	act 'Go outside': gs 'arousal', 'end' & gt 'pav_commcenter'
	
	if hour >= 8 and hour <= 23:
		gs 'willpower', 'exhib', 'self'
		if will_cost <= pcs_willpwr:
			act 'Enter the men''s restroom (<<will_cost>> Willpower)':
				gs 'willpower', 'exhib', 'self'
				gs 'willpower', 'pay', 'self'
				gs 'stat'
				gt 'gdktoilet_mens', 'mens_entrance_events'
			end
		else
			act 'Enter the men''s restroom (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
		act 'Enter the women''s restroom': gt 'gdktoilet_womens', 'womens'
	end

	if hour >=20 and hour <=23 and week >= 5 and week < 7:
		if music_on = 1:
			$track_loop = 'sound/komb.mp3'
			volume = 50
			music_loop = 1
			pl '<a href="exec: music_on = 0 & gt ''gdktoilet'', ''start''">Turn off the music</a>'
		else
			music_loop = 0
			close all
			pl '<a href="exec: music_on = 1 & gt ''gdktoilet'', ''start''">Play music</a>'
		end
		if gdkincum = daystart and (cumloc[6] = 0 and cumloc[7] = 0) and cumloc[11] = 0:
			gdkincum = 0
		end
		if gdkincum ! daystart:
			if gdkin_in = daystart:
				act 'Return to the dance':gt 'gdkin'
			elseif money >= 25:
				act 'Go to the dance (25 <b>â‚½</b>)':
					money -= 25
					minut += 5
					gt 'gdkin'
				end
			end
		else
			*nl
			if (cumloc[6] = 1 or cumloc[7] = 1) and cumloc[11] = 1:
				'You should clean the cum off your face and clothes before heading back to the dance.'
			elseif cumloc[11] = 1:
				'You should clean the cum off your face before heading back to the dance.'
			elseif cumloc[6] = 1 or cumloc[7] = 1:
				'You should clean the cum off your clothes before heading back to the dance.'
			end
		end
	else
		music_loop = 0
		CLOSE ALL
	end

	if hour >= 8 and hour <= 18: act 'Go to the library': gt 'pav_library'
	if hour >= 12 and hour <= 20: act 'Go upstairs to the hobby clubs': gt 'pav_commclubs'
	if hour >= 14 and hour <= 20: act 'Go to the gym': gt 'gdksport', 'start'
end

if $ARGS[0] = 'watercooler':
	*clr & cla
	minut += 1
	$menu_arg = 'watercooler'
	$metka = 'watercooler'
	gs 'stat'

	'<center><b><font color="maroon">Community Center - Restrooms</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/bathroom/waterfountain.jpg"></center>'
	
	act 'Leave': gt 'gdktoilet', 'start'
	act 'Take a drink':
		*clr & cla
		frost = 1
		if alko > 0: alko -= 1
		minut += 2
		pcs_health += 10
		pcs_energy += 4
		if pcs_hydra >= 100:
			pcs_hydra += 25
		else
			pcs_hydra += 50
		end
		cumspclnt = 2
		gs 'cum_cleanup'
		gs 'stat'
		
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/bathroom/waterfountain.jpg"></center>'		
		'You drink some water.'
		act 'Finish': gt 'gdktoilet', 'start'
	end
end


--- gdktoilet ---------------------------------

