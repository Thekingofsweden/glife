# lakecafe

if $ARGS[0] = 'main':
	$metka = 'main'
	$loc = 'lakecafe'
	$menu_loc = 'lakecafe'
	$menu_arg = 'main'
	menu_off = 0
	$location_type = 'public_indoors'
	cla
	*clr
	barrnd = rand(1, 3)
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><B><font color=maroon>The Lake Cafe</font></B></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakecafe.jpg"></center>'
	'An old-fashioned BBQ café/diner near the lake, known for offering relatively cheap but tasty meals. Russia is littered with these.'
	'Opening hours: 14:00 to 04:00'
	*nl

	act 'Leave the cafe':minut += 2 & gt 'nord'

	if hour >= 14 or hour < 4:
		if npc_QW['A89'] = 0:
			if barrnd = 1:'Behind the bar you see a smiling <a href="exec:gt ''lakecafe'', ''barmensha''">barmaid</a>, who is currently polishing some glasses.'
			if barrnd = 2:'Inside the diner you see a smiling <a href="exec:gt ''lakecafe'', ''barmensha''">barmaid</a>, who is currently picking up some dirty dishes and cleaning a table.'
			if barrnd = 3:'Behind the bar you see a smiling <a href="exec:gt ''lakecafe'', ''barmensha''">barmaid</a>, who is currently making some mixed drinks skilfully.'
		elseif npc_QW['A89'] = 1:
			if barrnd = 1:'Behind the bar you see <a href="exec:gt ''lakecafe'', ''barmensha''">Eugene</a>, with that perpetual smile on her face. She''s currently polishing some glasses, it must be a slow period.'
			if barrnd = 2:'Inside the diner you see <a href="exec:gt ''lakecafe'', ''barmensha''">Eugene</a>, with that perpetual smile on her face. She''s collecting dirty dishes from tables.'
			if barrnd = 3:'Behind the bar you see <a href="exec:gt ''lakecafe'', ''barmensha''">Eugene</a>, with that perpetual smile on her face. She''s skilfully mixing some drinks for customers, making a show out of it.'
		end


		if week = 1 and hour = 22:
			LCWorkRand = rand(1,6)

			if LCWorkRand = 1:
				randguycafe = rand(3, 6)

				if LCwork = 0:'A group of men is seated at one of the tables. They''re talking loudly and laughing, while having drinks.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start1''"><<randguycafe>> men</a> are seated at one of the tables. They''re talking loudly and laughing, while having drinks.'
			elseif LCWorkRand = 2:
				if LCwork = 0:'An older man is sitting at one of the tables, enjoying his meal.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start2''">An older man</a> is sitting at one of the tables, enjoying his meal.'
			elseif LCWorkRand = 3:
				if LCwork = 0:'Two guys are sitting at one of the tables in a corner. They''ve clearly had a few drinks already.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start3''">Two men</a> are sitting at one of the tables. They''ve clearly had a few drinks already.'
			elseif LCWorkRand = 4:
				if LCwork = 0:'A fairly young couple is seated at one of the tables. They''re in a lively discussion. The girl seems a bit embarrassed about something, while the guy exudes confidence.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start4''">A fairly young couple</a> is seated at one of the tables. They''re in a lively discussion. The girl seems a bit embarrassed about something, while the guy exudes confidence.'
			elseif LCWorkRand = 5:
				if LCwork = 0:'A woman sits by herself at one of the tables, enjoying a nice meal.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start5''">A woman</a> sits by herself at one of the tables, enjoying a nice meal.'
			elseif LCWorkRand = 6:
				if LCwork = 0:'A fat man is sitting at one of the tables. He''s loudly talking on his phone, receiving a few disgruntled looks from other visitors.'
				if LCwork = 1:'<a href="exec:gt ''LCwork'', ''start6''">A fat man</a> is sitting at one of the tables. He''s loudly talking on his phone, receiving a few disgruntled looks from other visitors.'
			end
		end

		if hour => 23:'In a fairly secluded corner of the diner, several <a href="exec:gt ''lakecafe'',''bandits''">shady looking men</a> are sitting around a small table. They talk in hushed voices amongst each another, and look like they might very well be criminals.'

		'The diner has a single <a href="exec:gt ''laketoilet'', ''main''">toilet</a> available, for both males and females.'

		act 'Order from the menu (0:05)':gs 'food_menu'
		if money >= 350:
			act 'Order take-out (0:20) (350 <b>₽</b>)':money -= 350 & gt 'food', 'f_food'
		end
	end

	if LCwork2 >= 1 and LCwork2day ! daystart and hour = 18:
		act 'Help out the kitchen (1:30)':gt 'LCwork', 'Kitchen'
	end
end

if $ARGS[0] = 'barmensha':
	menu_off = 1
	$location_type = 'event'
	cls
	gs 'stat'
	if npc_QW['A89'] > 0:
		'<center><B><font color=maroon>Barmaid Eugene</font></B></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
		if npc_rel['A89'] < 20:
			'You have a bad relationship with Eugene.'
		elseif npc_rel['A89'] < 40:
			'You have cold relations with Eugene.'
		elseif npc_rel['A89'] < 60:
			'You have a normal relationship with Eugene.'
		elseif npc_rel['A89'] < 80:
			'You have a good relationship with Eugene.'
		else
			'You have an excellent relationship with Eugene.'
		end
	else
		'<center><B><font color=maroon>Barmaid</font></B></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
	end


	if npc_QW['A89'] = 0:
		'The barmaid gives you a warm smile, and looks you straight in the eyes as she says: "Hi there! Can I get you anything?"'
	elseif npc_QW['A89'] = 1:
		'Eugene smiles as she sees you approach the bar, and says cheerfully: "Hi, <<$pcs_nickname>>! What''s up?"'
	end

	act 'Leave Eugene so she can work':gt 'lakecafe', 'main'

	if hour >= 14 or hour < 4:
		if npc_QW['A89'] = 0:
			act 'Chat with the barmaid (0:30)':
				cla
				*clr
				npc_QW['A89'] = 1
				npc_rel['A89'] = 10
				minut += 30
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
				'You spend a half hour chatting with the barmaid. She''s very open, and you learn a lot about her.'
				'Her name is Eugene, and she works here together with her sister, Angela. Angela is the cook, and Eugene is the barmaid in the diner. The two of them work here all days of the week, and wouldn''t have it any other way!'

				act 'Continue':gt 'lakecafe', 'barmensha'
			end
		end

		if npc_QW['A89'] => 1:
			act 'Chat with Eugene (0:30)':
				cla
				*clr

				if npc_rel['A89'] < 60:npc_rel['A89'] += 3

				minut += 30
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
				if npc_rel['A89'] < 0:
					'Eugene glares at you. "I have nothing to say to you."'
					npc_rel['A89'] = -5
				elseif npc_rel['A89'] < 30 and LCEugenefirst = 0:
					'You spend a half hour chatting with Eugene, getting to know her a little bit better. You chat about all sorts of things; your lives, work, and current events.'
				elseif lakepater[0] > 0:
					'You spend a half hour discussing the family with Eugene.'
				else
					'You spend a half hour chatting with Eugene. You chat about all sorts of things; your lives, work, and current events.'
				end
				act 'Continue':gt 'lakecafe', 'barmensha'
			end
		end


		if npc_rel['A89'] >= 60 and pcs_horny > 50:
			act 'Flirt with Eugene':
				cla
				*clr
				if npc_love['A89'] >= 0 and npc_love['A89'] < 5: npc_love['A89'] = 0
				minut += 10
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
				'While you chat with Eugene you flirt with her to your heart''s delight, tossing in as many double innuendos as you can.'
				'After a few minutes, she smiles wolfishly at you and says: "You want me, don''t you?"'
				'When you nod, she continues: "How about you follow me to a back room, and we have some fun?"'
				'Wow, that was easy!'
				'You nod again and follow Eugene, feeling your heart pound in your chest while you stare at her ass.'

				act 'Follow her into a back room':
					if LCEugenefirst = 0:gt 'LCporn', 'firsttime'
					if LCEugenefirst = 1:gt 'LCporn', 'sex'
				end
			end
		end

		if npc_rel['A89'] >= 35:
			act 'Ask about work':
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'

				if LCworkwork2 < 1:
					'You ask Eugene: "I''m looking for a job. Would you happen to know about anything?"'
					'Eugene ponders for a second and offers: "Actually, my sister always complains about how busy she is in the kitchen, during the early evening hours! If you come between 18:00 and 19:00, you could probably help her out in the kitchen for about an hour and a half. We''d be able to pay you 100 <b>₽</b> for your troubles!"'
					'You smile at her and thank her, saying you''ll keep it in mind.'
					LCworkwork2 = 1
					LCwork2 = 1

					act 'Continue':
						minut += 4
						gt 'lakecafe', 'barmensha'
					end
				else
					'When you ask Eugene about a job, she laughs: "Did you forget, <<$pcs_nickname>>? I already told you that my sister could use some help in the kitchen, during the early evening hours! If you come between 18:00 and 19:00, you can help her out for about an hour and a half! We can pay you 100 <b>₽</b> for your time."'

					act 'Thank her for the reminder':
						minut += 4
						gt 'lakecafe', 'barmensha'
					end
				end
			end
		end

		if npc_rel['A89'] >= 80 and LCworkwork < 2:
			act 'Ask about the special work':
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/lakebarmensha.jpg"></center>'
				if LCworkwork = 0:
					'During a slow moment, you ask Eugene: "Say, I''ve overheard some of the customers talking fondly about a ''special'' service this diner has. What''s that all about?"'
					if npc_love['A89'] > 80:
						'Eugene blushes and says: "That''s something some of the girls do here. It would hurt me to see you doing that, love"'
					elseif npc_love['A89'] > 60:
						'Eugene blushes and says: "That''s something some of the girls do here. I don''t think you should do it, <<$pcs_nickname>>"'
						act 'Press for more information':
							cla
							'You press for more information.'
							'Eugene seems to be hurt by your insistance to find out more. She begs you not to ask any more.'
							act 'Refuse to relent.':
								cla
								npc_love['A89'] -= 5
								npc_rel['A89'] -= 20
								'You refuse to relent.'
								'Eugene sighs: "All our regulars know that we ''arrange meetings'' every Monday between 22:00 and 23:00. If you''re here at that time and want to earn some money, introduce yourself to the person or group you want to service and say: ''I''ll be your waitress tonight.'' They''ll know what it means. Please don''t do it, <<$pcs_nickname>>."'
								'You see tears forming in her eyes.'
								gs 'willpower', 'misc', 'self', 'easy'
								if will_cost <= pcs_willpwr:
									act 'Say you won''t do it (<<will_cost>> Willpower)':
										cla
										gs 'willpower', 'pay', 'self'
										gs 'stat'
										'Eugene seems relieved as she looks back at you.'
										'"Thank you, <<$pcs_nickname>>. I don''t know what I would do if you were doing that..."'
										act 'Continue':
											cla
											LCworkwork = 2
											LCwork = 0
											minut += 4
											gt 'lakecafe', 'barmensha'
										end
									end
								else
									act 'Say you won''t do it (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
								end	
								
								act 'Walk away':
									cla
									npc_love['A89'] -= 30
									npc_rel['A89'] -= 40
									LCworkwork = 1
									LCwork = 1
									minut += 4
									gt 'lakecafe', 'main'
								end
							end
						end
					elseif npc_love['A89'] > 30:
						'Eugene blushes and says: "That''s something some of the girls do here. It''s not something I would like to see you doing, <<$pcs_nickname>>"'
						act 'Press for more information':
							cla
							'You press Eugene for more information.'
							'Eugene seems to be hurt by your insistance to find out more, but she reluctantly relents.'
							'"All our regulars know that we ''arrange meetings'' every Monday between 22:00 and 23:00. If you''re here at that time and want to earn some money, introduce yourself to the person or group you want to service and say: ''I''ll be your waitress tonight.'' They''ll know what it means. From what the girls told me, they get paid between 500 and 1,000 <b>₽</b>."'
							'Before you leave, she adds: "Please don''t do it. I''m starting to care for you and..."'
							'She looks away from you.'
							gs 'willpower', 'misc', 'self', 'easy'
							if will_cost <= pcs_willpwr:
								act 'Say you won''t do it (<<will_cost>> Willpower)':
									cla
									gs 'willpower', 'pay', 'self'
									gs 'stat'
									'Eugene seems relieved as she looks back at you.'
									'"Thank you, <<$pcs_nickname>>. I don''t know what I would do if you were doing that..."'
									act 'Continue':
										cla
										LCworkwork = 2
										LCwork = 0
										minut += 4
										gt 'lakecafe', 'barmensha'
									end
								end
							else
								act 'Say you won''t do it (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
							end	
							
							act 'Walk away':
								cla
								npc_love['A89'] -= 5
								npc_rel['A89'] -= 20
								LCworkwork = 1
								LCwork = 1
								minut += 4
								gt 'lakecafe', 'main'
							end
						end
					else
						if LCknowslut = 0:
							'Eugene ponders for a moment, but then decides she can trust you and says: "We... we do offer special services, once a week. It''s every Monday, at 22:00. I''m not sure it''s something you would be interested in."'
							'When you seem confused, she confesses: "It''s... it''s borderline prostitution, really. Some of the girls working here earn some extra money on the side, by offering their services to our customers after work. It happened sporadically at first, but then it became so popular that we organize a weekly event now. Customers can hook up with our waitresses, and our waitresses can earn some extra cash if they want. Everybody wins... right?"'

							act 'Ask for more information':
								cla
								'Eugene notices that spark in your eye, and laughs: "I see you might be interested after all!"'
								'You play it cool for now: "Maybe. But just in case I were, how does it work?"'
								'She gives you a kind smile: "I trust you, so... okay. I''ll let you do it if you want. Like I said, all our regulars know that we ''arrange meetings'' every Monday between 22:00 and 23:00. If you''re here at that time and want to earn some money, introduce yourself to the person or group you want to service and say: ''I''ll be your waitress tonight.'' They''ll know what it means. From what the girls told me, they get paid between 500 and 1,000 <b>₽</b>."'
								'Before you leave, she adds: "Keep in mind that the diner is just a meeting area; whatever happens when you leave with someone is between you and them! Don''t embarrass us though, we''d hate to lose regular customers, no matter how perverted they might be!" She added the last bit with a laugh and a wink.'

								act 'Say you''ll think about it':
									cla
									LCworkwork = 1
									LCwork = 1
									minut += 4
									gt 'lakecafe', 'barmensha'
								end
							end
						else
							'Eugene smiles at you. "This is something right up your alley"'
							'All our regulars know that we ''arrange meetings'' every Monday between 22:00 and 23:00. If you''re here at that time and want to earn some money, introduce yourself to the person or group you want to service and say: ''I''ll be your waitress tonight.'' They''ll know what it means. From what the girls told me, they get paid between 500 and 2,000 <b>₽</b>."'
							'Before you leave, she adds: "Keep in mind that the diner is just a meeting area; whatever happens when you leave with someone is between you and them! Don''t embarrass us though, we''d hate to lose regular customers, no matter how perverted they might be!" She added the last bit with a laugh and a wink.'

							act 'Thank her for the information':
								cla
								LCworkwork = 1
								LCwork = 1
								minut += 4
								gt 'lakecafe', 'barmensha'
							end
						end
					end
				elseif LCworkwork = 1:
					if LCknowslut = 1 and npc_love['A89'] > 30:
						'Eugene glares at you.'
						'"Did you come to brag about how much of a slut you are, <<$pcs_firstname>>? If you want to prostitute yourself out, come monday night between 2200 and 2300 and show the men here what kind of a person you are. Just don''t lose us any customers."'
						'She turns around and walks away from you.'
						npc_love['A89'] = -5
						act 'Continue':
							minut += 3
							gt 'lakecafe', 'barmensha'
						end
					else
						'"So you''re still interested in that, huh?" Eugene smirks, when you mention the special work.'
						'She reminds you: "Like I said, our regulars know that we ''arrange meetings'' every Monday between 22:00 and 23:00. If you''re here at that time and want to earn some money using your body, introduce yourself to the person or group you want to service and say: ''I''ll be your waitress tonight.'' They''ll know what it means. From what the girls told me, they get paid between 500 and 2,000 <b>₽</b>."'
						act 'Thank her for the reminder':
							minut += 3
							gt 'lakecafe', 'barmensha'
						end
					end
				end
			end
		end
	end

	if npc_horny['A89'] > 50:
		'Eugene smiles at you, with a glimmer in her eye.'
		if LCEugenefirst > 0:
			'You can see her clothing slightly bulging, making you think her penis is erect.'
			if LCknowslut > 0:
				cla
				'She grabs your hand and starts to lead you to the back room.'
				act 'resist':
					'"No!" You say to her.'
					if npc_love['A89'] < -5:
						'"You''ll service me, or you''ll get out." She hisses back in your ear.'
					else
						'She seems disappointed as she lets go of your hand.'
						'"We both know what kind of girl you are, <<$pcs_nickname>>. Don''t pretend you aren''t."'
					end
				end

				act 'Go with her.':gt 'LCporn', 'sex'
			else
				'"I seem to have a growing problem, <<$pcs_nickname>>. Could you help me out?"'
				'When you nod, she continues: "How about you follow me to a back room, and we have some fun?"'
				act 'Follow her into a back room':
					gt 'LCporn', 'sex'
					'Eugene guides you out to the back room, pinching your butt along the way.'
					npc_horny['A89'] = 49
					gt 'LCporn', 'sex'
				end
			end
		end
	end
end

if $ARGS[0] = 'bandits':
	menu_off = 1
	cls
	gs'stat'
	'<center><B><font color=maroon>Criminals</font></B></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/bbq/bandits.jpg"></center>'
	'A group of tough men, clearly very different from the other visitors this diner usually gets. Their dark clothes and threatening demeanor leave no doubt in your mind: these men are criminals. Fairly well organized, and probably dangerous criminals.'
	'You overheard that they will loan people money, but at abysmal terms. They demand 20% interest, and give you 7 days to repay the debt. Several girls who were unable to repay their debts have disappeared.'
	''
	if workDolg > 0:
		'One of them notices you and says: "Hey, you! You know you owe us <B><<workDolg>></B> <b>₽</b>? We want it back, and soon!"'
	else
		'"Are you lost, girl? Get lost!", one of them grunts menacingly.'
	end

	act '<B>Leave them alone</B>':gt'lakecafe', 'main'

	act 'Ask to borrow money':
		intinp = input ("How much money do you want to borrow?")
		if intinp <= 0:
			'"You want to borrow... no money? Quit wasting our time!" one of them shouts, rather annoyed at your inability to choose an amount.'
		elseif intinp > 2000000:
			'"Do we look like the National Bank, girl? Don''t be ridiculous!" one of them scoffs. They can''t lend you that much!'
		elseif intinp > 0 and intinp <= 2000000:
			cla
			money += intinp
			workDolg += intinp + (intinp/100)*20
			workDolgDay = 7
			'One of them extends his hand and says: "Passport."'
			'With your passport in his hand, he makes a few quick phone calls. He writes a bunch of stuff down, and then gives you your passport back, plus a wad of cash! As if it''s nothing!'
			'"Listen up, <<$pcs_nickname>> <<$pcs_lastname>>," he grunts. "You have <<workDolgDay>> days to pay back the <<intinp>> <b>₽</b>, plus <<(intinp/100)*20>> <b>₽</b> in interest."'
			'When you nod, he grabs your wrist roughly and says in a menacing voice: "I''m warning you, do not screw us over! I know everything about you, and I will get my money back! One way or the other."'
			act '<B>Promise you''ll pay, and leave</B>':gt'lakecafe', 'main'
		end
	end
	if workDolg > 0:
		cla
		act '<B>Move away</B>':gt'lakecafe', 'main'
		act 'Promise you''ll pay him on time':
			'You tell him: "Relax, I''ll pay you back on time! I swear!"'
			'"Talk is cheap, <<$pcs_nickname>> <<$pcs_lastname>>. You have <<workDolgDay>> days, don''t forget!" he shrugs, and turns back to his associates.'
		end
	end
	if workDolg > 0 and money >= workDolg:
		act 'Pay back your debt (<<workDolg>> <b>₽</b>)':
			cla
			minut += 5
			workDisk = 0
			money -= workDolg
			workDolg = 0
			gs'stat'

			'You pay back the money you owe the criminals. The man who gave you the money seems a tad surprised but quickly pockets the money.'
			'"Excellent! I knew I could trust you, pleasure doing business!" he grins, even though it''s fairly obvious he''s lying through his teeth.'

			act '<B>Leave them alone</B>':gt'lakecafe', 'main'
		end
	end

	if workDolgDay <= 0 and workDolg > 0:
		cls
		gs'stat'

		workDisk = 0

		'<center><img <<$set_imgh>> src="images/locations/city/residential/street/bug.jpg"></center>'
		'When two very broad men approach you, you can''t help but notice the distinct outline of pistols underneath their clothes.'
		'They look at you with disdain, and one of them says: "You. It''s time you repay your debt."'

		if money >= workDolg:
			money -= workDolg
			workDolg = 0

			'You give the man the amount of money you owe, and he counts it meticulously. Then he nods to his colleague: "It''s all there."'
			'The other guy already had his gun in his hand and puts it away reluctantly. He says in a menacing voice: "I guess you''re off the hook, for now, consider yourself lucky!"'

			act 'Watch them leave':gt'lakecafe', 'main'
		exit

		else
			if (money + stolmoney + karta) >= workDolg:
				money += stolmoney+karta
				stolmoney = 0
				karta = 0
				money -= workDolg
				workDolg = 0

				'You don''t have the full amount on you, but you have enough at home! When you tell them that, one of the drones grunts: "Oh really? Very original! We''re coming with! I''m not letting you out of my sight for even one second, until your debt is paid!"'
				'The men follow you to your place, and wait impatiently while you collect the money you owe them. When you produce the full amount, they actually seem a bit surprised.'
				'"Huh, guess you weren''t lying after all," one says, after counting it twice. "Consider your debt paid, miss. Pleasure doing business."'

				act 'Close the door behind them':gt'korr'
				exit
			else
				money += stolmoney + karta
				stolmoney = 0
				karta = 0
				workDolg -= money
				money = 0
				workDolg += 10000

				'"I... I don''t have the money, I''m sorry!" you tell the guys, who roughly grab you by the shoulders the moment you confess.'
				'"Oh, you will be sorry!" one laughs at you, not even worried. They walk to your house with you between them, never even asking for directions. Apparently they knew where you live. Hiding would''ve been pointless.'
				'One brute searches your house, piling up all the money you do have while the other keeps you at gunpoint.'
				'"Still not enough, huh." the man holding you the gun says matter-of-factly. "So, here''s what''s gonna happen. Since you can''t pay your debt and we have to go through all this trouble of getting it back, consider your debt increased by another 10,000 <b>₽</b>. Don''t you worry your pretty little head, though! We''re going to give you an alternative way to pay it off."'
				'"Hah! That pretty face will come in handy there!" the other laughs.'
				'Without explaining further, they drag you to a car outside your apartment and throw you into the back seat. About a half hour later, the car pulls over and one of the drones tells you to get out. You''re at a sauna building in the city.'

				act 'Get out of the car':
					cls
					minut += 30
					gs'stat'

					'After one of the criminals made sure the coast is clear, the two of them pull you out of the car and escort you to a room inside the sauna building. One leaves the room, and the other explains: "Until you pay off your debts, we own you! You will live here and work here, until you pay off your debts. Any expenses you incur while you are here will be added to your debt. Understood?"'
					'Silence fills the room as he waits for you to acknowledge what he just said. Mortified about what might happen if you don''t, you nod. He decides to paint the picture anyway: "If you try to escape or refuse to work, we''ll put a bullet in your head, chop you up and bury you in the woods in pieces! And that would be a waste, with a pretty face such as yours."'
					'He pinches your cheeks and then slaps your face; not hard enough to be painful, but it certainly drives the message home. You can tell he''s dead serious.'
					'After looking you in the eyes for a few more seconds to make sure you get the message, he turns around and leaves. You are all alone now.'

					act 'Look around':gt 'sauna', 'saunaroom'
				end
			end
		end

		exit
	end
end

--- lakecafe ---------------------------------

