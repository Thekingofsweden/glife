# sitrPar

if $ARGS[0] = '':
	CLOSE ALL
	act 'Leave the room':gt 'korrPar'

	$metka = ''
	$loc = 'sitrPar'
	$menu_loc = 'sitrPar'
	$menu_arg = ''
	menu_off = 0
	$location_type = 'private'
	$locclass = 'livingr'
	clr
	minut += 1
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Living Room</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/sitrpar.jpg"></center>'
	''
	'The living room has a <a href="exec:gt ''TV'',''pav''">TV</a>, which is near the window. In front of the TV is a <a href="exec:gt ''family'',''sofa''">sofa bed</a>, on which your brother sleeps at night. A large tapestry covers one wall, while the other wall has several shelves on it, stocked with books and other things.'
	*nl
	gs 'family'
	gs 'family_schedule'
	*nl
	gs 'subkid'

	if month = 12 and day >= 20:'A Christmas tree stands next to the TV, decorated with tinsel garlands and other ornaments.'
	if month = 12 and day = 31 and hour >= 20:
		act 'Celebrate the New Year with your family.':
			cls
			npc_rel['A29'] += 10
			npc_rel['A28'] += 10
			npc_rel['A33'] += 10
			npc_rel['A34'] += 10
			money += 5000
			hour = 23
			minut = 0
			minut += 120
			pcs_sleep = 0
			pcs_mood += 100
			wipo += 1000
			gs 'food', 'l_meal'
			gs 'stat'
			'You sit at the festive table laid in front of the TV and celebrate the New Year with your family, the TV providing some background noise. At the table people strike up a conversation and common problems are forgotten for a while, everyone is having fun talking to each other. At midnight, everyone wishes one another a happy New Year and enjoys the champagne and fireworks outside. Your <<$npc_nickname[''A29'']>> gives you an envelope with a greeting card and 5000 <b>â‚½</b>. For a while you continue to watch TV, enjoying the champagne and company of your family, but after a while everyone is kind of tired and one by one they go to bed.'

			act 'Leave':gt $loc, $metka
		end
	elseif month = 1 and day = 1:
		act 'Eat some leftovers at the festive table.':
			cla
			*clr
			frost = 0
			minut += 15
			pcs_health += 10
			pcs_mood += 20
			fat += 2
			pcs_energy += 20
			pcs_hydra += 20
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0

			gs 'stat'
			'<center><img <<$set_imgh>> src="images/shared/food/leftovers.jpg"></center>'
			'You have finished the leftovers remaining on the table from the holidays.'

		act 'Get up from the table':gt $loc, $metka

		end
	elseif momloc = 24:
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/uborka1.jpg"></center>'
		if kanikuli = 0 and StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and week < 6:
			if $clothingworntype = 'school':
				'As you walk into the living room you see your mother sweeping the floor. She looks up at and gives you a disappointed look. "What are you still doing home? Go to school, now!" With that she practically pushes you out of the apartment and closes the door. As she does she adds ones more. "Go to school."' 
				act 'Continue': gt'pod_ezd','etaj_2'
			else
				if schtype = 0:
					$schbrand = 'school'
					schtype = 6
				end
				if schoolH[schtype] = 0:
					$schbrand = 'school'
					schtype = 6
				end
				gs 'clothing', 'wear', $schbrand, schtype
				'As you walk into the living room you see your mother sweeping the floor. She looks up at and gives you a disappointed look. "What are you still doing home? Why are you not wearing your unifrom? Go get changed and go to school, now!" She follows you back to your room and watches as you change into your uniform, after that she practically pushes you out of the apartment and closes the door. As she does she adds ones more. "Go to school."' 
				act 'Continue':  gt'pod_ezd','etaj_2'
			end
		else
			'As you walk into the living room you see your mother sweeping the floor. She hasn''t noticed you, she is intent on cleaning.'
			act 'leave': gt 'korrPar'
		end
		exit
	elseif momloc = 19:
		!! Watches TV with Vladimir
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/momvladtv.jpg"></center>'
		'As you walk into the living room you see your <<$npc_nickname[''A29'']>> and Vladimir sitting on the couch watching tv, while Kolka sits in the armchair watching tv.'
		act 'leave':gt 'korrPar'
		
		act 'Join them':
			cla & *clr
			pcs_mood += 10
			minut += 30
			npc_rel['A28'] += 1
			npc_rel['A29'] += 1
			npc_rel['A34'] += 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/familytv.jpg"></center>'
			'You move over to join them and your <<$npc_nickname[''A29'']>> scoots away from Vladmir and pat the spot between her and him. You take a seat between them, Vlad barely even glances as you with a brief smile before he goes back to watching tv. Kolka never even looks up, your <<$npc_nickname[''A29'']>> pats you on the shoulder before returning her attention to the tv, apparently happy the family is spending time together.'
			act 'leave':gt 'korrPar'
			
			act 'Keep watching':gt 'sitrPar', 'TVmom'
			act 'Finish watching':gt 'sitrPar', 'TVend'
		end		
	end

	if ocpd_room = 0 and breastpump = 1 and bp_unbox <= 0:
		'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Unbox</a> your breast pump'
	elseif kid >= 1 and breastpump = 1 and bp_unbox <= 0:
		'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Unbox</a> your breast pump'
	elseif ocpd_room = 0 and breastpump = 1 and bp_unbox = 1 and pcs_inbition > 40:
		'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Try out</a> your breast pump'
	elseif kid >= 1 and breastpump = 1 and bp_unbox = 1 and pcs_inbition > 30:
		'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Try out</a> your breast pump'
	end
end


if $ARGS[0] = 'TVmom':
	cla & *clr
	pcs_mood += 10
	minut += 30
	gs 'family_schedule'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/familytv.jpg"></center>'
	'You continue to watch tv with the family, everyone seems very engrossed in the program and almost never says much. Though Kolka occasional makes a crack about what is happening, often getting shushed by Vlad or <<$npc_nickname[''A29'']>>.'
	if momloc = 19: act'Keep watching TV with your family': gt 'sitrPar', 'TVmom'
	act 'Finish watching':gt 'sitrPar', 'TVend'
end

if $ARGS[0] = 'TVend':
	cla & *clr
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/familytv.jpg"></center>'
	'Once the show is over Vladimir picks up the remote and turns the tv off, he gets up. Then he stretches, before he turns and walks out of the room, likely heading to bed. <<$npc_nickname[''A29'']>> gets up and says to you. "You should go to bed or at least go to your room so your brother can get his rest." She then turns to Kolka and says to him. "Don''t stay up, go to bed." With that she also leaves, after making sure you leave as well. As you walk out, you see Kolka putting his pillow and sheet on the couch.'
	act 'leave':gt 'korrPar'
end

--- sitrPar ---------------------------------

