# krupo
$metka = $ARGS[0]
$loc = $CURLOC
$location_type = 'event'
menu_off = 1

frost = 0
gs'stat'

gs 'themes', 'indoors'

'<center><b><font color = maroon>Sewing circle</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/krupo.jpg"></center>'
if pcs_sewng < 40:
	sewng_exp += rand(pcs_intel/20, pcs_intel/10)
	minut += 60
	'You spend an hour learning different sewing techniques.'
end

if pcs_sewng >= 40:
	if tkanfirst = 0:
		minut += 60
		sewng_exp += rand(pcs_intel/20, pcs_intel/10)
		'She has some spare fabric for you as it''s the first time you need some, but she tells you to bring some next time.'
		msg 'The teacher tells you that you will need to bring in fabric for further studies, you can buy some in the hardware department at the supermarket.'
		tkanfirst = 1
	elseif tkan <= 0:
		msg 'The teacher reminds you, that in order to further improve your skills, you need to buy some fabric to work with.'
		'You can''t do your lesson without fabric so there is nothing you can do here now.'
	else
		minut += 60
		tkan -= 1
		sewng_exp += rand(pcs_intel/20, pcs_intel/10)
		'For an hour you learn more advanced techniques and practice cutting and sewing the fabric you purchased.'

		if pcs_sewng >= 60 and poTalk = 0:
			poTalk = 1
			*nl
			'With a satisfied smile your teacher tells you, "Well done, now you can alter clothes to fit your size perfectly! However, this does not mean that any further studies would be useless. With further lessons I feel that I can teach you how to embroider splendid tapestries, which would be highly sought after, although very laborious to make."'
		end

		if pcs_sewng >= 80 and poTalk = 1:
			*nl
			'The teacher nods quietly and says, "You have mastered simple tapestries. It''s not without flaws, but good enough to be able to sell them and as your skills continue to grow, the quality will improve as well."'
		end
	end
end

*nl
'Your sewing skill is now <<pcs_sewng>>.'
*nl
if tkan >= 0:'Your cloth will suffice for <<tkan>> more lessons.'

gs'stat'
act 'Leave':gt'gdkkru'

--- krupo ---------------------------------

