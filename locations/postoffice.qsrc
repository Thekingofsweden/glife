# postoffice

if $ARGS[0] = 'start':
	*clr & cla
	$menu_loc = 'postoffice'
	$menu_arg = 'start'
	menu_off = 0
	gs 'stat'
	$postloc = ''

	act 'Leave':$postloc = 'leave' & gt 'postoffice'

	if (hour >= 9 and hour <= 10) or (hour >= 15 and hour <=16):

		'<center><b><font color="maroon">Post Office</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/city.jpg"></center>'
		'It is currently very busy and looks like you''ll have to wait to get to the counter.'

		act 'Wait in line (30 Minutes)':
			cla
			minut += 30
			gs 'stat'
			$postloc = 'counter' & gt 'postoffice'
		end

		if exhibitionist_lvl > 2:
			gs 'willpower', 'exhib', 'self', 'hard'
			if will_cost <= pcs_willpwr:
				act 'Flash both your tits and your pussy to skip the line (<<will_cost>> Willpower)':
					*clr & cla
					gs 'willpower', 'exhib', 'self', 'hard'
					gs 'willpower', 'pay', 'self'
					menu_off = 1
					gs 'flash', 'full', 'inside', 1
					gs 'stat'

					act 'Go to the counter':$postloc = 'counter' & gt 'postoffice'
				end
			else
				act 'Flash both your tits and your pussy to skip the line (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
			end
		end

		if exhibitionist_lvl > 1:
			gs 'willpower', 'exhib', 'self'
			if will_cost <= pcs_willpwr:
				act 'Flash your pussy to skip the line (<<will_cost>> Willpower)':
					*clr & cla
					gs 'willpower', 'exhib', 'self'
					gs 'willpower', 'pay', 'self'
					menu_off = 1
					gs 'flash', 'pussy', 'inside', 1
					gs 'stat'

					act 'Go to the counter':$postloc = 'counter' & gt 'postoffice'
				end
			else
				act 'Flash your pussy to skip the line (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
			end
		end

		if exhibitionist_lvl > 0 or pcs_inhib >= 45:
			gs 'willpower', 'exhib', 'self', 'easy'
			if will_cost <= pcs_willpwr:
				act 'Flash your tits to skip the line (<<will_cost>> Willpower)':
					*clr & cla
					gs 'willpower', 'exhib', 'self', 'easy'
					gs 'willpower', 'pay', 'self'
					menu_off = 1
					gs 'flash', 'tits', 'inside', 1
					gs 'stat'

					act 'Go to the counter':$postloc = 'counter' & gt 'postoffice'
				end
			else
				act 'Flash your tits to skip the line (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
			end
		end

		if exhibitionist_lvl > 0:
			gs 'willpower', 'bj', 'self'
			if will_cost <= pcs_willpwr:
				act 'Suggest a blowjob in exchange for jumping the queue (<<will_cost>> Willpower)':
					*clr & cla
					gs 'willpower', 'bj', 'self'
					gs 'willpower', 'pay', 'self'
					menu_off = 1
					if cumloc[11] = 0:
						gs 'npcgeneratec', 0, 'stranger', rand(18,45)
						gs 'boyStat', $npclastgenerated

						inhib_exp += rand(2,5)
						gs 'arousal', 'bj', 10, 'sub'
						gs 'stat'

						act 'Cum on your face':
							*clr & cla
							if pcs_haircol = 0:
								$cum_face_image = 'black/' + rand(1,23)
							elseif pcs_haircol = 1:
								$cum_face_image = 'brown/' + rand(1,31)
							elseif pcs_haircol = 2:
								$cum_face_image = 'red/' + rand(1,19)
							elseif pcs_haircol = 3:
								$cum_face_image = 'blonde/' + rand(1,25)
							else
								$cum_face_image = 'custom/' + rand(1,20)
							end
							'<center><img <<$set_imgh>> src="images/pc/body/cum/cumface/<<$cum_face_image>>.jpg"></center>'
							'The man cums on your face.'
							spafinloc = 11
							gs 'cum_manage'
							gs 'arousal', 'end'
							gs 'stat'

							act 'Go to the counter':$postloc = 'counter' & gt 'postoffice'
						end

						act 'Cum in your mouth':
							*clr & cla
							'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/sex/cumlip.jpg"></center>'
							'The man cums inside your mouth.'
							spafinloc = 12
							gs 'cum_manage'
							gs 'arousal', 'end'
							gs 'stat'

							act 'Go to the counter':$postloc = 'counter' & gt 'postoffice'
						end

					else
						'<center><video autoplay loop src="images/locations/shared/postoffice/punch.mp4"></video></center>'
						'The Man punches you in the gut and says, <font color="#0D42C0">"Go away bitch, your face is covered in sperm."</font> You stagger away from the postoffice and spend 2 hours curled up like a ball.'
						minut += 120
						gs 'pain', 4, 'tummy', 'hit'

						act 'Recover':
							if bankloc = 1:
								gt 'down'
							else
								gt 'pavCommercial'
							end
						end
					end
				end
			else
				act 'Suggest a blowjob in exchange for jumping the queue (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
			end
		end
	else
		'<center><b><font color="maroon">Post Office</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/city.jpg"></center>'
		'It is currently quiet and you can walk straight up to the counter.'

		act 'Go to the counter':
			$postloc = 'counter' & gt 'postoffice'
		end
	end

	if bankloc = 0: act 'Go to Postmaster''s Office': gt 'postmaster', 'start'
end

if $postloc = 'counter':
	cla
	*clr
	$menu_loc = 'postoffice'
	$menu_arg = 'counter'
	menu_off = 0
	'<center><b><font color="maroon">Counter</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/counter.jpg"></center>'
	'The counter, its boring here.'

	act 'Leave':$postloc = 'leave' & gt 'postoffice'

	if bankAccount = 1 and money > 0:
		act 'Deposit money to your bank account':
			cla
			menu_off = 1
			minut += 5
			gs 'stat'
			kartaIN = input ("How much money do you want to deposit into the account?")

			if kartaIN <= 0 or kartaIN > money:
				'Not correct operation.'
			else
				karta += kartaIN
				money -= kartaIN

				'<br>You put on the account <<kartaIN>> <b>₽</b>, now you have <<karta>> <b>₽</b> on it.'
			end

			'"Is there anything else I can do for you?"'

			act 'Leave the post office':$postloc = 'leave' & gt 'postoffice'
			act 'Yes':$postloc = 'counter' & gt 'postoffice'
		end
	end

	if legal_fine > 0:
		act 'Pay toward legal fine':
			menu_off = 1
			gs 'stat'
			fineIN = input ("How much money do you want to pay off your fine/s?")

			if fineIN <= 0 or fineIN > money:
				'Not correct operation.'
			else
				legal_fine -= fineIN
				money -= fineIN

				'<br>You pay <<fineIN>> <b>₽</b> of your legal fine/s, now you have <<legal_fine>> <b>₽</b> still outstanding.'
			end
		end

		if money > legal_fine:
			act 'Pay off your legal fine/s':
				money -= legal_fine
				legal_fine = 0
				*clr & cla
				menu_off = 1
				gs 'stat'
				'<center><b><font color="maroon">Counter</font></b></center>'
				'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/counter.jpg"></center>'
				'You pay the full amount of your outstanding fine/s to the cashier and they print out a receipt to say that it is paid off.'
				act 'Done':$postloc = 'counter' & gt 'postoffice'
			end
		end
	end

	if used_pattest > 0:
		act 'Send paternity test (20.000 <b>₽</b>)':
			menu_off = 1
			if money >= 20000:
				dynamic $send_test
				*clr & cla
				gs 'stat'
				'You pay the fee of the testing, then you mail your used paternity test to the testing labor in Saint Petersburg with the copy of your receipt of payment. You will get answer in SMS withing a week.'
			else
				'Unfortunately you have no money to send the test in. You could, but they would just throw it out without the receipt of payment.'
			end
			act 'Done':$postloc = 'counter' & gt 'postoffice'
		end
	end

	act 'Check received mail':
		*clr & cla
		menu_off = 1
		gs 'stat'
		minut += 5
		if mail <= 0:
			'Sorry no mail.'
		else
			'You have mail.'
		end

		'"Is there anything else I can do for you?"'

		act 'Yes':$postloc = 'counter' & gt 'postoffice'

		act 'Leave the post office':$postloc = 'leave' & gt 'postoffice'
	end

	i = 0

	*pl '<center><table align="center">'

	:loopcard

	if i = 0:
		n = 5
		price = 60
		$buy_link = '<a' + iif(money >= 60, ' href="exec: gs ''$buy_item'', <<n>>, ''hours of internet access'', <<price>>, ''internet'', ''postoffice''">buy ','>buy ') +'</a>'
	elseif i = 1:
		n = 10
		price = 115
		$buy_link = '<a' + iif(money >= 115, ' href="exec: gs ''$buy_item'', <<n>>, ''hours of internet access'', <<price>>, ''internet'', ''postoffice''">buy ','>buy ') +'</a>'
	elseif i = 2:
		n = 20
		price = 220
		$buy_link = '<a' + iif(money >= 220, ' href="exec: gs ''$buy_item'', <<n>>, ''hours of internet access'', <<price>>, ''internet'', ''postoffice''">buy ','>buy ') +'</a>'
	elseif i = 3:
		n = 50
		price = 520
		$buy_link = '<a' + iif(money >= 520, ' href="exec: gs ''$buy_item'', <<n>>, ''hours of internet access'', <<price>>, ''internet'', ''postoffice''">buy ','>buy ') +'</a>'
	end

	i += 1

	'<tr><td align="center"><<n>> hour internet card </td><td align="right">(you have <<internet>> hours of internet access) </td><td align="right"><<$buy_link>> </td><td align="left">for <<price>> <b>₽</b></td></tr>'

	if i < 4: jump 'loopcard'

	'</table></center>'

	if money > 0:
		act 'Put the money on the phone':
			cla
			menu_off = 1
			set minut = minut + 5
			obmennik = input ("How many <b>₽</b> do you want to deposit?")

			if obmennik <= 0:
				'<center><br><b>Do you want to think again?</b></center>'
				act 'Return':$postloc = 'counter' & gt 'postoffice'
			else
				if money < obmennik:
					'<center><br>You do not have <b><<obmennik>></b> <b>₽</b>.</center>'
					act 'Return':$postloc = 'counter' & gt 'postoffice'
				end

				if money >= obmennik:
					set balans = balans + obmennik
					set money = money - obmennik
					clr
					gs'stat'
					'<center><br>You put <b><<obmennik>></b> <b>₽</b> to the account.</center>'

					'"Is there anything else I can do for you?"'

					act 'Yes':$postloc = 'counter' & gt 'postoffice'

					act 'Leave the post office':$postloc = 'leave' & gt 'postoffice'
				end
			end
		end
	end

!!Might use for the city branch, but seems ott for Pav
!!	if askWork = 0 and bankloc = 0:
!!		act 'Ask for work':

!!			*clr & cla
!!			minut += 5
!!			gs 'stat'

!!			'You approach the counter and ask the girl at the counter: "Excuse me... I hear you are looking for postal workers? I would like to come work as a one."'
!!'She nods understandingly: "We are looking for new workers, that''s true. I''d hire you in an instant, but unfortunately it''s not up to me. You should go see Oleg Koltsov, the postmaster. He''s usually in his office between 12:00 and 14:00. I bet he will hire you! Good luck when you go see him!"'
!!			'"I''m afraid there is no position available at this time."'

!!			act 'Step away from the counter':gt'postoffice', 'start'
!!askWork += 1
!!		end
!!	end
end

if $postloc = 'leave':
	minut += 3
	killvar '$postloc'
	if bankloc = 1:
		gt 'down'
	else
		gt 'pavCommercial'
	end
end

!! Not used for Pav, maybe use for city?
if $postloc = 'post_off':
	*clr & cla
	menu_off = 1
	minut += 30
	gs 'stat'
	'<center><b><font color="maroon">Postmaster Office</font></b></center>'
	!!Blatte: MISSING IMAGE
	$ImageNeededPlacholder
	!!'<center><img src="images/system/image_needed.png"></center>'
	'Behind his desk, a man of about 50 is busy with some paperwork. He''s dressed quite sharply, and you had never expected the postmaster would be this good looking.'

	act 'Exit office':gt'postoffice', 'start'

	if post_wrk = 0:
		act 'Ask for work':
			*clr & cla
			minut += 15
			gs 'stat'
			!!Placeholder text
			'You approach the man''s desk, but he doesn''t look up from his work. With a soft voice you carefully try to get his attention: "Umm... excuse me?"'
			'He looks up from his desk: "Oh I''m sorry, I didn''t hear you come in. Hello, I''m Oleg Koltsov, postmaster at this post office. Is there a problem?"'
			'You smile and shake your head: "No, no problem. I am looking for a job, I hear you are often looking for workers?"'
			'You can feel his eyes exploring your body for a moment before he responds: "Yes, we are! We currently only have a part-time vacancy, for sorting the mail. You would be working between these and these hours, give or take. Does that sound good to you?"'

			act'"Sorry, not interested"':
				*clr & cla
				gs 'stat'
				'"I need to think about it if it''s okay."'
				'"Sure <<$pcs_nickname>>, come back when you''ve made up your mind."'

				act'Exit office':gt'postoffice', 'start'
			end

			act'"That sounds great!"':
				*clr & cla
				post_wrk += 1
				gs 'stat'
				!!Placeholder text
				'You beam: "That sounds fantastic, thank you so much!"'
				'Oleg gives you a friendly smile, happy to have found another potential worker. He quickly writes some of your personal data down.'
				'Oleg continues: "If you want to work a shift, you need to be here between this time. There''s no point in you getting changed for just 20 minutes of work."'
				'That also sounds fair. You give him a handshake as you get ready to leave: "Okay. Thank you sir!"'

				act'Exit Office':gt'postoffice', 'start'
			end
		end
	end
end

--- postoffice ---------------------------------

