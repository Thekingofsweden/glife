# rinok
$location_type = 'public_outdoors'
if $ARGS[0] = 'start' or $metka = 'start':
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end
	!if sound = 0:PLAY 'sound/street.mp3',30

	cls
	gs 'stat'
	minut = minut + 5
	mag = 1
	$metka = 'start'
	$loc = 'rinok'
	$menu_loc = 'rinok'
	$menu_arg = 'start'
	
	'<center><b><font color="maroon">Marketplace</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/market/rinok.jpg"></center>'
	''
	'On one of the stalls has a sign "Cash paid for secondhand clothes", here you can <a href="exec:cloc=2 & gt ''clothing'', ''view_clothing_list'', ''sell''">sell individual items</a> or <a href="exec:gs ''portnoi''">all of your unwanted clothes</a>.'

	if $message ! '':
		'<center>'+$message+'</center>'
		killvar '$message'
	else
		''
	end
	*pl '<center><table align="center" width=80%>'
	*P FUNC ('$stock_item', 10, 'Dishwashing detergent', 400, 'fairy', 'rinok')
	*P FUNC ('$stock_item', 10, 'Clothing detergent', 400, 'poroshok', 'rinok')
	*P FUNC ('$stock_item', 8, 'Plates', 450, 'cltarelka', 'rinok')
	*P FUNC ('$stock_item', 20, 'Cosmetics', 1000, 'kosmetica', 'rinok')
	*P FUNC ('$stock_item', 25, 'Razors (number of shaves)', 300, 'stanok', 'rinok')
	*P FUNC ('$stock_item', 10, 'Tampons', 160, 'tampon', 'rinok')
	*P FUNC ('$stock_item', 10, 'Sanitary napkins', 80, 'sanpad', 'rinok')
	*P FUNC ('$single_stock_item', 1, 'Umbrella', 1000, 'umbrella', 'rinok', '', iif(bag > 0, 0, 1), 'You need a purse')
	'</table></center>'
	*pl
	if analplugIN = 1 and $pantyworntype = 'none' and PCloSkirt > 2:
		nurand = rand(0, 100)

		if nurand >= 80:
			pcs_mood -= 5
			gs 'exhibitionism', 1
			'You feel someones hand gently touch <a href="exec:gt ''etoexhib'', ''pos15''">your butt</a>.'

			gs 'arousal', 'foreplay', 3
			gs 'stat'

		end
	end

	act 'Leave':
		cla
		minut += 5
		$metka = ''
		gs 'arousal', 'end'
		gs 'stat'
		gt 'street'
	end

	if workrin = 1:
		if week = 2 or week = 4 or week = 6:
			if hour < 9:
				'Arthur is waiting to set up a stall for you.'
				act '<b>Set up stall</b>':
					cla
					*clr
					hour = 9
					minut = 0
					prodpayum = 1200
					'Arthur unloads stuff from his truck and gives them to you. You start hanging the clothes on the stall.'
					'Before leaving Arthur gives you instructions. "Sell at whatever price you want, but at the end of your shift I will charge you 1000 <b>₽</b> for each item sold. You should be making me no less than <<arturplan>> <b>₽</b>."'

					act 'To work':gt 'rinwork', 'start'
				end
			end
		end
		act 'Retire from work':
			cla
			work = 0
			workrin = 0
			'You retired from work and collect papers.'

			act 'Leave':gt 'rinok', 'start'
		end
	end

	if (work = 0 or cheatWork = 1) and workrin = 0:
		nl
		'On one of the tents hangs an ad, "Need female market stall staff. Paychecks big."'

		act 'Approach the owner of the ad':
			cla
			*clr
			'Hi, my name is Arthur. I own many stalls here, I need a saleswoman. The last one moved to St. Petersburg. Want the job?'
			'Pay is a percentage of sales, paid at end of shift. Your shifts would be Tuesdays, Thursdays and Saturdays. From 8 till 14.'
			'Government insist I have your papers, so it would be your main job.'

			act 'Leave':gt 'rinok', 'start'

			act 'Take the job':
				cla
				work = 1
				workrin = 1
				arturplan = 5000
				'You give your papers to Arthur and shaking your hand he welcomes you on board.'

				act 'Leave':gt 'rinok', 'start'
			end
		end
	end

	act 'Go to the book tray':
		cls
		minut += 15
		gs'stat'

		'On the counter is a very entertaining fantasy book, a science fiction book and women''s romance novels. They all cost 350 <b>₽</b>.'

		if money >= 350:
			act 'Buy a book of fantasy 350 <b>₽</b>':
				cls
				minut += 15
				money -= 350
				f_book += 1
				gs'stat'

				'You bought the book for fantasy 350 <b>₽</b>.'

				act 'Leave':gt'rinok','start'
			end
		end

		if money >= 350:
			act 'Buy a book of science fiction 350 <b>₽</b>':
				cls
				minut += 15
				money -= 350
				f_book += 1
				gs'stat'

				'You bought the book for science fiction 350 <b>₽</b>.'

				act 'Leave':gt'rinok','start'
			end
		end

		if money >= 350:
			act 'Buy ladies for romance 350 <b>₽</b>':
				cls
				minut += 15
				money -= 350
				w_book += 1
				gs'stat'

				'You bought the novel for the ladies 350 <b>₽</b>.'

				act 'Leave':gt'rinok','start'
			end
		end

		act 'Leave':gt'rinok','start'
	end

	if coat[5] < 1:
		'On the market you see winter jackets for sale - 4,000 <b>₽</b>'
		if coat[5] = 0:dynamic 'act ''Look at cheap coat 5'':gt ''clothing'', ''view_clothing_item'', ''shop'', ''coat'', 5, 4000'
	end

	act 'View clothing':
		cla
		minut += 5
		gt 'rinok', 'clo'
	end
end

if $ARGS[0] = 'clo':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cls
	gs'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Clothes.</font></b></center>'
	'Cheap clothing on the market stall.'

	act 'Return':
		cla
		minut += 5
		gt 'rinok', 'start'
	end

	i = 15
	:loopcheap
	if cheap[i] = 0:*p '<a href="exec: i = <<i>> & gt ''rinok'', ''marketclo''"><img <<$set_imgh>> src="images/pc/clothing/1cheap/<<i>>.jpg" 	height="400" /></a>'
	i += 1
	if i <= 20:jump 'loopcheap'
end

if $ARGS[0] = 'marketclo':
	cla

	if cheap[i] ! 0:
		msg 'You already own these clothes.'
		gt 'rinok', 'clo'
	end

	gt 'clothing', 'view_clothing_item', 'shop', 'cheap',i, 750

end

if $ARGS[0] = 'tailor':
	CLOSE ALL
	cls
	gs 'stat'
	'Above the window the sign says, Jacob Solomonovich Schneersohn, tailor, 8:00-16:00 Monday - Friday'
	act 'Speak to the tailor':
		cla
		*clr
		'Elderly Jewish man makes a pattern at the table, he pulls his glasses down a little and looking over the top of them he points at you.'

		if portnoyQW > 0:
			'Aah <<$pcs_nickname>>, come, come to adjust clothes again?'

			if money >= 500:
				act 'Take in the clothes you are wearing (500 <b>₽</b>)':
					cla
					*clr
					minut += 15
					if CloStyle = 5 or $clothingworntype = 'exercise':
						'What are you wasting my time for? This type of clothing does not need to be resized.'
					else
						money -= 500
						dynamic '<<$clothingworntype>>B[<<clothingwornnumber>>] = pcs_hips'
						'You undress and give the clothes you need adjusting to Jacob Solomonovich, it is fifteen minutes until your clothes and returned to you.'
					end

					act 'Leave': minut += 10 & gt 'street'
				end

				act 'Take in different clothes':gt 'clothing', 'view_clothing_list', 'resize'
				act 'Take in all the clothes':gt 'portnoi2'
			end
		else
			'Hello young lady. I am Solomonovich Jacob, for you simply Yasha. And what is your name?'

			act 'Tell him your name':
				cla
				portnoyQW = 1
				'"<<$pcs_nickname>>"'
				'"What a pretty name. <<$pcs_nickname>>, come, sit here on this chair. Anyway how can I help you?'

				act 'Ask him about your clothes':
					cla
					*clr
					minut += 15
					gs 'stat'
					'"Can you tailor my clothes to fit me better?"'
					'"Of course I can! Otherwise, what kind of tailor would I be? Here, let me see what you''re wearing."'
					'Jacob Solomonovich jumps up smartly and nimbly removes the tape from his neck and begins to measure your chest, waist and hips.'
					'"Well, I can adjust any clothing that doesn''t fit you any more for 500 <b>₽</b> per item."'

					act 'Leave': minut += 10 & gt 'street'
				end
			end
		end
		act 'Leave': minut += 10 & gt 'street'
	end
end

--- rinok ---------------------------------

