# bedrPar
CLOSE ALL
$metkaM = $ARGS[0]
$locM = $CURLOC
$metka = $ARGS[0]
$loc = $CURLOC
clr
minut += 1
gs 'stat'
fcolor = rgb(0, 0, 0)
bcolor = rgb(255, 255, 255)
lcolor = rgb(106, 90, 205)

if sisboyday + 1 = daystart and hour = 18:gt'sisterQW','sisboyQW_expel'

'<center><b><font color="maroon">Your bedroom</font></b></center>'
! WD: Correct Image path ~ "images/qwest/alter/<<$loc>>.jpg"
'<center><img src="images/qwest/alter/bedrPar.jpg"></center>'
'A small room in which you and your sister live. It contains a <a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''">wardrobe</a> for your clothes, your <a href="exec:gt ''bed2''">bed</a>, a <a href="exec:gt ''stol'' ,''stolPar''">desk</a> and your sister''s bed.'
if komp = 1:'Your <a href="exec:gorodokKomp = 1 & GT ''Komp'',''start''">laptop</a> is on the desk.'
gorodokKomp = 0

gad = 0

if sisboyQW > 10:sisboyQW = 10

gs'dinsister'

if clrbelo <= 4: clrbelo = 5

act 'Exit the room':
	gt'korrPar'
end
if sisboyparty_day + 1 = daystart and sisboyparty = -1 and hour = 22:
	gs'family'
else
	gs'family'

	if rexCarCount >= 9 and sisterHere = 1 and hour < 3 and rexSmsDay ! daystart and smsForRex < 13:
		act 'Take your sister''s phone':gt 'reks','sms'
	end

	if student > 0 and kursovik < 100:
		act 'Do coursework (1:00)':
			cla
			*clr
			minut += 60
			kursovik += rand(intel / 20, intel / 10)

			if kursovik > 100:kursovik = 100

			manna -= 10
			'<center><img src="images/etogame/study.jpg"></center>'
			'You sit down at the desk and do your coursework, you''ve finished <<kursovik>> percent of it.'

			act 'Leave your desk':gt $curloc
		end
	end

	if horny >= 50 and sisterHere = 0:
		act 'Masturbate':gt 'selfplay'
	end
	if pornMagazine > 0:'You have hidden your porn magazines under the mattress of your bed.'
	if sisterHere = 0 and pornMagazine > 0:dynamic $d_read_porn

	dynamic $d_read_book

	if tovarL > 0:'You currently have <<tovarL>> trinkets to sell at the station. The maximum you can carry with you is 30.'

	if card > 0:
		act 'Look at your deck of cards':gt 'card'
	end

	if dur > 0:'You have hidden <<dur>> doses of Pale Lady.'

	if dur > 0 and narkday ! daystart:
		act 'Use some Pale Lady (0:05)':
			cla
			*clr
			minut += 5
			narkday = daystart
			health = vital * 10 + stren * 5
			willpower = intel * 5 + will * 5
			manna = (intel * magik) + magik * 100 + vital * 10 + rikudo
			horny = 100
			nark += 10
			dur -= 1
			'<center><img src="images/etogame/dur.jpg"></center>'
			'You snort some Pale Lady, feeling dizzy for just a moment. After that you feel fantastic, and incredibly horny.'

			act 'Continue':gt $curloc
		end
	end
end

if tanga = 0:
	act 'Wear underwear':
		cla
		tanga = 1
		gt $curloc
	end
elseif tanga = 1:
	act 'Take off your underwear':
		cla
		tanga = 0
		gt $curloc
	end
end

sports = 0

if obruch > 0 and obruchday ! daystart:
	$sitrobr = '<a href="exec: dynamic $sitrobrd ">Hula hoop</a>'
	sports = 1
else
	$sitrobr = ''
end

if bookYog > 0 and bookYogday ! daystart:
	$sitryog = '<a href="exec: dynamic $sitryogd ">Meditate</a>'
	sports = 1
else
	$sitryog = ''
end

if skak > 0 and skakday ! daystart:
	$sitrskak = '<a href="exec: dynamic $sitrskakd ">Jumping rope</a>'
	sports = 1
else
	$sitrskak = ''
end

if pressday ! daystart:
	$sitrpress = '<a href="exec: dynamic $sitrpressd ">Abdominal exercises</a>'
	sports = 1
else
	$sitrpress = ''
end

if pushday ! daystart:
	$sitrpush = '<a href="exec: dynamic $sitrpushd ">Push-ups</a>'
	sports = 1
else
	$sitrpush = ''
end

$sportsitr = '<<$sitrpush>> <<$sitrpress>> <<$sitrskak>> <<$sitryog>> <<$sitrobr>>'

if sports = 1:
	'There isn''t much space in the room, but enough for a variety of sport activities such as:'
	'<center><<$sportsitr>></center>'
end

if gobelen>0:'You have <<gobelen>> completed tapestries.'
if poSkill>=50 and tkan>0:'You have enough fabric for <<tkan>> lessons.'
if poSkill>=50 and tkan<=0:'You do not have any fabric.'
if poSkill>=200:
	if newgobelen=0 and tkan>0:
		act'Start a new tapestry':
			cls
			tkan-=1
			newgobelen=1
			minut+=15
			gs'stat'
			'You spend 15 minutes at the embroidery frame, preparing the fabric and outlining the pattern for the tapestry.'
			act'Set the tapestry aside':gt $curloc
		end
	end
	if newgobelen>=1:
		'Your tapestry is <<newgobelen/10>> percent finished.'
		act'Work on the tapestry for an hour':
			cls
			minut+=60
			poSkill+=rand(0,1000)
			gobramax=poSkill/100
			gobramin=poSkill/200
			newgobelen+=rand(gobramin,gobramax)
			gs'stat'
			if newgobelen<1000:'You spent an hour working on your tapestry, which is now <<newgobelen/10>> percent finished.'
				if newgobelen>=1000:
				newgobelen=0
				gobelen+=1
				'Your work on the tapestry is done.'
			end
			act'Set the tapestry aside':gt $curloc
		end
	end
end


--- bedrPar ---------------------------------

