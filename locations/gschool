# gschool
CLOSE ALL
$metka = $ARGS[0]
$loc = $CURLOC
gs 'stat'
gs 'time'
'<center><b><font color="maroon">School</font></b></center>'
'<center><img src="images/qwest/alter/gschool.jpg"></center>'
'Ordinary school where local children learn.'


act 'Leave':gt 'gorodok'

if zverRageQW = 1:
	cls
	zverRageQW = 2
	gs 'stat'
	gs 'time'
	'As you cross the school yard after school, you are surprised by a powerful kick to your ass. Dumbfounded you turn to see Christina Zverev her face contorted with rage, she yells at you.'
	'"You fucking bitch! How dare you spread shit about me like that? I''ll break your fucking face!"'
	$nameV = 'Christina'
	!!stats
	strenV = 85
	speedV = 75
	agilV = 75
	reactV = 75
	vitalV = 95
	intelV = 45
	willV = 45
	!Skills
	magikV = 95
	boxingV = 35
	shootV = 95
	!Parameters
	healthV = vitalV*10 + strenV*5
	mannaV = intelV*10 + magikV*10
	willpowerV = willV*10
	BonusAtakV = 0
	BonusDefV = 0
	!Action Programme
	program = 0
	ZverevaFight = 1

	if spellavtoklon = 1:klon = 3
	if spellbefshild = 1:defence = 500

	round = 0

	act 'fight':gt 'fight','start'

	exit
end

if day = 31 and month = 12 and age <= 17 and StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock ! 1:
	if hour = 14:
		'Near the school are already well-dressed girls and boys in your class. Someone called you "<<$name>>, you did not hand over a thousand for the new year"'

		if money >= 1000:act 'Go to a party (1,000 rubles)':money -= 1000 & gt 'sny'
	end
end
if week = 6 and schoolSorev = 2 and hour = 10:
	'Near the school students from different schools and athletes prepare for the running competition.'

	act 'Participate in city running race':
		cls
		minut += 30
		gs 'stat'
		'You sign up to the race with the other athletes. As you prepare for the competition, you watch the guys race. As expected Ivan Prokhorov won. After the guys have finished it''s the girls turn.'

		if swinbeggor = 0:
			'Getting ready at the start is Christina Zvereva, one of the girls in your class. She scoffs at you, "You''ll never beat me."'
		elseif swinbeggor = 1:
			'Getting ready at the start is Christina Zvereva, one of the girls in your class. She looks at you angrily, "You beat me once, but never again."'
		elseif swinbeggor = 2:
			'Getting ready at the start is Christina Zvereva, one of the girls in your class. She looks at you with hatred "You again, bitch? Never mind, this time I''ll do it!"'
		end

		act 'Go to start':
			cls
			kolsorev += 1
			minut += 30
			schoolSorev = 0
			gs 'stat'
			'<center><img src="images/etogame/begsor.jpg"></center>'
			'Girls 400 meters race. You change clothes and go to the track.'
			wait 725
			'On your marks.'
			wait 725
			'Get set.'
			wait 725
			'Go!'
			wait 725

			begresult = beg + speed + vital + rand(1, 20) - salo/2 - rand(1, 20)

			if begresult <= 0:
				'You barely managed to reach the line, there''s no point checking were you finished, you are last by some distance.'
			elseif begresult < 70:
				'You struggle through the race, finishing in last place.'
			elseif begresult < 80:
				'You fought hard, but only manage to finish second to last.'
			elseif begresult < 90:
				'You fought hard, but only manage to take 6th place.'
			elseif begresult < 100:
				'You fought hard, but only manage to take 5th place.'
			elseif begresult < 110:
				'You fought hard, but only manage to take 4th place.'
			elseif begresult < 120:
				'You fought hard and manage to take 3rd place. The P.E. teacher congratulates you.'
			elseif begresult < 130:
				'You fought hard and managed to take 2nd place. The P.E. teacher congratulates you on your success.'
			else
				swinbeggor += 1
				dom += 1
				'You fought hard and managed to take 1st place. The P.E. teacher congratulate you on winning the competition.'
			end

			beg += 1
			fat -= 10

			if swinbeggor = 3 and razradbeg = 1:
				razradbeg = 2
				grupNPC[18] = 0
				zverevaQW = 1
				'You have won the competition three times and your P.E. teacher awards you the second sports category in running. Christina Zvereva stands nearby, staring at you the whole time snarling, her hands balled into fists.'
			elseif swinbeggor = 2 and begresult >= 130:
				grupNPC[18] = 0
				'At the finish line is Christina Zvereva trying to catch her breath, she was unable to overtake you, and came second. Christina tries not to look you in the eye.'
			elseif swinbeggor = 1 and begresult >= 130:
				grupNPC[18] = 0
				'At the finish line is Christina Zvereva trying to catch her breath, she was unable to overtake you, and came second. Through clenched teeth Christina growls at you, "You bitch!"'
			end

			gs 'stat'

			act 'Leave':gt 'gorodok'
		end
	end
end

if week = 6 and schoolSorev = 1 and hour = 10:
	'Near the school are students from different classes preparing for the running competition.'

	act 'Participate in school running race':
		cls
		minut += 30
		gs 'stat'
		'You came to the athletes and he wrote you. After this athlete finished all the preparations for the competition and the guys went to a stratum. The guys went over and as expected won the Ivan Prokhorov. After the guys finished the turn girls.'

		act 'Go to start':
			cls
			minut += 30
			schoolSorev = 0
			kolsorev += 1
			gs 'stat'
			'<center><img src="images/etogame/begsor.jpg"></center>'
			'Girls 400 meters race. You change clothes and go to the track.'
			wait 725
			'On your marks.'
			wait 725
			'Get set.'
			wait 725
			'Go!'
			wait 725

			begresult = beg + speed + vital + rand(1, 20) - salo/2 - rand(1, 20)

			if begresult <= 0:
				'You barely managed to reach the line, there''s no point checking were you finished, you are last by some distance.'
			elseif begresult < 10:
				'You struggle through the race, finishing in last place.'
			elseif begresult < 20:
				'You fought hard, but only manage to finish second to last.'
			elseif begresult < 30:
				'You fought hard, but only manage to take 6th place.'
			elseif begresult < 40:
				'You fought hard, but only manage to take 5th place.'
			elseif begresult < 50:
				'You fought hard, but only manage to take 4th place.'
			elseif begresult < 60:
				'You fought hard and manage to take 3rd place. The P.E. teacher congratulates you.'
			elseif begresult < 70:
				'You fought hard and manage to take 2nd place. The P.E. teacher congratulates you on your success.'
			else
				swinbeg += 1
				dom += 1
				'You fought hard and manage to take 1st place. The P.E. teacher congratulate you on winning the competition.'
			end

			beg += 1
			fat -= 10

			if swinbeg = 3 and razradbeg = 0:
				razradbeg = 1
				'You have won the school competition three times and your P.E. teacher announces that your success has not gone unnoticed and awards you the third sports category in running.'
			end

			gs 'stat'

			act 'Leave':gt 'gorodok'
		end
	end
end

if hour <= 8 and week < 6 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 6:
	'Today is graduation and the school prom.'

	act 'Go to graduation':
		cls
		SchoolAtestat = 1
		intel += 10
		gs 'stat'
		'Everyone form your year assembles. The teachers congratulate you all on successful completion of school and hand out diplomas.'

		act 'Leave':gt 'gorodok'

		act 'Go to the prom':
			cls
			minut += 120
			alko += 5
			cumlip = 0
			fbreath = 0
			tanga = 0
			gs 'stat'
			gs 'clothing', 'strip'
			!!gee gee, no normal outlet
			'After graduation all get as drunk as assholes. You barely remember anything that happened, only that you were one of the group who went skinny-dipping in the fountain. You wake up by the town center completely naked.'

			act 'Leave':gt 'gorodok'
		end
	end
end

if hour <= 8 and week < 6 and StoryLine > 0 and SchoolAtestat = 0 and kanikuli = 0:
	if $clothingworntype = 'sforma':
		act 'Go to school':gt 'gschool2'
	else
		'You can only attend school if you are wearing your school uniform.'
	end
end

if hour >= 16 and GorSlut >= 3 and SchoolGossip > 0 and afterschoolday ! daystart:
	aftgopbjrand = rand(0, 2)

	if aftgopbjrand = 0:
		cla
		'Waiting for you at the school exit are Vitek and his friends. "Well, then cocksucker. Ready to work your fucking mouth? Come with us back to school."'

		act 'Return to school':
			cls
			afterschoolday = daystart
			SchoolGossip += 1
			minut += 60
			gs 'stat'
			'You return to school with the boys giggling and looking at you. Dan says, "I do not understand why such an experienced cocksucker, still doesn''t get it?"'

			act 'Suck':
				cls
				minut += 60
				bj += 3

				if npcSex[9] = 0:npcSex[9] = 1 & guy += 1
				if npcSex[10] = 0:npcSex[10] = 1 & guy += 1
				if npcSex[11] = 0:npcSex[11] = 1 & guy += 1

				sub += 1
				gs 'stat'
				'<center><img src="images/qwest/alter/Ev/toiletBJtreesome.jpg"></center>'
				'You obediently squat and pull Viteks member out of his sweatpants. You clasp his hard cock between your <<$liptalk3>> and start sucking him off. Dan and Bob come to you and take out their members. You become one by one to pleasure cocks with your mouth, caressing those that are not in your mouth with your hands.'

				act 'Suck':
					cls
					minut += 60
					bj += 3
					cumlip += 1
					cumface += 1
					cumfrot += 1
					sub += 1
					gs 'stat'
					'<center><img src="images/qwest/alter/Ev/toiletBJcum.jpg"></center>'
					'Finally, a member discharges semen in your mouth. Watching you as sperm follows from your mouth, the remaining guys cum on your face. You close your eyes and obediently wait until the boys have finished. Finally, they leave you, walking away laughing at you.'

					act 'Leave':gt $curloc
				end
			end
		end

		exit
	end
end

if week = 1 and kanikuli = 0 and arthakQW >= 10 and apmeetday ! daystart and hour >= 16:gt 'artgar'

if hour = 16 and week < 6 and kanikuli = 0:

	if LinaSlut > 0:
		randLina = rand(1,10)
		if randLina < 3:'You see Lina, Dan and Vasily leaving school.'
	end

	if LariskaHomeIn > 0:'Walking through the courtyard of the school walks <a href="exec:minut += 1 & GT ''LariskaGo''">Lariska Gruzdev</a>.'

	if JuliaMilLernQW >= 1:'Strolling across the school yard is <a href="exec:minut += 1 & gt ''JuliaMilGo''">Julia Milov</a>.'
	if NatBelLernQW >= 1:'Strolling across the school yard is <a href="exec:minut += 1 & gt ''NatBelGo''">Natasha Belova</a>.'
	if artemLernQW >= 1 and artemVitokQW ! 2:'Waiting for you buy the school entrance is <a href="exec:minut += 15 & gt ''artemEv'', ''start''">Artem Chebotarev</a>.'
	if albinaCarday ! daystart:
		!!if DanceWhore > 0 or AlbinaLast > 0 or AlbinaBlokDance > 0:'You see standing in the school yard and BMW X5 <a href="exec:minut += 15 & gt ''albinaEv''">Albina Barlovskuyu</a> sitting behind the wheel'
		if AlbFrend >= 1 and albrideday ! daystart:'Near a large black SUV parked near the school stands <a href="exec:minut += 15 & gt ''albinaRide''">Albina Barlovskaya</a>.'
	end

	if katjaMeynoldday ! daystart and meynoldQW >= 2:'Strolling across the school yard is <a href="exec:minut += 15 & gt ''katjaEv''">Katja Meynold</a>.'
	if DimaBeHomeOnce = 1 and DimaRudeBlock = 0:
		'In the school yard, deep in thought, you see <a href="exec:minut += 15 & gt ''dimaGoHome2''">Dimka Nosov</a>.'
	elseif DimaRudeQW > 0 and dimarudeday ! daystart and DimaRudeBlock = 0:
		'In the school yard, deep in thought, you see <a href="exec:minut += 15 & gt ''dimaRudeEv''">Dimka Nosov</a>.'
	end
	if IgorLoverQW > 0 and igorkruglovday ! daystart and IdorBlock = 0:'Strolling across the school yard is <a href="exec:minut += 15 & gt ''IgorEv''">Igor Kruglov</a>.'
	if fedorKozlovQW >= 10:'Far away from the school entrance stands <a href="exec:minut += 15 & gt ''FedorEv''">Fedor Kozlov</a>, he is glad to see you and waves you over to him.'
end

if strelaDay ! daystart and strelaQW = 1 and hour = 16:
	cls
	gs 'stat'
	'A Mitsubishi Pajero and BMW drive upto the school gates. Strong, musclular guys in leather jackets and tracksuits get out of the cars. They have came to meet Vitek, Dan and Bob. You watch them talking about something before you notice Fedor Kozlov leaving school, his face swollen and bruised. Kozlov waves his hand at you, "<<$name>>, come here, you''re a witness."'

	act 'Go and see what he needs':gt 'strela'

	exit
end

gs 'family'

--- gschool ---------------------------------

