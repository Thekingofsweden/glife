# butik
if $ARGS[0] = 'start':
	$metka = $ARGS[0]
	$loc = $CURLOC
	$metkaM = $ARGS[0]
	$locM = $CURLOC
	cla
	clr
	*clr
	mag = 1
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Boutique</font></b></center>'
	'<center><img src="images/etogame/butik.jpg"></center>'
	'All kind of dresses, all at 25,000 rubles'

	if rand(1, 100) >= 80 and skin > 0 and lesbiQW = 0:
		'In the boutique you see a stylish and slender woman ,with about forty years old.'

		act 'Stare at the woman.':gt 'event', 'lesbi'
	end

	if QWdogreiqTimer = 0 and QWdogReiq = 1 and week = 7 and qwdogiventday ! daystart:
		qwdogiventday = daystart
		'Going into the boutique you begin to consider picking dresses on hangers.By casually taking one you try it on in passing it on your back ,you hang with mild grim face.One of the dresses unexpectedly attracts your attention , as you began to study it more closely ,you feel a strange feeling as if you were being watched'

		act 'Disregard':gt 'butik', 'start'
		act 'Look around':dynamic $butikreiqdi

		exit
	end

	if RimmaSex > 0:
		if rimmaday ! daystart:
			'<a href="exec:gt ''RimmaSexQW''">Rima</a> smiled at you. She bits her lip and starts shooting her eyes in your direction.'
		else
			'Rima smiled at you and continued to do her business.'
		end
	end

	'The boutique sells designer winter coats - 50,000 rubles'

	act 'View clothes':gt 'butik', 'clothes'

	if money >= 50000:
		if coat[9] < 1:
			if coat[9] = 0:dynamic 'act ''Look at designer coat 9'':gt ''clothing'', ''view_clothing_item'', ''shop'', ''coat'', 9, 50000'
		end

		if coat[11] < 1:
			if coat[11] = 0:dynamic 'act ''Look at designer coat 11'':gt ''clothing'', ''view_clothing_item'', ''shop'', ''coat'', 11, 50000'
		end
	end

	act 'Leave the boutique':
		cla
		minut += 5
		gt 'torgcentr'
	end
end

if $ARGS[0] = 'clothes':
	$metka = $ARGS[0]
	$loc = $CURLOC
	cls
	gs'stat'

	act 'Return':
		cla
		minut += 5
		gt 'butik', 'start'
	end

	if Clothingstock > 11:
		i = 1
	else
		i = 2
	end

	:loopformal
		if formal[i] = 0:*p '<a href="exec: i = <<i>> & gt ''butik'', ''formal''"><div style="width:100%; max-width:250px; max-height:250px;"><img src="images/Clothing/4formal/Formal<<i>>.jpg" 	height="250" /></div></a>'
		i += 2
	if i <= 14:jump 'loopformal'
end

if $ARGS[0] = 'formal':
	cla
	
	if formal[i] ! 0:
		msg 'You already own these clothes.'
		gt 'butik', 'clothes'
	end

	gt 'clothing', 'view_clothing_item', 'shop', 'formal',i, 25000

end

--- butik ---------------------------------

