# kid

$breastfeed = {
	cla
	*clr
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/pc/body/tits/breast_feed.jpg"></center>'
	'You pick up <<$kidname[ks]>> from <<$kidPosPro[ks]>> crib and cuddle for a moment before exposing your breast.'
	if rand(1,10) = 10:
		'You lift <<$kidname[ks]>> to your breast but your <<$sdtext[ks]>> doesn''t latch on.'
		pcs_mood -= 5
		minut += 5
	else
		minut += 15

		breastcounter += 1
		milkedvolume = func('lact_lib','$get_breastmilk', 2, 15)
		if milkedvolume < 800:
			'You lift <<$kidname[ks]>> to your breast and feed your baby <<$polreb[ks]>>. Your baby doesn''t seem to be satisfied with the amount of milk.'
		else
			'You lift <<$kidname[ks]>> to your breast and feed your baby <<$polreb[ks]>>. Your baby seems satisfied with the amount of your milk.'
		end
	end
	'You gently lay <<$kidname[ks]>> back <<$babyloc>>.'
	act 'Done':
		if $pcs_mothername = '':
			$pcs_mothername = input("What name do you wish your children to call you by? (Leave it blank for ''Mom'')")
			if $pcs_mothername = '': $pcs_mothername = 'Mom'
		end
		gt 'kid', 'start'
	end
}

$play = {
	cla
	*clr
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/pc/activities/baby_play.jpg"></center>'
	'You get down on the floor with your <<$sdtext[ks]>> and begin to make face and noises to your little <<$polreb[ks]>>.'
	'<<$kidname[ks]>> giggles in delight and squirms on the floor in response to you.'
	'After a while you get up and straighten your clothes before going on with your day.'
	pcs_mood += 20
	minut += 20
	act 'Done':
		gt 'kid', 'start'
	end
}

$totplay = {
	cla
	*clr
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/pc/activities/tot_play.jpg"></center>'
	'"<<$pcs_mothername>>! <<$pcs_mothername>>! <<$pcs_mothername>>! <<$pcs_mothername>>!" Yells <<$kidname[ks]>>, "Come play with me!"'
	'"Ok <<$kidname[ks]>>!" You reply.'
	'You pull out a bin of blocks and spend a period of time playing and building with your child.'
	'After a short time, <<$kidname[ks]>> pushes over the small tower you have built, snickers and runs off'
	'You get up, straighten up your clothes, laugh, and move on with your day'
	pcs_mood += 20
	minut += 20
	act 'Done':
		gt 'kid', 'start'
	end
}
$tottv = {
	cla
	*clr
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/tv/kid_tv.jpg"></center>'
	'You pull <<$kidname[ks]>> up onto the couch with you and cuddle up to watch TV'
	'You watch a cartoon featuring a Rabbit, a Duck, and a Pig that is quite silly'
	pcs_mood += 10
	minut += 60
	act 'Done':
		gt 'kid', 'start'
	end
}
$kidtv = {
	cla
	*clr
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/tv/kid_tv2.jpg"></center>'
	'You attempt to curl up on the couch with <<$kidname[ks]>>'
	'"<<$pcs_mothername>>! I''m trying to watch TV!" <<$kidname[ks]>> says.'
	'"Well I am gonna watch with you." You reply, followed by tickling <<$kidname[ks]>> to open up a spot on the couch for you to sit.'
	'You watch a kids TV show about two American teenagers that barely catches your interest.'
	minut += 60
	act 'Done':
		gt 'kid', 'start'
	end
}

$homework = {
	cla
	*clr
	if polkid[k] = 0:
			$kidPosPronn[ks] = 'her'
		else
			$kidPosPronn[ks] = 'him'
	end
	gs 'stat'
	'<center><b><font color="maroon"><<$kidname[ks]>> <<$pcs_lastname>></font></b></center>'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/kolka/event/homework/homework1.jpg"></center>'
	'You see that <<$kidname[ks]>> is working on a pile of homework. They give kids so much homework now days.'
	'"Does my little darling want <<$pcs_mothername>> to help you with some of that?" you ask'
	'<<$kidname[ks]>> replies, "<<$pcs_mothername>>?!... <<$pcs_mothername>> stop treating me like a baby... but yes, I would like some help."'
	'<br>'
	'You sit down at the table next to <<$kidname[ks]>> and begin to help <<$kidPosPronn[ks]>> with the homework'
	if pcs_grades > 50:
		'The homework is completed quickly thanks to the attention you payed in school. An A should be an easy achievement.'
	else
		'You really wish you had payed more attention in school. The homework is not overly complex but is still not the easiest thing for you.'
	end
	homework[ks] = daystart
	minut += rand(30,80)
	act 'Done':
		gt 'kid', 'start'
	end
}

if $ARGS[0] = 'start':
	gs 'stat'
	if $pcs_mothername = '':
		$pcs_mothername = input("What name do you wish your children to call you by? (Leave it blank for ''Mom'')")
		if $pcs_mothername = '': $pcs_mothername = 'Mom'
	end
	act 'Leave':
		cla
		killvar 'ks'
		gt $loc, $metka
	end
	if kid > 0:k = 0
	if lactation['active'] > 0 and pain['nipples'] > 60:
		'You cannot breast feed right now. Your nipples are too sore.'
	end
	:loop
	if k < kid:
		gs 'kid','kidlist'
		k += 1
		jump 'loop'
	end
	'Child Selected: <<$kidname[ks]>>'
	if ks < (kid - 1):
		'<a href="exec:ks += 1& gt ''kid'', ''start'' ">Select Next Child</a>'
		act 'Select Next Child': ks += 1 & gt 'kid', 'start'
	end
	if ks > 0:
		'<a href="exec:ks -= 1& gt ''kid'', ''start'' ">Select Previous Child</a>'
		act 'Select Previous Child': ks -= 1 & gt 'kid', 'start'
	end
end

if $ARGS[0] = 'kidlist':
		if kidage[k] < 1:
			if yearkid[k] = year:
				if (month - monthkid[k]) < 1 and (day-daykid[k]) < 7:
					kiddaycalc[k] = day - daykid[k]
					if kiddaycalc[k] = 1:
						$kidagetext[k] = '<<kiddaycalc[k]>> day old'
					else
						$kidagetext[k] = '<<kiddaycalc[k]>> days old'
					end
				elseif (month - monthkid[k]) < 1 :
					kiddaycalc[k] = (day - daykid[k]) / 7
					if kiddaycalc[k] = 1:
						$kidagetext[k] = '<<kiddaycalc[k]>> week old'
					else
						$kidagetext[k] = '<<kiddaycalc[k]>> weeks old'
					end
				else
					kidmonthcalc[k] = (month - monthkid[k])
					if kidmonthcalc[k] = 1:
						$kidagetext[k] = '<<kidmonthcalc[k]>> month old'
					else
						$kidagetext[k] = '<<kidmonthcalc[k]>> months old'
					end
				end
			else
				if month = 1 and monthkid[k] = 12:
					if 31 - daykid[k] + day < 7:
						kiddaycalc[k] = 31 - daykid[k] + day
						if kiddaycalc[k] = 1:
							$kidagetext[k] = '<<kiddaycalc[k]>> day old'
						else
							$kidagetext[k] = '<<kiddaycalc[k]>> days old'
						end
					elseif 31 - daykid[k] + day >= 7 :
						kiddaycalc[k] = 31 - daykid[k] + day / 7
						if kiddaycalc[k] = 1:
							$kidagetext[k] = '<<kiddaycalc[k]>> week old'
						else
							$kidagetext[k] = '<<kiddaycalc[k]>> weeks old'
						end
					elseif daykid[k] > day:
						$kidagetext[k] = '<<kidmonthcalc[k]>> month old'
					end
				else
					if daykid[k] - day <= 0:
						kidmonthcalc[k] = (12 - monthkid[k] + month)
					else
						kidmonthcalc[k] = (11 - monthkid[k] + month)
					end
					if kidmonthcalc[k] = 1:
						$kidagetext[k] = '<<kidmonthcalc[k]>> month old'
					else
						$kidagetext[k] = '<<kidmonthcalc[k]>> months old'
					end
				end
			end
		else
			if kidage[k] >= 1:
			$kidagetext[k] = '<<kidage[k]>> year old'
			else
			$kidagetext[k] = '<<kidage[k]>> years old'
			end
		end
		if $polreb[k] = 'girl':
			$sdtext[k] = 'daughter'
		elseif $polreb[k] = 'boy':
			$sdtext[k] = 'son'
		end
		if polkid[k] = 0:
			$kidPosPro[k] = 'her'
		else
			$kidPosPro[k] = 'his'
		end
		if polkid[k] = 0:
			$kidPosProC[k] = 'Her'
		else
			$kidPosProC[k] = 'His'
		end
		if polkid[k] = 0:
			$kidPosProPN[k] = 'She'
		else
			$kidPosProPN[k] = 'He'
		end
		if ks = k:
			'<font color= #06992D>Your <<$sdtext[k]>> <<$kidname[k]>> is <<$kidagetext[k]>>. <<$kidposproC[k]>> date of birth is <<monthkid[k]>>/<<daykid[k]>>/<<yearkid[k]>>.</font>'
		else
			'Your <<$sdtext[k]>> <<$kidname[k]>> is <<$kidagetext[k]>>. <<$kidposproC[k]>> date of birth is <<monthkid[k]>>/<<daykid[k]>>/<<yearkid[k]>>.'
		end

		if kidage[k] < 1:
			if hour >= 12 and hour < 18:
				$babyloc = 'onto the blanket'
				'<<$kidname[k]>></a> is laying on a blanket'
			else
				$babyloc = 'into the crib'
				'<<$kidname[k]>> is asleep, lying in <<$kidPosPro[k]>> crib.'
			end

		elseif kidage[k] >= 1 and kidage[k] < 7:
			if hour >= 9 and hour < 21 and rkidloc < 8:
			$totloc = 'playing with toys on the floor'
			'<<$kidname[k]>> is playing with toys.'
			elseif hour < 9 or hour >= 21:
			'<<$kidname[k]>> is asleep in <<$kidPosPro[k]>> bed.'
			else
			$totloc = 'watching TV'
			'<<$kidname[k]>> is watching TV.'
			end
		elseif kidage[k] >= 7 and kidage[k] < 18:
			if hour >= 21 or hour < 8:
				'<<$kidname[k]>> is asleep in <<$kidPosPro[k]>> bed.'
			elseif hour >= 8 and hour < 9:
				'<<$kidname[k]>> is getting ready for school.'
			elseif hour >= 9 and hour < 16:
				'<<$kidname[k]>> is at school.'
			elseif hour >= 16 and hour < 18 and homework[k] < daystart:
				$kidloc = 'working on homework'
				'<<$kidname[k]>> is working on homework.'
			else
				$kidloc = 'watching TV'
				'<<$kidname[k]>> is watching TV.'
			end
		end
		if kid > 0 and kidage[ks] < 1:

			if lactation['active'] > 0:
				act 'Breast Feed <<$kidname[ks]>>':dyneval($breastfeed)
			end
			if $babyloc = 'onto the blanket':
				act 'Play with <<$kidname[ks]>> (0:20)': dyneval($play)
			end
		end
		if kid > 0 and kidage[ks] >=1 and kidage[ks] <7:
			if $totloc = 'playing with toys on the floor':
				act 'Play with <<$kidname[ks]>>(0:30)':dyneval($totplay)
			elseif $totloc = 'watching tv':
				act 'Watch TV with <<$kidname[ks]>>(1:00)':dyneval($tottv)
			end
		end
		if kid > 0 and kidage[ks] >=7 and kidage[ks] < 18:
			if $kidloc = 'watching TV':
				act 'Watch TV with <<$kidname[ks]>> (1:00)':dyneval($kidtv)
			elseif $kidloc = 'working on homework':
				act 'Help <<$kidname[ks]>> with homework.':dyneval($homework)
			end
		end
			"<hr>"
	end

--- kid ---------------------------------

