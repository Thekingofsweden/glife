# vokzalG
$metka = $ARGS[0]
$loc = $CURLOC
CLOSE ALL
if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

clr
gs 'stat'
gs 'time'
'<center><h4>Pavlovsky''s train station</h4></center>'
'<center><img src="images/etogame/vokzal.jpg"></center>'
'Pavlovsky''s modest train station. You can enter the central hall of the <a href="exec:minut += 3 & gt ''vokzalGin''">train station</a>, in which you can find Pavlovsky''s police station, and a post office. Also attached to the train station is the <a href="exec:minut += 5 & gt ''gkafe''">cafe</a> where your mother works.'
'The front of the train station has a large square, where you can find a <a href="exec:minut += 1 & gt ''Grinok''">market</a> on most days.'
'Pavlovsky''s only and rather run-down <a href="exec:minut += 1 & gt ''HotelHole''">hotel</a> is just across the square, where travelers can spend the night. You know they''re often looking for maids.'

if kidaloa = 0:
	podrand = rand(0, 5)

	if money >= 5000 and hour >= 8 and hour <= 20 and podrand = 0 and monthKidalo ! month:
		cls
		monthKidalo = month
		gs 'stat'
		'<center><h4>Pavlovsky''s train station</h4></center>'
		'<center><img src="images/etogame/vokzal.jpg"></center>'
		'When you arrive at the train station, it''s impossible to not notice a girl holding a microphone and a man following her with a large camera. They''re coming straight towards you. The girl beckons you to come closer and glances over at towards the man holding the camera, who gives her a subtle nod.'
		'She turns back to you and happily exclaims: "Hello! We''re shooting a pilot for TV. It''s going to be a novel program: it is a show in which we hand out gifts to random strangers, and then film their response. It''s called "Happiness in Russia", we''re hoping it will catch on all over the world. We have chosen you to be part of our pilot, congratulations!"'
		'She grabs hold of a large box the camera man handed to her. It''s beautifully wrapped, with a nice bow to boot. She whispers to you: "This box contains a brand new laptop! Just act surprised when you open it!"'

		act 'Wow, this must be your lucky day!':
			cls
			gs 'stat'
			'<center><h4>Pavlovsky''s train station</h4></center>'
			'<center><img src="images/etogame/vokzal.jpg"></center>'
			'You happily take the box from the girl''s hands as the man starts filming. The girl holds the microphone closer to her mouth, and invites you to give some shoutouts to relatives and friends while you''re on TV. Then she points the microphone at you. You gladly accept the invitation and address your friends and family enthusiastically while looking at the camera, sincerely hoping you did not leave anyone out. Then the man turns the camera away for a second.'
			'The girl tells you: "Unfortunately, the government doesn''t just let us give away items for nothing. We''re legally obligated to have our winners pay VAT over the amount of money their gift is worth. In your case, this comes down to 5000 rubles."'

			act 'Pay the VAT and check out your new laptop':
				cls
				money -= 5000
				kidaloa = 1
				if manna > 5: manna = 5
				gs 'stat'
				'<center><h4>Pavlovsky''s train station</h4></center>'
				'<center><img src="images/etogame/vokzal.jpg"></center>'
				'You quickly give them the money and enthusiastically begin to pull at the bow. When you finally manage to tear the wrapping paper off and open the box, you only find a brick wrapped in newspaper. You look up trying to find the man and girl to get your money back, but they are nowhere to be found. You feel terrible for being scammed out of your money, and angrily stuff the box in a garbage can before you move on.'
				
				act 'Return to the train station':minut += 15 & gt $curloc
			end
			act 'Say you can''t afford it right now':
				cls
				gs 'stat'
				'<center><h4>Pavlovsky''s train station</h4></center>'
				'<center><img src="images/etogame/vokzal.jpg"></center>'
				'With some regret in your voice, you apologize to the man and girl and explain that you can''t afford to pay the VAT right now. They try to explain to you that this could very well be a one-time opportunity and that you are missing out on a free laptop, but you have to decline. You simply cannot afford to spend the money you have right now on a frivolous purchase like a laptop, even if it comes really cheap.'
				'Sympathizing with you, they promise to look for you if they ever come back to Pavlovsky for another episode of the show.'
				
				act 'Thank them and leave': minut +=15 & gt $curloc
			end
		end
		act 'Say you have no time':
			cls
			gs 'stat'
			kidaloa = 1
			'<center><h4>Pavlovsky''s train station</h4></center>'
			'<center><img src="images/etogame/vokzal.jpg"></center>'
			'Even though you definitely wouldn''t mind a new laptop, you simply don''t have time to talk to them right now. You politely decline and walk away before they can protest. A few minutes later you see the two of them hastily running across the square, while some poor guy opens the fancily packed box only to find a brick in it. By the time he realizes he got scammed, they are long gone. Looks like you dodged a bullet there!'
			
			act 'Return to the station' :minut += 15 & gt $curloc
		end	
		exit
	end
end
		
		
if kidalob = 0:
	podrand = rand(0, 5)	
	if money >= 1000 and hour >= 8 and hour <= 20 and podrand = 5 and glamur > 0 and monthKidalo ! month:
		cls
		monthKidalo = month
		gs 'stat'
		'<center><h4>Pavlovsky''s train station</h4></center>'
		'<center><img src="images/etogame/vokzal.jpg"></center>'
		'As you walk across the square towards the station, you notice two well dressed men staring at you. After some brief consideration they come towards you, and you can just about hear what they''re saying: "Wow, she''s just the model we need. She has this... Claudia Schiffer feel to her, don''t you agree?"'
		'You can see the other man nod just as they get close to you, and one of the men stops you: "Excuse me, miss. We are opening a new fashion store in the city and are looking for a model to feature in our advertisements. We have been looking for just the right girl all day and... I think we just found her!"'
		'The second man nods enthusiastically as the first man continues: "We would be able to offer you 10.000 rubles for an extensive photo shoot, featuring our clothing line."'
		
		act 'You''re not interested':minut += 15 & gt $curloc
		
		act 'Agree to do the photo shoot':
			cls
			gs 'stat'
			'Wow, this offer sounds amazing! There is no way you can decline. The guys take you to the back of the railway station, and the guy who hasn''t said anything to you pulls out an expensive looking camera, mounting it on a tripod near some trees.'
			'The first guy approaches you and says: "The shoot will be against a natural background. We''ve done market research, and have determined that the picturesque background of the outdoors is by far the most desirable option for our campaign. There is only one problem... your dress is not what we are looking for at all. You''ll have to change."'
			'The guy opens the trunk of his car and gives you a rather cheap looking sundress. "You can change your clothes inside the car, no one will notice", he says.'
			
			act 'Refuse and leave, you''re not taking off your clothes!':minut += 15 & gt $curloc
			
			act 'Change into the sundress':
				cls
				!! ths will either add the sundress or simply set it to a certain damage level
				sarafan[3] = 1
				sarafanH[3] = 20
				gs 'clothing', 'wear', 'sarafan', 3
				
				gs 'stat'
				'You quickly get into the car and take off your dress, and put on the dress the man gave you. You give your <<$titsize>> breasts a squeeze through the fabric, adjusting the dress a bit so it fits well around them for the photo shoot.'
				
				act 'Pose for the camera':
					cls
					kidalob = 1
					gs 'stat'
					'You get out of the car leaving your possessions in there, figuring it''s easier to just collect them when you change back to your regular clothes. You get out and walk to the tree the camera is aimed at. The guy who hasn''t said a word mans the camera, while the first guy instructs you how to stand in several poses. "Very nice, very nice... now turn your back to the camera!" The camera clicks several times. "Excellent. Now tilt your head backwards and close your eyes." You do as instructed, waiting for the now familiar click of the camera. It never comes.'
					
					act 'Ask what''s taking so long':
						cls
						money = 0
						if manna>50: manna = 50
						gs 'stat'
						'After a while you turn around and open your eyes, wanting to ask the guys what''s taking so long. You open your eyes right on time to see the car, with the guys in it, speed off and zip around a corner. Then you realize: all your stuff is still on the back seat of the car! Slowly realizing you have just been scammed out of your good dress and all of your money, you sit down on a tree stump and hold your head in your hands for a minute or two, pondering how you could''ve been so stupid as to believe them.'
						
						act 'Return to the train station':minut += 15 & gt $curloc
					end
				end
			end
		end
	exit
		
	end
end

gs 'family'

act 'Return to Pavlovsky':minut += 15 & gt 'gorodok'
act 'Exit to the northern highway':minut += 15 & nroad = 20 & gt 'road'

--- vokzalG ---------------------------------

