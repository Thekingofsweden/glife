# street

music_loop = 0
CLOSE ALL
$metka = ''
$loc = 'street'
$location_type = 'public_outdoors'
$menu_loc = 'street'
$menu_arg = ''
menu_off = 0
killvar '$locclass'

if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3',30
	end
end

!all random events move to [street_events], and display them before show location. by illume
if streetevent_hour + 2 < hour:gt'street_event','street', 'main'

'<center><h2>St. Petersburg</h2></center>'
'<center><b>Residential Area</b></center>'
if month >= 11 or month <= 3:
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/city/residential/streetw.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/residential/streetwn.jpg"></center>'
	end
else
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/city/residential/street.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/residential/street_night.jpg"></center>'
	end
end

gs'street_event','street', 'extras'

if opusk > 0 or gnewQW = 5:
	if rand(0,9) = 0:gt 'gopnew', 'start5'
	if home_owned[1] > 0:'Near the entrance to where you live there is something written on the wall. It says:<b>"<<$pcs_nickname>> from apartment 69 is a cheap slut!"</b>'
end

gs 'stat'
palMir = 0
bordysh = 0
housrA = 0
obdysh = 0
tanhouse1 = 0
obMir = 0
houseob = 0
borMir = 0
$onobjsel = 'onobjsel'

if gopdeath = 1 and vsehsdaladay ! daystart:
	over = 6
	gt 'gameover'
	exit
end

housrA = 0

if car > 0 and cardrive = 1: 'In the parking lot is <a href="exec:GS ''carF'', ''start''">your <<$car>></a>.'

if home_owned[1] > 0:
	act '<b>Go home</b>':
		gorand = rand(0, 100)

		if (loh > 0 or gnewQW = 7) and gopdolg > 0 and hour >= 20 and gorand >= 80:
			gt 'gopnew', 'start'
			exit
		end

		if (opusk > 0 or gnewQW = 5) and hour >= 20 and gorand >= 80 and gopopusday ! daystart:
			gt 'gopnew', 'start'
			exit
		end
		minut += 1
		gt 'korr'
	end
end

act 'Wait':gs 'obj_din', 'wait'

gs 'taxi'

if hour >= 8 and hour <= 22:'You can see the <a href="exec:$locM = ''street'' & minut += 1 & gt ''kiosk'', ''start''">kiosk</a> selling cigarettes and magazines.'
if Enable_tablemap = 0:
	if katkey = 1:'<a href="exec:gt ''katspalnya'', ''start''">Kats</a> apartment'
	if IrinaQW >= 1:'<a href="exec:gt ''IrinaRoom'', ''start''">Irenes</a> apartment is not far away from yours.'
	if hour >= 16 and hour < 22 and dimaQW > 0:'<a href="exec:gt ''dima'', ''start''">Dimas</a> apartment'
	if klofQW >= 2:'<a href="exec:gt ''klofdomhouse'', ''start''">Joras</a> apartment'
else
	st_count=0 & $show_table=''
	if katkey = 1:gs'show_table','<a href="exec:gt ''katspalnya'', ''start''">Kats</a> apartment','3'
	if IrinaQW >= 1:gs'show_table','<a href="exec:gt ''IrinaRoom'', ''start''">Irenes</a> apartment is not far away from yours.','3'
	if hour >= 16 and hour < 22 and dimaQW > 0:gs'show_table','<a href="exec:gt ''dima'', ''start''">Dimas</a> apartment','3'
	if klofQW >= 2:gs'show_table','<a href="exec:gt ''klofdomhouse'', ''start''">Joras</a> apartment','3'
	if hour >= 10 and hour < 22 and lesbiQW = 1:
		gs'show_table','<a href="exec:gt ''lesbidomhouse'', ''start''">Natalya Petrovnas</a> apartment.'
	elseif lesbiday ! daystart and subdom = 2:
		gs'show_table','<a href="exec:gt ''lesbisubhouse'', ''start''">Your slaves</a> apartment.','3'
	elseif lesbiday ! daystart and hour >= 8 and hour < 22 and lesbiQW >= 2 and lesbiQW < 13:
		gs'show_table','<a href="exec:gt ''lesbidomhouse'', ''start''">Mistress''</a> apartment.','3'
	elseif lesbiday ! daystart and hour >= 8 and hour < 22 and lesbiQW >= 13:
		gs'show_table','<a href="exec:gt ''lesbimistress'', ''start''">Mistress''</a> apartment.','3'
	end
	if st_count > 0 :'<TABLE BORDER=1><<$show_table>></TR></TABLE>'
end

if hour >= 5 and hour <= 23:'The <a href="exec: minut += 5 & gt ''metro'', ''start''">Metro</a> Station is within a 5 minute walk from here.'
if hour >= 8 and hour <= 20:'The <a href="exec: minut += 3 & gt ''shop'', ''start''">supermarket</a> is not far from here.'
if (hour >= 8 and hour <= 20) or preg = 2:'The <a href="exec:gt ''poli'', ''start''">clinic</a> is also nearby.'
if hour >= 6 and hour <= 9 and military = 1 and week = 6:'There is a <a href="exec:minut += 60 & gt ''Military'', ''start''">military bus</a> waiting on the other side of the street.'
if hour >= 8 and hour <= 14 and week ! 1:'You could go shopping at the noisy local <a href="exec: minut += 10 & gt ''rinok'', ''start''">marketplace</a>.'
if hour >= 8 and hour <= 15 and week < 6:'Near the market is a <a href="exec: minut += 10 & gt ''rinok'', ''tailor''">tailor shop</a> where you can get your clothes resized if needed.'
if hour >= 11 and hour < 12 and week > 1 and week < 5 and workKafe = 1:'The side door of <a href="exec: minut += 5 & gt ''kafe'', ''work''">"The Roadhouse"</a> is open, so employees can enter to begin their shift.'
if hour >= 12 and hour <= 22 and week ! 1:'In a side alley, next to the uniquely styled diner <a href="exec: minut += 5 & gt ''kafe'', ''start''">"The Roadhouse"</a>, you can hear the swearing and laughing of some <a href="exec: minut += 5 & gt ''gopnew'', ''start''">gopniks</a>.'
if hour > 22 or hour <= 3:'In one of the parking lots you can hear the swearing and laughing of some <a href="exec:gt ''gopnew'', ''start''">gopniks</a>.'
if clener >= 1 and hour >= 7 and hour <= 22:'There''s a small office building where a <a href="exec: minut += 5 & gt ''clener'', ''start''">cleaning lady</a> is needed.'

'The city''s large <a href="exec:minut += 20 & gt ''park'', ''start''">central park</a> is about a 20-minute walk away.'
'The city''s only <a href="exec:minut += 40 & gt ''lake'', ''start''">lake</a> is just a 40-minute walk away.'
'24 Hour <a href="exec:$loc = ''street'' & gt ''pharmacy'', ''start''">Pharmacy</a>.'

if Enable_tablemap = 0:
	if hour >= 10 and hour < 22 and lesbiQW = 1:
		'<a href="exec:gt ''lesbidomhouse'', ''start''">Natalya Petrovna''s</a> apartment.'
	elseif lesbiday ! daystart and subdom = 2:
		'<a href="exec:gt ''lesbisubhouse'', ''start''">Your slave''s</a> apartment.'
	elseif lesbiday ! daystart and hour >= 8 and hour < 22 and lesbiQW >= 2 and lesbiQW < 13:
		'<a href="exec:gt ''lesbidomhouse'', ''start''">Mistress''</a> apartment.'
	elseif lesbiday ! daystart and hour >= 8 and hour < 22 and lesbiQW >= 13:
		'<a href="exec:gt ''lesbimistress'', ''start''">Mistress''</a> apartment.'
	end
end

if home_owned[1] = 0:
	'In the courtyard of one of the apartment buildings, you see several <a href="exec: minut += 1 & gt ''trashplace''">trash bins</a>.'
else
	'In the courtyard of your <a href="exec: gt ''etoexhib'', ''pos51''">apartment building</a>, you see several <a href="exec: minut += 1 & gt ''trashplace''">trash bins</a>.'
end

if SNarkPriton > 0:'The <a href="exec:minut += 5 & gt ''narkopriton''">drug house</a> is clearly recognizable by the syringes and the trash that lies in front of it.'
if hour >= 8 and hour <= 23 or saunaWorkWhore > 0 or workDisk = 2:'<a href="exec:minut += 10 & gt ''sauna''">The Sauna</a> is a 10 minute walk away. Rumors say the sauna is just the front for a brothel.'

if hour >= 20 or hour <= 4:
	*nl
	'There are prostitutes working in places near here.'
	if pcs_makeup > 2 and PCloStyle = 4 and pcs_inhib >= 40 and (stat['vaginal'] >  0 or pav_slut >= 50):
		act 'Prostitute yourself': gt 'street_walker', 'work'
	elseif pcs_inhib >= 40 and (stat['vaginal'] >  0 or pav_slut >= 50):  
		*nl
		'You could work as a prostitute, but you need to wear appropriate clothes and more than a little bit of makeup.'
	else
		*nl
		'You could work as a prostitute, but you aren''t comfortable with the idea.'
	end
end

--- street ---------------------------------

