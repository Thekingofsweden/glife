# din_bad

$mobile_check = {
	gs 'stat'
	if vladimirday ! daystart and hour >= 15 and hour <= 17:
		if vladimirQW = 20:
			'<b><font color="red">Your phone is ringing</font></b>'
			$caller = 'Vladimir'
			$callerid = 'images/characters/city/vladimir/001.jpg'
			gs 'telefon','phone_ring'
			pl'<center><a href="exec:gt ''qwloc'',''2''"><img src="images/system/phone/call_accept.png"></a>		<a href="exec:vladimirday = daystart & gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
		elseif vladimirQW = 25 and week = 6:
			'<b><font color="red">Your phone is ringing</font></b>'
			$caller = 'Vladimir'
			$callerid = 'images/characters/city/vladimir/001.jpg'
			gs 'telefon','phone_ring'
			pl'<center><a href="exec:gt ''qwloc'',''3''"><img src="images/system/phone/call_accept.png"></a>		<a href="exec:vladimirday = daystart & gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
		elseif vladimirQW = 35 and week = 6:
			'<b><font color="red">Your phone is ringing</font></b>'
			$caller = 'Vladimir'
			$callerid = 'images/characters/city/vladimir/001.jpg'
			gs 'telefon','phone_ring'
			pl'<center><a href="exec:gt ''qwloc'',''4''"><img src="images/system/phone/call_accept.png"></a>		<a href="exec:vladimirday = daystart & gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
		end
	end

	if leonidSlaveDay = daystart and hour >= 6 and hour <= 10 and leonidVisit = 0:
		'<b><font color="red">Your phone is ringing</font></b>'
		$caller = 'Leonid Vostov'
		$callerid = 'images/characters/pavlovsk/resident/leonid/leonidphone.jpg'
		gs 'telefon','phone_ring'
		pl'<center><a href="exec:gt ''leonid'', ''call''"><img src="images/system/phone/call_accept.png"></a>		<a href="exec:leonidSlaveDay = daystart & gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
	end

	if QWdogreiqTimer = 0 and QWdogReiq = 2 and week = 7 and qwdogiventday ! daystart and hour >= 12:
		'<b><font color="red">Your phone is ringing</font></b>'
		$caller = 'Margaret'
		$callerid = ''
		gs 'telefon','phone_ring'
		pl''
		pl''
		pl''
		pl''
		pl''
		pl''
		pl''
		pl''
		pl''
		pl''
		'<center><a href="exec:gt ''qwlocdog''"><img src="images/system/phone/call_accept.png"></a>			 <a href="exec:qwdogiventday = daystart & gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
	end

	!! iterating all lovers
	lover_number = 0
	:loverloop
	if pcs_lovers[lover_number] = 1 and loverday[lover_number] ! daystart and hour > 5 and hour < 20 and ringA = 0 and rand(1,7) > 5:
		if pcs_apprnc < vneshlover[lover_number]:
			loverrelation[lover_number] -= 1
			if loverrelation[lover_number] = 0:
				'<b><font color="red">Your lover hasn''t been calling you lately, they''ve probably grown tired of you.</font></b>'
			else
				'Your lover <<$loverdesc[lover_number]>> has not called you.'
			end
		else
			ring = 1
			ringA = 1
			'<b><font color="red">Your phone is ringing</font></b>'
			PLAY 'sound/old_phone.mp3',30
			$caller = '<<$loverdesc[lover_number]>>'
			$callerid = 'images/characters/shared/'+lover_picture[lover_number]+'.jpg'
			gs 'telefon','phone_ring'
			pl'<center><a href="exec:gt ''telefon'', ''lover'', <<lover_number>>"><img src="images/system/phone/call_accept.png"></a>			 <a href="exec:gt $loc, $metka"><img src="images/system/phone/decline.png"></a></center>'
		end
	end
	lover_number += 1
	if lover_number < arrsize('pcs_lovers'): jump 'loverloop'

	killvar 'lover_number'
}

$d_salf = {
	*clr & cla
	'<center><h1>Tissues</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	if cumloc[13] > 0 or cumloc[11] > 0 or cumloc[16] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 12 & gs ''cum_cleanup'' & cumspclnt = 9 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Wipe your face and hands clean</b></a>'
	if cumloc[14] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 14	& gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Wipe your belly clean</b></a>'
	if cumloc[15] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 16 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Wipe your breasts clean</b></a>'
	if cumloc[0] > 0 or cumloc[1] > 0 or cumloc[3] > 0 or cumloc[4] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 15 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Wipe your ass and pussy clean</b></a>'
	if cumloc[10] > 0 or cumloc[9] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 17 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Wipe your arms and legs clean</b></a>'
	if cumloc[2] > 0 or cumloc[5] > 0 or cumloc[6] > 0 or cumloc[7] > 0: '<a href="exec:salfetka -= 1 & cumspclnt = 6 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Clean your clothing</b></a>'
	if pcs_makeup ! 1: '<a href="exec:salfetka -= 1 & pcs_makeup = 1 & gs ''stat'' & gs ''din_bad'', ''d_bag''"><b>Remove your makeup</b></a>'
	'<a href="exec:gs ''stat'' & gs ''din_bad'', ''d_bag''"><br><b>Put tissues away</b></a>'
	'</td></tr></table></center>'
}
		
$d_mouthwash = {
	*clr & cla
	'<center><h1>Mouth Wash</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath = 1
	mouthwash -= 1
	gs 'stat'
	'Your breath smells minty fresh.'
	''
	act 'Done':gs 'stat' & gs 'din_bad', 'd_bag'
	'</td></tr></table></center>'
}

if $ARGS[0] = 'd_bag':
	*clr & cla
	gs 'stat'
	'<center><h1>Purse</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	if salfetka > 0: 'You have <b><<salfetka>></b> wet wipes left in your purse. <a href="exec:dynamic $d_salf">Use a tissue</a>'
	if painkiller > 0 and pain['total'] > 0 and pain['killer'] = 0: 
		'You have <b><<painkiller>></b> painkiller'+iif(painkiller > 1, 's ', ' ')+'left in your purse. <a href="exec: pain[''killer''] += 1 & painkiller -= 1 & gs ''din_bad'', ''d_bag''">Take One</a>'
	elseif painkiller > 0 and (pain['total'] = 0 or pain['killer'] > 0):
		'You have <b><<painkiller>></b> painkiller'+iif(painkiller > 1, 's ', ' ')+'left in your purse.'
	end
	if mouthwash > 0 and (pcs_breath = 0 or cumloc[12] = 1): 
		'There''s mouthwash in your purse. <b><<mouthwash>></b> uses remaining. <a href="exec:dynamic $d_mouthwash">Use mouthwash</a>'
	elseif mouthwash > 0 and pcs_breath = 1 and cumloc[12] = 0:
		'There''s mouthwash in your purse. <b><<mouthwash>></b> uses remaining.'
	end
	if compact_mirror = 1: 
		'You have a <a href="exec: gt ''mirror'', ''d_bag_mirror''">compact mirror</a> in your purse'+iif(kosmetitka > 0, ' and some portable makeup.', '.')
	elseif compact_mirror = 0 and kosmetitka > 0:
		'You have some portable makeup in your purse, but you need a compact mirror to apply it.'
	end
!! Hair options
	*nl
	if (compact_mirror = 1 or greben = 1) and pcs_hairbsh = 0: 
		'There''s a hairbrush in your purse. <a href="exec:pcs_hairbsh = 1 & gs ''stat'' & gs ''din_bad'', ''d_bag''">Brush your hair</a>'
	elseif (compact_mirror = 1 or greben = 1) and pcs_hairbsh = 1: 
		'There''s a hairbrush in your purse. Your hair doesn''t need brushing right now.'
	end
	if pcs_hairlng > 41 and hscrunch >= 1 and hbraids = 0:
		if hscrunchw = 0 and hpingripw = 0: 'As you have a scrunchy, you can arrange your hair into a <a href="exec:hscrunchw = 1 & gs ''din_bad'', ''d_bag''">ponytail</a>. You have <<hscrunch>> scrunchies left.'
		if hscrunchw = 1: 'Remove the <a href="exec:hscrunchw = 0 & gs ''din_bad'', ''d_bag''">scrunchie</a> to get rid of the ponytail.'
	end
	if pcs_hairlng > 41 and hpingrip >= 1 and hbraids = 0:
		if hscrunchw = 0 and hpingripw = 0: 'You have several clips, pins and grips for you hair. Hence, you can <a href="exec:hpingripw = 1 & gs ''din_bad'', ''d_bag''">updo your hair</a>. You have <<hpingrip>> hair accessories at your disposal.'
		if hpingripw = 1: 'Let your hair <a href="exec:hpingripw = 0 & gs ''din_bad'', ''d_bag''">flow free</a>.'
	end
	if pcs_hairlng > 80 and hscrunch >= 2 and hbraids = 0:
		if hscrunchw = 0 and hpingripw = 0: 'You can use two hair ties to make yourself some <a href="exec:hpigtail = 1 & hscrunchw = 2 & gs ''din_bad'', ''d_bag''">pigtails</a>.'
		if hscrunchw = 2: '<a href="exec:hpigtail = 0 & hscrunchw = 0 & gs ''din_bad'', ''d_bag''">Remove</a> your scrunchies and let your hair flow free again.'
	end
	
	*nl
	if prezik + prezikcount + preziksabo > 0:
		if preziktype = 2 or preziksabo > 0:
			'You have <b><<iif(preziktype = 2, prezikcount, prezikcount+prezik)>></b> normal condoms left, and <b><<iif(preziktype = 2, prezik+preziksabo, preziksabo)>></b> sabotaged <a href="exec:dynamic $din_Switch_Condom_Menu">condoms left in your purse.</a>'
		elseif preziktype < 2 and preziksabo = 0:
			'You have <b><<prezik+prezikcount>></b> <a href="exec:dynamic $din_Switch_Condom_Menu">condoms left in your purse.</a>'
		end
	else
		'You are out of condoms.'
	end
	if lubri > 0:
		'You have <b><<lubri>></b> squirts of lubricant available'
		if auto_anal_lube = 0:
			'Automatically use for anal sex: <a href="exec:auto_anal_lube = 1 & gs ''stat'' & gs ''din_bad'', ''d_bag''">No</a>'
		else
			'Automatically use for anal sex: <a href="exec:auto_anal_lube = 0 & gs ''stat'' & gs ''din_bad'', ''d_bag''">Yes</a>'
		end
		if auto_vag_lube = 0:
			'Automatically use for vaginal sex: <a href="exec:auto_vag_lube = 1 & gs ''stat'' & gs ''din_bad'', ''d_bag''">No</a>'
		else
			'Automatically use for vaginal sex: <a href="exec:auto_vag_lube = 0 & gs ''stat'' & gs ''din_bad'', ''d_bag''">Yes</a>'
		end
	end
	*nl
	if amphetamine > 0:	'You have <b><<amphetamine>></b> tablets of amphetamine left in your purse. <a href="exec: gs ''drugs'', ''amphetamine'' & gs ''din_bad'', ''d_bag''">Take One</a>'
	if joint > 0: 'You have enough weed for <b><<joint>></b> joints.'
	if tabletki > 0 and pillsleft[ptype] > 0:
		'You have <b><<tabletki>></b> unopened packages of <a href="exec:dynamic $d_tabletkied">birth control pills</a> and <b><<pillsleft[ptype]>></b> pills left in your opened package.'
	elseif tabletki	= 0 and pillsleft[ptype] > 0:
		'You have <b><<pillsleft[ptype]>></b> <a href="exec:dynamic $d_tabletkied">birth control pills</a> left in your purse.'
	elseif tabletki > 0 and pillsleft[ptype] = 0:
		'You have <b><<tabletki>></b> unopened packages of <a href="exec:dynamic $d_tabletkied">birth control pills</a> in your purse.'
	else
		'You are out of birth control pills.'
	end
	if morning_after_pill > 0:
		if morning_after_pill = 1:
			'You have one <a href="exec:gs''medical_din'',''morning_after_pill''">morning after pill</a>.'
		else
			'You have <b><<birth_control[''morning_after'']>></b> <a href="exec:gs''medical_din'',''morning_after_pill''">morning after pills</a>.'
		end
	else
		'You have no morning after pills.'
	end
	if mentats_have > 0 and mentats_dose < 3:
		'<a href="exec: gs ''drugs'', ''mentats'' & gs ''din_bad'', ''d_bag'' & *pl ''<font color = green><br>You swallow the neurobooster pill.</font>''">Use a neurobooster pill in your purse.</a> There are <b><<mentats_have>></b> pills left. The warning label says not to take more than one per day.'
	elseif mentats_have > 0:
		'There are <b><<mentats_have>></b> neurobooster pills left. You really shouldn''t take any more pills today.'
	end
	if steroid_have > 0:'<a href="exec:dynamic $use_steroids & gs ''din_bad'', ''d_bag''">Use steroids.</a> You have <b><<steroid_have>></b> steroids left in your purse.'
	if bcream_have > 0:'<a href="exec:dynamic $use_bcream & gs ''din_bad'', ''d_bag''">Apply breast cream.</a> You have <b><<bcream_have>></b> cream left in your purse.'
	if aphrodisiac_have > 0:'<a href="exec: gs ''drugs'', ''aphrodisiac'' & gs ''din_bad'', ''d_bag'' & *pl $aphrodisiac_msg & *pl $min_arousal_msg & killvar ''$min_arousal_msg''">Chew aphrodisiac gum.</a> You have <b><<aphrodisiac_have>></b> gum left in your purse.'

	if refillable_bottle = 4: 
		'You carry your sports water bottle in your purse. It''s still completely full. <a href="exec: refillable_bottle = 3 & pcs_hydra += 75 & cumspclnt = 2 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''">Drink</a>'
	elseif refillable_bottle = 3:
		'You carry your sports water bottle in your purse. It''s two-thirds full. <a href="exec: refillable_bottle = 2 & pcs_hydra += 75 & cumspclnt = 2 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''">Drink</a>'
	elseif refillable_bottle = 2:
		'You carry your sports water bottle in your purse. It''s one-third full. <a href="exec: refillable_bottle = 1 & pcs_hydra += 75 & cumspclnt = 2 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''">Drink</a>'	
	elseif refillable_bottle = 1:
		'You carry your sports water bottle in your purse. It''s empty.'
	end
	if bottle > 0: 'There''s a bottle of water in your purse. <a href="exec:bottle = 0 & pcs_hydra += 100 & cumspclnt = 2 & gs ''cum_cleanup'' & gs ''stat'' & gs ''din_bad'', ''d_bag''">Drink</a>'
	if buterbrod > 0: 'There''s a sandwich in your purse. <a href="exec:buterbrod = 0 & pcs_energy += 40 & cumspclnt = 2 & gs ''cum_cleanup'' & fat += 1 & gs ''stat'' & gs ''din_bad'', ''d_bag''">Eat</a>'
	if strapon = 1 and strapnumber = 0:
		'You have a strapon harness in your purse, but you don''t have a dildo attached to it'+iif(dildo + middildo + largedildo + bigdildo + extradildo + superdildo + maddildo > 0, '. You keep your dildos in your desk at home.', ', because you don''t own one.')
	elseif strapon = 1 and strapnumber ! 0:
		'You have a strapon harness in your purse and a <<(strapnumber +1)*5>>cm attached to it.'
	end
	if wine = 1 and alkowine = 0:'There''s a bottle of <a href="exec:dynamic $dinalkowin1">wine</a> in your purse.'
	if wine = 1 and alkowine = 1:'There''s a half-bottle of <a href="exec:dynamic $dinalkowin2">wine</a> in your purse.'
	if foto = 1:'Your purse contains a strange photo'
	if pattest > 0: 'You have <b><<pattest>></b> unused paternity test'+iif(used_pattest > 1, 's ', ' ')+'in your purse.'
	if used_pattest > 0: 'You have <b><<used_pattest>></b> <a href="exec:dynamic $test_purse">used</a> paternity test'+iif(used_pattest > 1, 's ', ' ')+'in your purse.'
	*nl
	if $pursepantytype ! '' or $pursebratype ! '': 'You also have '+iif($pursepantytype ! '','<a href="exec:view''<<func(''$panty_image'', $pursepantytype, pursepantynumber)>>''">a pair of panties</a>','')+iif($pursepantytype ! '' and $pursebratype ! '',' and ','')+iif($pursebratype ! '','<a href="exec:view''<<func(''$bra_image'', $pursebratype, pursebranumber)>>''">a bra</a>','')+' in your purse, which you can put on in the nearest bathroom.'
	
	act 'Return':gt 'obj_din', 'menu_exit'
	'</td></tr></table></center>'
end

$dinalkowin1 = {
	*clr & cla
	alkowine = 1
	minut += 5
	gs 'drugs', 'alcohol', 'wine'
	gs'stat'
	'<center><img <<$set_imgh>> src="images/shared/food/alkowin.jpg"></center>'
	*nl
	'You take the wine bottle out of your purse and slowly drink half of it. You struggle quite a lot to put the cork back before being able to make it.'
	act 'Continue':gs 'din_bad', 'd_bag'
}

$dinalkowin2 = {
	*clr & cla
	alkowine = 0
	wine = 0
	minut += 5
	gs 'drugs', 'alcohol', 'wine'
	gs'stat'
	'<center><img <<$set_imgh>> src="images/shared/food/alkowin.jpg"></center>'
	*nl
	'You take out the remaining half wine bottle out of your purse, slowly drinking the rest of it. As you finish you throw the empty bottle away.'
	act 'Continue':gs 'din_bad', 'd_bag'
}

$din_Switch_Condom_Menu = {
	dynamic $din_Update_Condom_Counts
	*clr & cla
	'<center><h1>Condoms</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	i = 0
	'You have <<iif(preziktype < 2, prezik+prezikcount, prezikcount)>> normal condoms left and <<iif(preziktype = 2, prezik+preziksabo, preziksabo)>> sabotaged condoms left.'
	*nl
	if preziktype > 0:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 1">Switch to using normal condoms</a>.'
	end
	if preziktype ! 1:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 2">Stop using condoms</a>.'
	end
	if preziktype ! 2:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 3">Switch to using sabotaged condoms</a>.'
	end
	if prezikcount > 0 or preziktype = 0:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 4">Sabotage a condom</a>.'
	end
	*nl
	act 'Return':gs 'din_bad', 'd_bag'
	'</td></tr></table></center>'
}

$din_Table_Condom_Menu = {
	dynamic $din_Update_Condom_Counts
	*clr & cla
	'<center><h1>Condoms</h1></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/bedroom/stol.jpg"></center>'
	*nl
	i = 1
	'You have <<iif(preziktype < 2, prezik+prezikcount, prezikcount)>> normal condoms left and	<<iif(preziktype = 2, prezik+preziksabo, preziksabo)>> sabotaged condoms left.'
	*nl
	if preziktype > 0:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 1">Switch to using normal condoms</a>.'
	end
	if preziktype ! 1:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 2">Stop using condoms</a>.'
	end
	if preziktype ! 2:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 3">Switch to using sabotaged condoms</a>.'
	end
	if prezikcount > 0 or preziktype = 0:
		'<a href="exec:dynamic $din_Do_Condom_Counts, 4">Sabotage a condom</a>.'
	end
	*nl
	act 'Return':gt 'stol', 'bc'
}

!! Condom System
!! prezik: Total number of condoms sveta is using (normal or damaged).
!! prezikcount: Number of normal condoms sveta isn''t using.
!! preziksabo: Number of damaged condoms seveta isn''t using.
!! preziktyp: 0 - Use condoms / 1 - don''t use condoms / 2 - use damaged condoms
!! to check if sveta wants to use a condom use if prezik > 0:

$din_Do_Condom_Counts = {
	*clr & cla
	'<center><h1>Condoms</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	if ARGS[0] = 1:
		'Use a condom the next time you have sex.'
		if preziktype = 2: preziksabo = prezik
		prezik = prezikcount
		prezikcount = 0
		preziktype = 0
	elseif ARGS[0] = 2:
		'You will not use a condom the next time you have sex.'
		if preziktype = 0: prezikcount = prezik
		if preziktype = 2: preziksabo = prezik
		prezik = 0
		preziktype = 1
	elseif ARGS[0] = 3:
		'Use your sabotaged condom the next time you have sex.'
		if preziktype = 0: prezikcount = prezik
		prezik = preziksabo
		preziksabo = 0
		preziktype = 2
	elseif ARGS[0] = 4:
		if preziktype ! 0 and prezikcount > 0:
			preziksabo +=1
			prezikcount -=1
			'You use a needle to punch holes in one of the condoms.'
		elseif preziktype = 0 and prezik > 0:
			preziksabo +=1
			prezik -=1
			'You use a needle to punch holes in one of the condoms.'
		else
			'You do not have any condoms to sabotage.'
		end
	end
	if i = 0:
		dynamic $din_Switch_Condom_Menu
	else
		dynamic $din_Table_Condom_Menu
	end
	'</td></tr></table></center>'
}

$din_Update_Condom_Counts = {
	if prezik + prezikcount + preziksabo > 0: prezikmsg = 1
	if preziktype = 0:
		if prezikcount > 0:
			prezik += prezikcount
			prezikcount = 0
		end
		if prezik <= 0:
			'You are out of condoms.'
			prezik = 0
			prezikcount = 0
			preziktype = 1
		end
	elseif preziktype = 1:
		if prezik > 0:
			prezikcount += prezik
			prezik = 0
		end
	elseif preziktype = 2:
		if preziksabo > 0:
			prezik += preziksabo
			preziksabo = 0
		end
		if prezik <= 0 and prezikcount <= 0:
			if prezikmsg = 1:prezikmsg = 2
			prezik = 0
			preziksabo = 0
			prezikcount = 0
			preziktype = 1
		elseif prezik <= 0 and prezikcount > 0:
			'You have run out of sabotaged condoms so you will have to use normal condoms.'
			prezik = prezikcount
			prezikcount = 0
			preziktype = 0
		end
	end
}

$d_tabletkied = {
	*clr & cla
	'<center><h1>Birth Control</h1></center>'
	'<center><img <<$set_imgh>> src="<<FUNC(''$purse_image'',	$currentpursetype, currentpursenumber)>>"></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td cellspacing="0" cellpadding="0" valign="top">'
	tabloc = 0
	if tabletkiday < daystart and thinkpreg = 0 and knowpreg = 0: '<a href="exec:dynamic $d_tabletkieda, 1">Take the birth control pill now.</a>'
	if tabletkicheck = 0 and thinkpreg = 0 and knowpreg = 0:
		'<a href="exec:dynamic $d_tabletkieda, 2">Automatically take your birth control pills.</a>'
	elseif tabletkicheck = 1:
		'<a href="exec:dynamic $d_tabletkieda, 3">Stop automatically taking your birth control pills.</a>'
	end
	'<a href="exec:dynamic $d_tabletkieda, 4">Examine the birth control pills.</a>'
	if tabletkioddk = 1: '<a href="exec:dynamic $d_tabletkieda, 5">Dump the strange pills.</a>'
	act 'Return':gs 'din_bad', 'd_bag'
	'</td></tr></table></center>'
}

$d_tabletkiedt = {
	*clr & cla
	'<center><h1>Birth Control</h1></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/bedroom/stol.jpg"></center>'
	*nl
	tabloc = 1
	if tabletkiday < daystart and thinkpreg = 0 and knowpreg = 0: '<a href="exec:*clr & dynamic $d_tabletkieda, 1">Take the birth control pill now.</a>'
	if tabletkicheck = 0 and thinkpreg = 0 and knowpreg = 0:
		'<a href="exec:*clr & dynamic $d_tabletkieda, 2">Automatically take your birth control pills.</a>'
	elseif tabletkicheck = 1:
		'<a href="exec:*clr & dynamic $d_tabletkieda, 3">Stop automatically taking your birth control pills.</a>'
	end
	'<a href="exec:*clr & dynamic $d_tabletkieda, 4">Examine the birth control pills.</a>'
	if tabletkioddk = 1: '<a href="exec:*clr & dynamic $d_tabletkieda, 5">Dump the strange pills.</a>'
	act 'Return':gt 'stol', 'bc'
}

$d_tabletkieda = {
	cla
	if ARGS[0] = 1:
		tabletkipd += tabletkipt
		if tabletkiday ! daystart:
			gs 'din_bad','takepill'
			tabletkiday = daystart
			tabletkipt = 0
			tabletkipi = 0
		else
			pcs_health -= 1
		end
		pilldaychk = 1
		*clr
		dynamic $d_tabletkied
		'You take the birth control pill.'
		gs 'stat'
	elseif ARGS[0] = 2:
		'You will do your best to remember to take your birth control pills every day.'
		tabletkicheck = 1
		gs 'stat'
	elseif ARGS[0] = 3:
		'You will take your birth control pills as you desire.'
		tabletkicheck = 0
		gs 'stat'
	elseif ARGS[0] = 4:
		if pcs_intel + pillsdays <= rand(30,95):
			'These pills appear to be normal birth control pills.'
			tabletkioddk = 0
			gs 'stat'
		else
			if ptype = 0: $pillprob = 'nothing unusual about them.'
			if ptype = 1: $pillprob = 'that the pills have expired months ago.'
			if ptype = 2: $pillprob = 'that the label has spelling mistakes and the logo is backwards.'
			if ptype = 3: $pillprob = 'that the pills are discolored and there is no expiry date.'
			if ptype = 4: $pillprob = 'that the pills are all the same color.'
			if ptype = 5: $pillprob = 'that the pills seem to be smaller than normal.'

			'Upon taking a closer look at your birth control pills, you notice <<$pillprob>>'

			if ptype > 0: tabletkioddk = 1
			gs 'stat'
			if tabloc = 0:
				act 'Return':gs 'din_bad', 'd_bag'
			else
				act 'Return':gt 'stol', 'bc'
			end
		end
	elseif ARGS[0] = 5:
		'You get rid of the strange pills.'
		tabletkioddk = 0
		pillsleft[ptype] = 0
		tabletkioddp = 0
		gs 'stat'
	end

	if tabloc = 0:
		act 'Return':gs 'din_bad', 'd_bag'
	else
		act 'Return':gt 'stol', 'bc'
	end
}

$d_cycreport_choice = {
	*clr & cla
	'<center><h1>Fertility Cycle</h1></center>'
	'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
	'<td width="800" cellspacing="0" cellpadding="0" valign="top">'
	dynamic $d_cycreport_update
	'<center><b>Current Status:</b></center>'
	'<center><<$cycreport_txt>></center>'
	'</td></tr></table></center><center><table cellspacing="0" cellpadding="20" valign="top"><tr><td width="250" valign="top">'
	if $cycreport_txt ! 'In labour':
		'<center><b>Change Status because:</b></center>'
		if thinkpreg = 0:
			if knowpreg = 1:
				'<a href="exec: dynamic $d_cycreport_actor, 2 & gs ''stat''">I''m pregnant.</a>'
				'<a href="exec: dynamic $d_cycreport_actor, 1 & gs ''stat''">I can''t be pregnant no matter the evidence.</a>'
			else
				'<a href="exec: dynamic $d_cycreport_actor, 2 & gs ''stat''">I think I''m pregnant.</a>'
			end
		elseif thinkpreg = 1:
			if knowpreg = 1:
				'<a href="exec: dynamic $d_cycreport_actor, 1 & gs ''stat''">I can''t be pregnant no matter the evidence.</a>'
			else
				'<a href="exec: dynamic $d_cycreport_actor, 1 & gs ''stat''">I''m not pregnant.</a>'
			end
		end
		if mesec > 0 and thinkpreg ! 1 and knowpreg ! 1:
			'<a href="exec: dynamic $d_cycreport_actor, 3">I''m menstruating.</a>'
		elseif thinkpreg ! 1:
			'<a href="exec: dynamic $d_cycreport_actor, 4">I just recently menstruated.</a>'
			'<a href="exec: dynamic $d_cycreport_actor, 5">I think my menstrual cycle has stopped.</a>'
		end
		if thinkpreg = 1 or knowpreg = 1:
			'<a href="exec: dynamic $d_cycreport_actor, 6">I think my due date is...</a>'
			'<a href="exec: dynamic $d_cycreport_actor, 7">I think the father is...</a>'
		end
		if knowpregloss = 1:
			'<a href="exec: dynamic $d_cycreport_actor, 8">I just had an abortion.</a>'
		elseif knowpregloss > 1 or thinkpreg = 1:
			'<a href="exec: dynamic $d_cycreport_actor, 8">I think I lost a baby.</a>'
		end
	elseif cycle = 5:
		'Broken water and contractions, it''s pretty clear what is going on. You need to get to a hospital ASAP.'
	else
		dynamic $d_cycreport_actor, 10
	end
	act 'Exit':gs 'stat' & gt $loc, $metka
	'</td></tr></table></center>'
}

$d_cycreport_actor = {
	!! Svetlana has decided she is not pregnant
	if ARGS[0] = 1:
		$cycreport_txt = 'It has been <<temp>> days since you last remember having a period.'
		if knowpreg = 1:
			denypreg = 1
			thinkpreg = 0
		else
			denypreg = 0
			thinkpreg = 0
		end
		dynamic $d_cycreport_update
		gs 'stat'
		dynamic $d_cycreport_choice
	!! Svetlana has decided she is pregnant.
	elseif ARGS[0] = 2:
		thinkpreg = 1
		denypreg = 0
		dynamic $d_cycreport_update
		gs 'stat'
		dynamic $d_cycreport_choice
	!! Svetlana is Menstrating. 
	elseif ARGS[0] = 3:
		thinkpreg = 0
		denypreg = 0
		daylastperiod = daystart
		dynamic $d_cycreport_update
		gs 'stat'
		dynamic $d_cycreport_choice
	!! Svetlana is remembering her last period. 
	elseif ARGS[0] = 4:
		thinkpreg = 0
		denypreg = 0
		*clr & cla
		'<center><h1>Fertility Cycle</h1></center>'
		'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
		'<td width="250" cellspacing="0" cellpadding="0" valign="top">'
		'How many days ago was it you had your last period?'
		'Currently: <<pertemp>>'
		'<a href="exec: pertemp += 1 & dynamic $d_cycreport_actor, 4">+1</a> <a href="exec: pertemp += 5 & dynamic $d_cycreport_actor, 4">+5</a> <a href="exec: pertemp += 10 & dynamic $d_cycreport_actor, 4">+10</a> <a href="exec: pertemp = 0 & dynamic $d_cycreport_actor, 4">Reset</a> <a href="exec: daylastperiod = (daystart - pertemp) & dynamic $d_cycreport_update & gs ''stat'' & dynamic $d_cycreport_choice">Accept</a>'
		act 'Return':dynamic $d_cycreport_choice
		act 'Exit':gs 'stat' & gt $loc, $metka
		'</td></tr></table></center>'
	!! Svetlana thinks she doesn''t have periods anymore. Good birth control has this effect (as does pregnancy.)
	elseif ARGS[0] = 5:
		if knowpreg = 1:
			denypreg = 1
		else
			denypreg = 0
		end
		thinkpreg = 0
		$cycreport_txt = 'You do not believe you have periods any more.'
		gs 'stat'
		dynamic $d_cycreport_choice
	!! Svetlana is trying to figure out her due date.
	elseif ARGS[0] = 6:
		*clr & cla
		'<center><h1>Fertility Cycle</h1></center>'
		'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
		'<td width="250" cellspacing="0" cellpadding="0" valign="top">'
		'How many days ago was it you had your last period?'
		'Currently: <<pertemp>>'
		'<a href="exec: pertemp += 1 & dynamic $d_cycreport_actor, 6">+1</a> <a href="exec: pertemp += 5 & dynamic $d_cycreport_actor, 6">+5</a> <a href="exec: pertemp += 10 & dynamic $d_cycreport_actor, 6">+10</a> <a href="exec: pertemp = 0 & dynamic $d_cycreport_actor, 6">Reset</a> <a href="exec: daylastperiod = daystart - pertemp & dynamic $d_cycreport_update & gs ''stat'' & dynamic $d_cycreport_choice">Accept</a>'
		act 'Return':dynamic $d_cycreport_choice
		act 'Exit':gs 'stat' & gt $loc, $metka
		'</td></tr></table></center>'
	!! Svetlana is trying to guess who is the babydaddy.
	elseif ARGS[0] = 7:
		*clr & cla
		'<center><h1>Fertility Cycle</h1></center>'
		'<center><table cellspacing="0" cellpadding="20" valign="top"><tr>'
		'<td width="500" cellspacing="0" cellpadding="0" valign="top">'
		
		potfather_temp[1] = arrsize('$wombpotfath')
		if potfather_temp[1] > 0:
			potfather_counter = 0
			'List of potential fathers:'
			:potfather_loop
			if $wombpotfath[potfather_temp] ! 'unknown':
				$potfather_temp[2] = '<a href="exec: $wombthfath = ''<<$npc_usedname[$wombpotfath[potfather_counter]]>>'' & $wombthfathID = ''<<$wombpotfath[potfather_counter]>>'' & dynamic $d_cycreport_actor, 7 & gs ''stat''"><<$npc_usedname[$wombpotfath[potfather_counter]]>> (<<$wombpotfath[potfather_counter]>>)</a>'
			else
				$potfather_temp[2] = '<a href="exec: $wombthfath = ''<<$npc_usedname[$wombpotfath[potfather_counter]]>>'' & $wombthfathID = ''<<$wombpotfath[potfather_counter]>>'' & dynamic $d_cycreport_actor, 7 & gs ''stat''">Unknown (-)</a>'
			end
			'Potential father (<<potfather_counter + 1>>): <<$potfather_temp[2]>>'
			potfather_counter += 1
			if potfather_counter < arrsize('$wombpotfath'): jump 'potfather_loop'
		else
			'There are no potential fathers.'
		end
		
		killvar 'potfather_counter'
		
		'<br>  <a href="exec:dynamic $d_cycreport_choice & gs ''stat''">Accept</a>'
		'</td></tr></table></center>'
		
		act 'Return':dynamic $d_cycreport_choice
		act 'Exit':gs 'stat' & gt $loc, $metka
		
		!! This is the old code that was broken, keeping it for now (julzor)
		!! {
		tempmax = arrsize('$cumfthname')-1
		tempcurr = 0
		sumcum = 0
		*nl
		:potfathloop
			if cumtime[tempcurr] > 1:$cum = 'cumshots' else $cum = 'cumshot'
			if cumtime[tempcurr] > 0: '<<$npc_usedname[$cumfthname[tempcurr]]>> (<<$cumfthname[tempcurr]>>) - <<cumtime[tempcurr]>> <<$cum>>' & sumcum += cumtime[tempcurr]
		if tempcurr < tempmax:tempcurr += 1 & jump 'potfathloop'
		if sumcum > 1:$cum = 's' else $cum = ''
		'<br>You think you have had <<sumcum>> load<<$cum>> of cum shot<<$cum>> in your womb altogether that could have gotten you pregnant.'
		'You suspect the father being: <<$wombthfath>><br>'
		*nl
		tempcurr = 0
		:pickfathloop
			if cumtime[tempcurr] > 0:
				if $npc_usedname[$cumfthname[tempcurr]] <> '':
					'<a href="exec: $wombthfath = ''<<$npc_usedname[$cumfthname[tempcurr]]>>'' & $wombthfathID = ''<<$cumfthname[tempcurr]>>'' & dynamic $d_cycreport_actor, 7 & gs ''stat''"><<$npc_usedname[$cumfthname[tempcurr]]>> (<<$cumfthname[tempcurr]>>)</a>'
				else
					' <a href="exec: $wombthfath = ''<<$npc_usedname[$cumfthname[tempcurr]]>>'' & $wombthfathID = ''<<$cumfthname[tempcurr]>>'' & dynamic $d_cycreport_actor, 7 & gs ''stat''">Unknown (-)</a>'
				end
			end
		if tempcurr < tempmax:tempcurr += 1 & jump 'pickfathloop'
		killvar 'sumcum'
		killvar 'cum'
		killvar 'tempmax'
		killvar 'tempcurr'
		}
	!! Just recently lost a baby to abortion, miscarriage, or think you did because your period finally arrived.
	elseif ARGS[0] = 8:
		thinkpreg = 0
		$cycreport_txt = 'Your body is recovering from the trauma of pregnancy after your miscarrage.'
		dynamic $d_cycreport_update
		gs 'stat'
		dynamic $d_cycreport_choice
	!! Just recently gave birth.
	elseif ARGS[0] = 10:
		temp = arrsize('Babyptype')
		temp -= 1
		$cycreport_txt = 'You are currently recovering from childbirth. You gave birth on <<monthkid[temp]>> / <<daykid[temp]>> / <<yearkid[temp]>>.'
		gs 'stat'
	end
}

$d_cycreport_update = {
	killvar 'temp'
	if thinkpreg = 1 or knowpreg = 1: dynamic $d_cycreport_upduedate
	!! Vagblood in a future update to account for babydamage and miscarrages.
	if mesec > 0 or vagblood > 0:
		$cycreport_txt = 'You are currently bleeding.'
	else
		if cyccustom = 1:
			if preg = 1 and knowpreg = 1:
				$cycreport_txt = 'You are pregnant. Your due date is around <<$pregduedate>>.'
				if $wombthfath ! 'unknown': 
					$cycreport_txt = '<<$cycreport_txt>> You suspect the father being <<$wombthfath>>.'
				end
			elseif knowpregloss = 1:
				$cycreport_txt = 'Your body is recovering from the trauma of pregnancy after your abortion.'
			elseif knowpregloss = 2:
				$cycreport_txt = 'Your body is recovering from the trauma of pregnancy after your miscarrage.'
			elseif knowpregrecover = 1:
				$cycreport_txt = 'Your body is recovering from the trauma of childbirth.'
			elseif menoage <= age and cycle = 6:
				$cycreport_txt = 'You have gone through menopause.'
			else
				temp[1] = daystart-daylastperiod
				if tabletkishot > 0:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=green>You are currently protected by a birth control shot.</font>'
				elseif birth_control['implant_status'] > 1 and pillcon2 >= 30000:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=green>Your implant protects you from getting pregnant.</font>'
				elseif birth_control['implant_status'] > 1 and pillcon2 < 30000:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=red>Your implant is not yet effective.</font>'				
				elseif pillcon2 > 30000:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=green>You are currently on the birth control pill.</font>'
				elseif pillcon2 > 8000 and ((FocH > (Foch_Max - 96)) or ovulate > 0):
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. You are currently on the pill but it is not yet effective. <font color=red>It is currently a dangerous day.</font>'
				elseif pillcon2 > 8000:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. You are currently on the pill but it is not yet effective.'
				elseif daystart = tabletkiday:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. You have taken the pill but it will be several weeks before it is fully effective.'
				elseif FocH > (Foch_Max - 96) or ovulate > 0:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=red>It is currently your fertile stage.</font>'
				elseif birth_control['implant_status'] = 1:
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period. <font color=red>Your implant stopped working. You need a new one.</font>'
				else
					$cycreport_txt = 'It has been <<temp[1]>> days since the start of your last period.'
				end
			end
		elseif denypreg = 1:
			if pregchem < 525:
				denypreg = 1
				thinkpreg = 0
				$cycreport_txt = 'There is nothing wrong, your cycle is just irregular.'
			elseif pregchem < 1863:
				denypreg = 1
				thinkpreg = 0
				$cycreport_txt = 'You have a strange flu making you feel ill.'
			elseif pregchem < 4000:
				denypreg = 1
				thinkpreg = 0
				$cycreport_txt = 'Your strange flu still hasn''t gone away. As soon as it does you plan to exercise and lose the excess fat you''be been building up.'
			elseif pregchem < 6000:
				denypreg = 1
				thinkpreg = 0
				$cycreport_txt = 'Your weight just won''t come off...'
			elseif pregchem < 7500:
				denypreg = 1
				thinkpreg = 0
				$cycreport_txt = 'People are rude, constantly asking you when you are due. You are not pregnant!'
			elseif pregchem >= 7500:
				thinkpreg = 0
				$cycreport_txt = 'Surely if you were pregnant, you''d have had the baby by now, so you can''t possibly be pregnant.'
			else
				temp = daystart-daylastperiod
				$cycreport_txt = 'It has been <<temp>> days since you last remember having a period.'
			end
		elseif cyccustom = 0:
			if $cycreport_txt ! 'In labour':
				if thinkpreg = 1 or knowpreg = 1:
					if knowpreg = 0: $temp = ' think you'
					if $wombthfath = 'unknown' or $wombthfath = '':
						$cycreport_txt = 'You<<$temp>> are pregnant. You aren''t yet sure who the father is. Based on your last remembered period, your due date is probably around <<$pregduedate>>.'
					else
						$cycreport_txt = 'You<<$temp>> are pregnant with <<$wombthfath>>''s baby. Based on your last remembered period, your due date is probably around <<$pregduedate>>.'
					end
				elseif $cycreport_txt = 'Your body is recovering from the trauma of pregnancy after your miscarrage.':
					if knowpregloss = 1: $cycreport_txt = 'Your body is recovering from the trauma of pregnancy after your abortion.'
				elseif $cycreport_txt ! 'You do not believe you have periods any more.':
					temp = daystart-daylastperiod
					$cycreport_txt = 'It has been <<temp>> days since you last remember having a period.'
				end
				killvar 'temp'
			end
		end
	end
}


$d_cycreport_upduedate = {
	!! implant_calc: Calculation of an implantation date in the uterus (Average is 5 days after ovulation)
	implant_calc = daystart - lastovulation + 5
	
	!! Human gastation period: 280 days
	gastation_time = 280 / (1 + pregspeedcheat)
	
	temp = gastation_time - implant_calc
	pregdueday = day
	pregduemonth = month
	pregdueyear = year
	
	temp += pregdueday
	pregdueday = 0
	
	:pregduedateloop
	if pregduemonth = 1 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 2 and temp > 28:
		pregduemonth += 1
		temp -= 28
	elseif pregduemonth = 3 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 4 and temp > 30:
		pregduemonth += 1
		temp -= 30
	elseif pregduemonth = 5 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 6 and temp > 30:
		pregduemonth += 1
		temp -= 30
	elseif pregduemonth = 7 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 8 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 9 and temp > 30:
		pregduemonth += 1
		temp -= 30
	elseif pregduemonth = 10 and temp > 31:
		pregduemonth += 1
		temp -= 31
	elseif pregduemonth = 11 and temp > 30:
		pregduemonth += 1
		temp -= 30
	elseif pregduemonth = 12 and temp > 31:
		pregduemonth = 1
		pregdueyear += 1
		temp -= 31
	else
		pregdueday = temp
		temp = 0
	end
	if temp > 0: jump 'pregduedateloop'
	$pregduedate = '<<pregdueday>> '
	if pregduemonth = 1:
		$pregduedate += 'Jan'
	elseif pregduemonth = 2:
		$pregduedate += 'Feb'
	elseif pregduemonth = 3:
		$pregduedate += 'Mar'
	elseif pregduemonth = 4:
		$pregduedate += 'Apr'
	elseif pregduemonth = 5:
		$pregduedate += 'May'
	elseif pregduemonth = 6:
		$pregduedate += 'Jun'
	elseif pregduemonth = 7:
		$pregduedate += 'Jul'
	elseif pregduemonth = 8:
		$pregduedate += 'Aug'
	elseif pregduemonth = 9:
		$pregduedate += 'Sep'
	elseif pregduemonth = 10:
		$pregduedate += 'Oct'
	elseif pregduemonth = 11:
		$pregduedate += 'Nov'
	elseif pregduemonth = 12:
		$pregduedate += 'Dec'
	end
	$pregduedate += ' <<pregdueyear>>'
}

$d_pregmovement = {
	if denypreg = 0 and pregchem > 0:
		if thinkpreg > 0 and knowpreg = 0:
			knowpreg = 1
			if kid <= 0:
				'Your heart flutters as you feel a baby moving around inside you for the first time.'
			else
				'The movement inside you confirms what you already suspected. You are pregnant once again.'
			end
		elseif knowpreg > 0:
			temprand = rand(0,2)
			if temprand = 0:
				if pregchem > 5000:
					'Your baby visibly moves around inside you.'
				else
					'You feel movement deep inside you as your baby shifts around.'
				end
			elseif temprand = 1:
				if pregchem > 5000:
					'You wince as your baby delivers a vicious kick to your insides.'
				else
					'You feel your baby kicking.'
				end
			elseif temprand = 2:
				if pregchem > 5000:
					'You feel your baby kicking, look down, and see your belly twitching from it''s movements.'
				else
					'You feel your baby kicking.'
				end
			end
		else
			if cyccustom = 1:
				if pregchem < 4000:
					if kid <= 0 :
						'You feel a weird and strange fluttering sensation around inside your stomach. Perhaps you should see the doctor?'
					else
						'It feels as if a baby is moving inside you. Are you pregnant?'
					end
				else
					knowpreg = 1
					if kid <= 0:
						'It''s become pretty obvious. You are pregnant.'
					else
						'It''s become pretty obvious. You are pregnant once again.'
					end
				end
			end
		end
	elseif pregchem > 0:
		if kid > 0 and PregChem > 3903:
			'You try to deny it, but you just can''t any more. The large belly, the movement, you are pregnant once again.'
			denypreg = 0
			dynamic $d_cycreport_upduedate
			$cycreport_txt = 'You are pregnant.'
		else
			'You feel movement deep inside you. It''s probably just gas.'
		end
	end
}

$use_steroids = {
	if steroid_dose < 3:
		'<font color = blue>You swallow the orange pill.</font>'
		steroid_dose += 1
		steroid_counter += 1
		steroid_have -= 1
	else
		'You really shouldn''t take any more pills today.'
	end
}

$use_bcream = {
	if bcream_used < 1:
		'<font color = #DB7093>You smear the cream onto your breasts and rub it in.</font>'
		bcream_used = 1
		bcream_have -= 1
	else
		'Using any more cream won''t have any effect.'
	end
}

if $ARGS[0] = 'takepill':

	if tabletki > 0 and pillsleft[ptype] = 0:
		tabletkirand = rand(1, tabletki)
		if tabletkirand <= tabletkiold:
			tabletki -=1
			tabletkiold -=1
			ptype = 1
			pillsleft[ptype] = 28
		elseif tabletkirand <= tabletkiold + tabletkifake:
			tabletki -=1
			tabletkifake -=1
			ptype = 2
			pillsleft[ptype] = 28
		elseif tabletkirand <= tabletkiold + tabletkifake + tabletkirej:
			tabletki -=1
			tabletkirej -=1
			ptype = 3
			pillsleft[ptype] = 28
		elseif tabletkirand <= tabletkiold + tabletkifake + tabletkirej + tabletkisug:
			tabletki -=1
			tabletkisug -=1
			ptype = 4
			pillsleft[ptype] = 28
		elseif tabletkirand <= tabletkiold + tabletkifake + tabletkirej + tabletkisug + tabletkifert:
			tabletki -=1
			tabletkifert -=1
			ptype = 5
			pillsleft[ptype] = 28
		else
			tabletki -=1
			ptype = 0
			pillsleft[ptype] = 28
		end
		killvar 'tabletkirand'
	end
	!! added empty week to allow menstruation - it take effect only when the pills are taken automatically
	if placebopart <= 0 or tabletkicheck ! 1:
		if ptype = 0:
			pillsleft[ptype] -= 1
			pcs_skin += 1
			pillcon += ((pillcon + 15000) / 25) + 4000
		elseif ptype = 1:
			pillsleft[ptype] -= 1
			pcs_skin += rand(0,1)
			pillcon += (( pillcon+ 15000) / 25)+ rand(2000,4000)
		elseif ptype = 2:
			pillsleft[ptype] -= 1
			pcs_skin += rand(0,1)
			pillcon += ((pillcon + 15000) / 25)+ rand(500,2000)
		elseif ptype = 3:
			pillsleft[ptype] -= 1
			pcs_skin += rand(0,1)
			if rand(0,4) = 0: pain['tummy'] += rand(5,10)
			pillcon += ((pillcon + 15000) / 25)+ rand(2000,6000)
		elseif ptype = 4:
			pillsleft[ptype] -= 1
		elseif ptype = 5:
			pillsleft[ptype] -= 1
			pcs_skin += 1
			EggRH += rand(10,80)
		end
		
		!!pillcon2 is the ideal state, PC considers this the believed pillcon value
		pillcon2 += ((pillcon2 + 15000) / 25) + 4000
		if pillcon2 > 40000: pillcon2 = 40000
		if pillcon > 40000: pillcon = 40000
	end
end

--- din_bad ---------------------------------
