# wardrobe

if $ARGS[0] = 'start':
	PShoQuality = ShoQuality
	PShoHeels = ShoHeels
	PShoCut = ShoCut
	PShoStyle = ShoStyle
	$wloc = 'wardrobe'
	menu_off = 1
	gs 'stat'
	if CloBra = 1: gs 'bras', 'remove'
	if CloPanties = 1: gs 'panties','remove'
	if $sports_clothing_name = '':$sports_clothing_name = 'default sports outfit'
	if $clothingworntype = 'misc_outfits' and clothingwornnumber = '1':
		gs 'clothing', 'swamp_clothes', 0
		gs 'clothing', 'strip'
	end
	if ward_img_hgt = 0:ward_img_hgt = 150
	'<h3><center><b><font color="maroon">Wardrobe overview</b></h3>'
	if $pursepantytype ! '' or $pursebratype ! '':
		'<center>You put the '+iif($pursepantytype ! '','panties','')+iif($pursepantytype ! '' and $pursebratype ! '',' and ','')+iif($pursebratype ! '','bra','')+' from your purse back in the wardrobe.</center><br>'
		killvar '$pursepantytype' & killvar 'pursepantynumber'
		killvar '$pursebratype' & killvar 'pursebranumber'
	end
	'<center><font color="maroon">These are the clothes you are currently wearing - Click on an item to change it</font></center><center><table border=0 cellspacing=<<ward_img_hgt/25>> cellpadding=5>'
	*p '<TH>Bra worn</TH><TH>Outfit worn</TH><TH>Coat (only worn outside when cold)</TH>'
	*P '<TR><TD><center><a href="exec:gt ''bras'', ''view_bra_list'', ''wardrobe''"><img Height = <<ward_img_hgt>> src="<<FUNC(''$body_image'',''bra'')>>"></a></center></TD><TD><center><a href="exec:gt ''clothing_QV'', ''list''"><img Height = <<ward_img_hgt * 2>> src="<<FUNC(''$body_image'',''clothes'')>>"></a></center></TD><TD><center>'
	if $coatworntype = 'none':
		'<a href="exec:gt ''coats'', ''view_coat_list'', ''wardrobe''">Search the wardrobe</a>'
	else
		'<a href="exec:gt ''coats'', ''view_coat_list'', ''wardrobe''"><img Height = <<ward_img_hgt * 2>> src="<<FUNC(''$body_image'', ''coat'')>>"></a>'
	end
	'</center></TD><TR><TD><center>'
	if $braworntype ! 'none':
		'<a href="exec:gs ''bras'', ''remove'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD><TD><center>'
	if $clothingworntype ! 'nude':
		'<a href="exec:gs ''clothing'', ''strip'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD><TD><center>'
	if $coatworntype ! 'none':
		'<a href="exec:gs ''coats'', ''remove'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD>'

	*p '<TR><TH>Panties worn</TH><TH>Shoes (Only worn outside)</TH><TH>Purse worn</TH>'
	*P '<TR><TD><center><a href="exec:gt ''panties'', ''view_panty_list'', ''wardrobe''"><img Height = <<ward_img_hgt>> src="<<FUNC(''$body_image'',''panties'')>>"></a></center></TD><TD><center><a href="exec:gt ''shoes'', ''view_shoe_list'', ''wardrobe''"><img Height = <<ward_img_hgt>> src="<<FUNC(''$body_image'', ''shoes'')>>"></a></center></TD><TD><center>'
	if bag = 1:
		'<a href="exec:gt ''purses'', ''view_purse_list'',''wardrobe''"><img Height = <<ward_img_hgt>> src="<<FUNC(''$purse_image'', $currentpursetype, currentpursenumber)>>"></a>'
	else
		'<a href="exec:gt ''purses'', ''view_purse_list'',''wardrobe''">Search the wardrobe</a>'
	end
	'</center></TD><TR><TD><center>'
	if $pantyworntype ! 'none':
		'<a href="exec:gs ''panties'', ''remove'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD><TD><center>'
	if $shoeworntype ! 'none':
		'<a href="exec:gs ''shoes'', ''strip'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD><TD><center>'
	if bag = 1:
		'<a href="exec:gs ''purses'', ''remove'' & gt ''wardrobe'', ''start''">Remove</a>'
	else
		'Remove'
	end
	'</center></TD><TR><TD><TD><center>'
	if ($clothingworntype ! 'nude' and ($coatworntype ! 'none' or bag > 0)) or ($coatworntype ! 'none' and bag > 0):
		'<a href="exec:gs ''wardrobe'', ''removeall'' & gt ''wardrobe'', ''start''">Remove all items</a>'
	else
		'Remove all items'
	end
	*nl
	'<center><font color="grey">Save/load outfits with "Managed saved clothing sets".</font></center>'
	'<TD></center></TD></table></center>'
	act 'Close wardrobe': killvar '$wloc' & gt $loc, $loc_arg

	act 'Strip all clothing':
		gs 'bras', 'remove'
		gs 'clothing', 'strip'
		gs 'panties', 'remove'
		gs 'shoes', 'strip'
		gt 'wardrobe', 'start'
	end

	act 'Organise your wardrobe': org_loc = 1 & gt 'clothing', 'view_clothing_list', 'wardrobe'

	act 'Manage saved clothing sets': gt 'wardrobe', 'default'

	act 'Set image size for this view':
		ward_img_hgt = input("Enter height in pixels you want for images on this page <br>(Default 150, min 50, max 500)")
		if ward_img_hgt < 50:
			ward_img_hgt = 50
		elseif ward_img_hgt > 500:
			ward_img_hgt = 500
		end
		gt 'wardrobe', 'start'
	end
end

if $ARGS[0] = 'removeall':
	gs 'bras', 'remove'
	gs 'clothing', 'strip'
	gs 'panties', 'remove'
	gs 'shoes', 'strip'
	gs 'purses', 'remove'
	gs 'coats', 'remove'
end

!!---------------------------Oufit stuff----------------------------------

if $ARGS[0] = 'default':
	gs 'stat'
	'<center><b>Default outfits</b></center>'
	'<center><font color="grey">You can overwrite and rename default outfits, but you cannot delete a default outfit.</font></center>'
	*nl
	'<center><table width="600">'
	'<tr><td>'
	'<b><<$sports_clothing_name>>:</b> <a href="exec:gs ''wardrobe'', ''sports_entry_wear''">Wear</a> | <a href="exec:gs ''wardrobe'', ''sports_entry_set''">Overwrite</a> | <a href="exec:$sports_clothing_name = ''default sports outfit'' & gt ''wardrobe'', ''default''">Reset name</a>'
	'</td></tr>'
	'</table></center>'
	'<center><b>Custom outfits</b></center>'
	'<center><font color="grey">Choose your clothing, then select "Add as a new custom set" to save your clothing choice</font></center>'
	*nl
	'<center><table width="600">'
	'<tr><td>'
	if ARRSIZE('defclothingnumber') > 0:
		i = 0
		:loopdefclothing
		'<b><<$def_clothing_name[i]>>:</b> <a href="exec:gs ''wardrobe'', ''default_entry_wear'', <<i>>">Wear</a> | <a href="exec:gs ''wardrobe'', ''default_entry_set'', <<i>>">Overwrite</a> | <a href="exec:gs ''wardrobe'', ''default_entry_rename'', <<i>>">Rename</a> | <a href="exec:gs ''wardrobe'', ''default_entry_delete'', <<i>>">Delete</a> | <a href="exec:gs ''wardrobe'', ''default_entry_up'', <<i>>">↑</a> | <a href="exec:gs ''wardrobe'', ''default_entry_down'', <<i>>">↓</a>'
		i += 1
		if i < ARRSIZE('defclothingnumber'):jump 'loopdefclothing'
	end
	'</td></tr>'
	'</table></center>'
	act 'Return':
		if quick_dress = 1:
			killvar '$wloc'
			killvar 'quick_dress'
			gt $loc, $loc_arg
		else
			gt 'wardrobe', 'start'
		end
	end
	act 'Add as a new custom set': gt 'wardrobe', 'default_entry_set', ARRSIZE('defclothingnumber')
	'<center>Note: Selecting an outfit will show the bonus for equipped shoes, this is only applied when outside.</center>'
end

if $ARGS[0] = 'default_entry_wear':
	gs 'stat'
	default_entry = ARGS[1]

	gs 'clothing_attributes', $defclothingtype[default_entry], defclothingnumber[default_entry]
	if ARRPOS('$CloLosTyp', $defclothingtype[default_entry]) ! -1 and ARRPOS('CloLosNum', defclothingnumber[default_entry]) ! -1:
		msg '<center>You''ve lost part of this set, and are unable to wear all items.<br>Maybe you can find the missing clothes somewhere?</center>'
	else
		gs 'clothing', 'wear', $defclothingtype[default_entry], defclothingnumber[default_entry]
	end

	if dyneval('RESULT = <<$defbratype[default_entry]>>_bras[<<defbranumber[default_entry]>>]') = 0:
		$defbratype[default_entry] = 'none' & defbranumber[default_entry] = 0
	end
	if $defbratype[default_entry] = 'none':
		if $braworntype ! 'none': gs 'bras', 'remove'
	else
		gs 'bras', 'wear', $defbratype[default_entry], defbranumber[default_entry]
	end

	if dyneval('RESULT = <<$defpantytype[default_entry]>>_panties[<<defpantynumber[default_entry]>>]') = 0:
		$defpantytype[default_entry] = 'none' & defpantynumber[default_entry] = 0
	end
	if $defpantytype[default_entry] = 'none':
		if $pantyworntype ! 'none': gs 'panties', 'remove'
	else
		gs 'panties', 'wear', $defpantytype[default_entry], defpantynumber[default_entry]
	end

	if dyneval('RESULT = <<$defshoetype[default_entry]>>_shoe[<<defshoenumber[default_entry]>>]') = 0:
		$defshoetype[default_entry] = 'none' & defshoenumber[default_entry] = 0
	else
		gs 'shoes', 'wear', $defshoetype[default_entry], defshoenumber[default_entry]
	end

	if dyneval('RESULT = <<$defpursetype[default_entry]>>_purses[<<defpursenumber[default_entry]>>]') = 0:
		$defpursetype[default_entry] = 'none' & defpursenumber[default_entry] = 0
	else
		$currentpursetype = $defpursetype[default_entry]
		currentpursenumber = defpursenumber[default_entry]
	end

	if $currentpursetype = 'none':
		bag = 0
	else
		bag = 1
	end

	if dyneval('RESULT = <<$defcoattype[default_entry]>>[<<defcoatnumber[default_entry]>>]') = 0:
		$defcoattype[default_entry] = 'none' & defcoatnumber[default_entry] = 0
	else
		gs 'coats', 'wear', $defcoattype[default_entry], defcoatnumber[default_entry]
	end

	!! default school girl uniform is always allowed to be worn if still in school
	if StoryLine = 1 and SchoolAtestat = 0 and SchoolBlock = 0 and $defclothingtype[default_entry] = 'gm_school' and defclothingnumber[default_entry] = 6:
		cls
		'<center><video autoplay loop src="images/pc/activities/misc/dress_1.mp4"></video></center>'
		'Your put on your <<$def_clothing_name[default_entry]>> outfit without any problems.'
		act 'Return to wardrobe':
			killvar 'default_entry'
			gs 'stat'
			gt 'wardrobe', 'start'
		end
	!! do not own this clothing anymore
	elseif dyneval('RESULT = <<$defclothingtype[default_entry]>>[<<defclothingnumber[default_entry]>>]') = 0:
		cls
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/niko/nikoev/school/strip/strip2.jpg"></center>'
		'As you go to pull your <<$def_clothing_name[default_entry]>> outfit out of your wardrobe you remember you don''t own that piece of clothing anymore. Maybe you should buy it again? Guess you will have to find something else to wear.'
		act 'Return to wardrobe':
			gs 'clothing', 'strip'
			killvar 'default_entry'
			gs 'stat'
			gt 'wardrobe', 'start'
		end
	!! strength of clothing is less than or equal to 0
	elseif dyneval('RESULT = <<$defclothingtype[default_entry]>>_h[<<defclothingnumber[default_entry]>>]') <= 0:
		cls
		'<center><video autoplay loop src="images/characters/pavlovsk/school/girl/vicky/event/vika_game_strip16.mp4"></video></center>'
		'Your <<$def_clothing_name[default_entry]>> outfit has worn out and can''t be used anymore. Guess you will have to find something else to wear.'
		act 'Strip out of worn out clothing':
			gs 'clothing', 'strip'
			killvar 'default_entry'
			gs 'stat'
			gt 'wardrobe', 'start'
		end
	!! clothing exhibition is greater than player exhibition
	elseif CloInhibit > pcs_inhib:
		cls
		'<center><video autoplay loop src="images/locations/pavlovsk/trainstation/bimbo.strip6.mp4"></video></center>'
		'You don''t feel daring enough to wear your <<$def_clothing_name[default_entry]>> outfit. Maybe you will be confident enough in the future? Guess you will have to find something else to wear.'
		act 'Strip out of revealing clothing':
			gs 'clothing', 'strip'
			killvar 'default_entry'
			gs 'stat'
			gt 'wardrobe', 'start'
		end
	!! not exercise clothing, and not loose fitting
	elseif $defclothingtype[default_entry] ! 'danilovich' and CloStyle ! 5:
		!! check for if the hipsize is too far from your hipsize
		dynamic 'RESULT = <<$defclothingtype[default_entry]>>_b[<<defclothingnumber[default_entry]>>]'
		if RESULT < pcs_hips - 8 or RESULT > pcs_hips + 8:
			cls
			iif(RESULT < pcs_hips - 8,'<center><video autoplay loop src="images/locations/pavlovsk/clinic/therapist/dressup.mp4"></video></center>','<center><video autoplay loop src="images/locations/pavlovsk/clinic/therapist/stripping1.mp4"></video></center>')
			'As you put on your <<$def_clothing_name[default_entry]>> outfit you notice it doesn''t fit correctly anymore. It''s ' + iif(RESULT < pcs_hips - 8,'unbearably tight and uncomfortable to wear.','extremely loose and won''t stay in place no matter how hard you try.') + ' You must have ' + iif(RESULT < pcs_hips - 8,'gained','lost') + ' some weight since this was purchased or last tailored. You will have to get this clothing resized before you can wear it again.'
			act 'Strip out of ill-fitting clothing':
				gs 'clothing', 'strip'
				killvar 'default_entry'
				gs 'stat'
				gt 'wardrobe', 'start'
			end
		!! clothing ok to wear after passing hip check
		else
			cls
			'<center><video autoplay loop src="images/pc/activities/misc/dress_1.mp4"></video></center>'
			'Your put on your <<$def_clothing_name[default_entry]>> outfit without any problems.'
			act 'Return to wardrobe':
				killvar 'default_entry'
				gs 'stat'
				gt 'wardrobe', 'start'
			end
		end
	!! clothing ok to wear without needing hip check and passing all other checks
	else
		cls
		'<center><video autoplay loop src="images/pc/activities/misc/dress_1.mp4"></video></center>'
		'Your put on your <<$def_clothing_name[default_entry]>> outfit without any problems.'
		act 'Return to wardrobe':
			killvar 'default_entry'
			gs 'stat'
			gt 'wardrobe', 'start'
		end
	end
end

if $ARGS[0] = 'default_entry_set':
	gs 'stat'
	i = ARGS[1]
	$defclothingtype[i] = $clothingworntype
	defclothingnumber[i] = clothingwornnumber
	$defbratype[i] = $braworntype
	defbranumber[i] = brawornnumber
	$defpantytype[i] = $pantyworntype
	defpantynumber[i] = pantywornnumber
	$defshoetype[i] = $shoeworntype
	defshoenumber[i] = shoewornnumber
	if bag = 1:
		$defpursetype[i] = $currentpursetype
		defpursenumber[i] = currentpursenumber
	else
		$defpursetype[i] = 'none'
	end
	$defcoattype[i] = $coatworntype
	defcoatnumber[i] = coatwornnumber

	$def_clothing_temp = '<<$def_clothing_name[i]>>'
	$def_clothing_name[i] = input ('<center>Enter a name for this outfit set<br><font color="grey">Leaving blank will reuse current name.</font></center>')
	if $def_clothing_name[i] = '' and $def_clothing_temp = '':
		$def_clothing_name[i] = 'Custom <<i>>'
	elseif $def_clothing_name[i] = '':
		$def_clothing_name[i] = '<<$def_clothing_temp>>'
	end
	gt 'wardrobe', 'default'
end

if $ARGS[0] = 'default_entry_rename':
	gs 'stat'
	i = ARGS[1]

	$def_clothing_temp = '<<$def_clothing_name[i]>>'
	$def_clothing_name[i] = input ('<center>Enter a name for this outfit set<br><font color="grey">Leaving blank will reuse current name.</font></center>')
	if $def_clothing_name[i] = '' and $def_clothing_temp = '':
		$def_clothing_name[i] = 'Custom <<i>>'
	elseif $def_clothing_name[i] = '':
		$def_clothing_name[i] = '<<$def_clothing_temp>>'
	end
	gt 'wardrobe', 'default'
end

if $ARGS[0] = 'default_entry_up':
	gs 'stat'
	i = ARGS[1]

	if i = 0:
		j = ARRSIZE('defclothingnumber') - 1
	else
		j = i - 1
	end

	$tempclothingworntype = $defclothingtype[i]
	tempclothingwornnumber = defclothingnumber[i]
	$tempbraworntype = $defbratype[i]
	tempbrawornnumber = defbranumber[i]
	$temppantyworntype = $defpantytype[i]
	temppantywornnumber = defpantynumber[i]
	$tempshoeworntype = $defshoetype[i]
	tempshoewornnumber = defshoenumber[i]
	$tempcurrentpursetype = $defpursetype[i]
	tempcurrentpursenumber = defpursenumber[i]
	$tempcurrentcoattype = $defcoattype[i]
	tempcurrentcoatnumber = defcoatnumber[i]
	$def_clothing_temp = '<<$def_clothing_name[i]>>'

	$defclothingtype[i] = $defclothingtype[j]
	defclothingnumber[i] = defclothingnumber[j]
	$defbratype[i] = $defbratype[j]
	defbranumber[i] = defbranumber[j]
	$defpantytype[i] = $defpantytype[j]
	defpantynumber[i] = defpantynumber[j]
	$defshoetype[i] = $defshoetype[j]
	defshoenumber[i] = defshoenumber[j]
	$defpursetype[i] = $defpursetype[j]
	defpursenumber[i] = defpursenumber[j]
	$defcoattype[i] = $defcoattype[j]
	defcoatnumber[i] = defcoatnumber[j]
	$def_clothing_name[i] = '<<$def_clothing_name[j]>>'

	$defclothingtype[j] = $tempclothingworntype
	defclothingnumber[j] = tempclothingwornnumber
	$defbratype[j] = $tempbraworntype
	defbranumber[j] = tempbrawornnumber
	$defpantytype[j] = $temppantyworntype
	defpantynumber[j] = temppantywornnumber
	$defshoetype[j] = $tempshoeworntype
	defshoenumber[j] = tempshoewornnumber
	$defpursetype[j] = $tempcurrentpursetype
	defpursenumber[j] = tempcurrentpursenumber
	$defcoattype[j] = $tempcurrentcoattype
	defcoatnumber[j] = tempcurrentcoatnumber
	$def_clothing_name[j] = '<<$def_clothing_temp>>'

	gt 'wardrobe', 'default'
end

if $ARGS[0] = 'default_entry_down':
	gs 'stat'
	i = ARGS[1]

	if i = ARRSIZE('defclothingnumber') - 1:
		j = 0
	else
		j = i + 1
	end

	$tempclothingworntype = $defclothingtype[i]
	tempclothingwornnumber = defclothingnumber[i]
	$tempbraworntype = $defbratype[i]
	tempbrawornnumber = defbranumber[i]
	$temppantyworntype = $defpantytype[i]
	temppantywornnumber = defpantynumber[i]
	$tempshoeworntype = $defshoetype[i]
	tempshoewornnumber = defshoenumber[i]
	$tempcurrentpursetype = $defpursetype[i]
	tempcurrentpursenumber = defpursenumber[i]
	$tempcurrentcoattype = $defcoattype[i]
	tempcurrentcoatnumber = defcoatnumber[i]
	$def_clothing_temp = '<<$def_clothing_name[i]>>'

	$defclothingtype[i] = $defclothingtype[j]
	defclothingnumber[i] = defclothingnumber[j]
	$defbratype[i] = $defbratype[j]
	defbranumber[i] = defbranumber[j]
	$defpantytype[i] = $defpantytype[j]
	defpantynumber[i] = defpantynumber[j]
	$defshoetype[i] = $defshoetype[j]
	defshoenumber[i] = defshoenumber[j]
	$defpursetype[i] = $defpursetype[j]
	defpursenumber[i] = defpursenumber[j]
	$defcoattype[i] = $defcoattype[j]
	defcoatnumber[i] = defcoatnumber[j]
	$def_clothing_name[i] = '<<$def_clothing_name[j]>>'

	$defclothingtype[j] = $tempclothingworntype
	defclothingnumber[j] = tempclothingwornnumber
	$defbratype[j] = $tempbraworntype
	defbranumber[j] = tempbrawornnumber
	$defpantytype[j] = $temppantyworntype
	defpantynumber[j] = temppantywornnumber
	$defshoetype[j] = $tempshoeworntype
	defshoenumber[j] = tempshoewornnumber
	$defpursetype[j] = $tempcurrentpursetype
	defpursenumber[j] = tempcurrentpursenumber
	$defcoattype[j] = $tempcurrentcoattype
	defcoatnumber[j] = tempcurrentcoatnumber
	$def_clothing_name[j] = '<<$def_clothing_temp>>'

	gt 'wardrobe', 'default'
end

if $ARGS[0] = 'default_entry_delete':
	gs 'stat'
	i = ARGS[1]
	killvar 'defclothingtype', i
	killvar 'defclothingnumber', i
	killvar 'defbratype', i
	killvar 'defbranumber', i
	killvar 'defpantytype', i
	killvar 'defpantynumber', i
	killvar 'defshoetype', i
	killvar 'defshoenumber', i
	killvar 'defpursetype', i
	killvar 'defpursenumber', i
	killvar 'defcoattype', i
	killvar 'defcoatnumber', i
	killvar 'def_clothing_name', i
	gt 'wardrobe', 'default'
end

!!-- sports outfit stuff

if $ARGS[0] = 'sports_entry_set':
	gs 'stat'
	if $clothingworntype ! 'danilovich' OR $shoeworntype ! 'danilovich':
		MSG 'The sports outfit needs to include exercise clothes and training shoes'
		gt 'wardrobe', 'start'
	else
		gs 'stat'
		$sportsclothingtype = $clothingworntype
		sportsclothingnumber = clothingwornnumber
		$sportsbratype = $braworntype
		sportsbranumber = brawornnumber
		$sportspantytype = $pantyworntype
		sportspantynumber = pantywornnumber
		$sportsshoetype = $shoeworntype
		sportsshoenumber = shoewornnumber
		$sportspursetype = $currentpursetype
		sportspursenumber = currentpursenumber
		$sportscoattype = $coatworntype
		sportscoatnumber = coatwornnumber

		$sports_clothing_temp = '<<$sports_clothing_name>>'
		$sports_clothing_name = input ('<center>Enter a name for this outfit set<br><font color="grey">Leaving blank will reuse current name.</font></center>')
		if $sports_clothing_name = '' and $sports_clothing_temp = '':
			$sports_clothing_name = 'default sports outfit'
		elseif $sports_clothing_name = '':
			$sports_clothing_name = '<<$sports_clothing_temp>>'
		end
		gt 'wardrobe', 'default'
	end
end

if $ARGS[0] = 'sports_entry_wear':
	gs 'stat'

	!gs 'wardrobe', 'prev_entry_set' this does not exist

	gs 'clothing_attributes', 'danilovich_outfits', sportsclothingnumber

	if ARRPOS('$CloLosTyp', 'danilovich') ! -1 and ARRPOS('CloLosNum', sportsclothingnumber) ! -1:
		msg '<center>You''ve lost part of this set, and are unable to wear all items.<br>Maybe you can find the missing clothes somewhere?</center>'
	else
		gs 'clothing', 'wear', 'danilovich', sportsclothingnumber
	end

	if dyneval('RESULT = <<$sportsbratype>>_bras[<<sportsbranumber>>]') = 0: $sportsbratype = 'none' & sportsbranumber = 0
	gs 'bras', 'wear', $sportsbratype, sportsbranumber

	if dyneval('RESULT = <<$sportspantytype>>_panties[<<sportspantynumber>>]') = 0: $sportspantytype = 'none' & sportspantynumber = 0
	gs 'panties', 'wear', $sportspantytype, sportspantynumber

	gs 'shoes', 'wear', 'danilovich', sportsshoenumber

	minut += 2
	gs 'stat'

	!! do not own this clothing anymore
	if dyneval('RESULT = danilovich[<<sportsclothingnumber>>]') = 0:
		cls
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/niko/nikoev/school/strip/strip2.jpg"></center>'
		'As you go to pull your sports outfit out of your wardrobe you remember you don''t own that piece of clothing anymore. Maybe you should buy it again? Guess you will have to find something else to wear.'
		!gs 'wardrobe', 'prev_entry_set' this does not exist
		gs 'clothing', 'strip'
		gs 'stat'
	!! strength of clothing is less than or equal to 0
	elseif dyneval('RESULT = danilovich_h[<<sportsclothingnumber>>]') <= 0:
		cls
		'<center><video autoplay loop src="images/characters/pavlovsk/school/girl/vicky/event/vika_game_strip16.mp4"></video></center>'
		'Your sports outfit has worn out and can''t be used anymore. Guess you will have to find something else to wear.'
		!gs 'wardrobe', 'prev_entry_set' this does not exist
		gs 'clothing', 'strip'
		gs 'stat'
	!! clothing exhibition is greater than player exhibition
	elseif CloInhibit > pcs_inhib:
		cls
		'<center><video autoplay loop src="images/locations/pavlovsk/trainstation/bimbo.strip6.mp4"></video></center>'
		'You don''t feel daring enough to wear your sports outfit. Maybe you will be confident enough in the future? Guess you will have to find something else to wear.'
		!gs 'wardrobe', 'prev_entry_set' this does not exist
		gs 'clothing', 'strip'
		gs 'stat'
	!! clothing ok to wear without needing hip check and passing all other checks
	else
		cls
		'<center><video autoplay loop src="images/pc/activities/misc/dress_1.mp4"></video></center>'
		'You put on your sports outfit without any problems.'
	end
	act 'Return':
		if $wloc = 'wardrobe':
			gt 'wardrobe', 'start'
		elseif $wloc = 'workout':
			gt 'exercise', 'workout'
		else
			gt $loc, $loc_arg
		end
	end
end

if $ARGS[0] = 'sports_outfit':
	gs 'stat'
	if $clothingworntype ! 'danilovich' or $shoeworntype ! 'danilovich':
		act 'Change into: <<$sports_clothing_name>>': gs 'wardrobe', 'sports_entry_wear'
	end
end

if $ARGS[0] = 'school_outfit':
	! check to see if player has created a school uniform outfit
	if ARRSIZE('defclothingnumber') > 0:
		SchoolOutfitLoop = 0
		:FindSchoolOutfit
		gs 'clothing_attributes', $defclothingtype[SchoolOutfitLoop], defclothingnumber[SchoolOutfitLoop]
		! found an outfit that includes a school uniform
		if CloStyle2 = 4:
			tempSchtype = 0
			! if they have worn a uniform recently we will use that rather than the one in the outfit
			if schtype <> 0:	
				tempSchtype = schtype
				$tempSchbrand = $schbrand
			end
			! put the outfit on
			gs 'wardrobe', 'default_entry_wear', SchoolOutfitLoop
			!over ride the uniform with the most recent one used if there was one
			if tempSchtype <> 0:	
				clothingwornnumber = tempSchtype
				$clothingworntype = $tempSchbrand
				schtype = clothingwornnumber
				$schbrand = $clothingworntype
			end
			jump 'FoundSchoolOutfit'
		end 
		SchoolOutfitLoop += 1
		if SchoolOutfitLoop < ARRSIZE('defclothingnumber'):jump 'FindSchoolOutfit'
		:FoundSchoolOutfit
		killvar 'SchoolOutfitLoop'
		killvar 'tempSchtype'
		killvar '$tempSchbrand'
	end

	! if no uniform found or the one selected is worn out, use the default one
	if schtype = 0 or dyneval('RESULT = <<$schbrand>>_h[<<schtype>>]') <= 0:
		schtype = 6
		$schbrand = 'gm_school'
	end
	gs 'clothing', 'wear', $schbrand, schtype
end

--- wardrobe ---------------------------------

