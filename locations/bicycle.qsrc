# bicycle

! BikeLoc - tells us where bicycle is currently located
!----------------------------------------------
! BikeLoc = 1 'gadsarai' ''
! BikeLoc = 2 'gadforest' 'forest_edge'
! BikeLoc = 3 'swamp_yard' 'start'
!----------------------------------------------

! this gs handles biking for Sveta in the various locations of gadukino
! will only display bicycle actions if the BikeLoc is correct for the location passed in
! $ARGS[0] - location where this is called from
! Example: gs 'bicycle', 'gadsarai'
if ($ARGS[0] = 'gadsarai' and BikeLoc = 1) or ($ARGS[0] = 'gadforest' and BikeLoc = 2) or ($ARGS[0] = 'swamp_yard' and BikeLoc = 3):
	act 'Ride your bicycle':
		*clr & cla
		menu_off = 1
		
		if $clothingworntype = 'nude':
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel_nude.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/findvel.jpg"></center>'
		end
		*nl
		'You see your old bike. It looks like it''s still in a good condition, you can probably ride it without any trouble.'
		
		if BikeLoc = 1:
			act 'Leave your bike alone':gt 'gadsarai'
		elseif BikeLoc = 2:
			act 'Leave your bike alone':gt 'gadforest','forest_edge'
		elseif BikeLoc = 3:
			act 'Leave your bike alone':gt 'swamp_yard','start'
		end
		if BikeLoc = 1 and hour >= 6 and hour < 20 and sunWeather = 1 and month >= 4 and month <= 10 and gadbikeday ! daystart:
			act 'Bike around the village (1:00)':
				*clr & cla
				menu_off = 1
				minut += 60
				run_exp += rand(0,2)
				stren_exp += rand(0,2)
				fat -= 2
				pcs_mood += 10
				pcs_sweat += 2
				gadbikeday = daystart
				
				if $clothingworntype = 'nude':
					pcs_horny += 15
					pcs_mood += 15
					if pcs_inhib < 50: pcs_inhib += rand(0,2)
					gs 'exhibitionism', 3
					gs 'stat'
					
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_nude.jpg"></center>'
					*nl
					'You ride nude, avoiding crowded places, exulting from this newfound courage. It''s hard to say what excites you more, fear or desire to be noticed.'
				elseif PCloSkirt > 0:
					bikerand = rand(0,10)
					if bikerand >= 8:
						if $pantyworntype ! 'none':
							pcs_horny += 5
							pcs_mood += 5
							if pcs_inhib < 20: pcs_inhib += rand(0,2)
							gs 'exhibitionism', 1
							gs'stat'
							
							'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_tanga_1.jpg"></center>'
							*nl
							'During your ride you lift your skirt several times, showing a glimpse of your panties to a few lucky villagers passing-by.'
						else
							pcs_horny += 10
							pcs_mood += 10
							if pcs_inhib < 30: pcs_inhib += rand(0,2)
							gs 'exhibitionism', 2
							gs'stat'
							
							'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_tanga_2.jpg"></center>'
							*nl
							'During your ride you lift your skirt several times, showing your bare ass to a few lucky villagers passing-by.'
						end
					else
						pcs_mood += 5
						gs 'stat'
						
						'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_2.jpg"></center>'
						*nl
						'You enjoy the breath of fresh air from riding your bike by the seemingly endless fields of the village.'
					end					
				elseif PCloSkirt = 0:
					pcs_mood += 5
					gs 'stat'
					
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike.jpg"></center>'
					*nl
					'You ride your bike, enjoying the surrounding country-side.'
				end
				act 'Finish your ride':gt 'gadsarai'
			end
		end
		
		if BikeLoc ! 1:
			if BikeLoc = 2:
				$bikeaction = 'Bike to your grandparents barn (0:12)'
			elseif BikeLoc = 3:
				$bikeaction = 'Bike to your grandparents barn (0:44)'
			end
			act '<<$bikeaction>>':
				*clr & cla
				menu_off = 1
				if BikeLoc = 2:
					minut += 12
					run_exp += rand(0,1)
					stren_exp += rand(0,1)
					fat -= 1
					pcs_mood += 5
					pcs_sweat += 1
					if swamp_clothes = 1:
						'You remove the terrible excuse for clothing the hunters gave you. You''re sure you can find something better to wear at your grandparents house.'
						gs 'clothing', 'strip', 'gadforest'
						gs 'clothing', 'swamp_clothes', 0
					end
				elseif BikeLoc = 3:
					minut += 44
					run_exp += rand(0,3)
					stren_exp += rand(0,3)
					fat -= 3
					pcs_mood += 15
					pcs_sweat += 3
					if swamp_clothes = 1:
						'You remove the terrible excuse for clothing the hunters gave you. You''re sure you can find something better to wear at your grandparents house.'
						gs 'clothing', 'strip', 'gadforest'
						gs 'clothing', 'swamp_clothes', 0
					end
				end
				BikeLoc = 1
				gs'stat'

				gs 'bicycle', 'clothes'
				*nl
				'You jump onto your bike and ride to your grandparent barn.'
				
				act 'Finish your ride':gt 'gadsarai'
			end
		end
		
		if BikeLoc ! 2 and pcs_bushcraft > 0:
			if BikeLoc = 1:
				$bikeaction = 'Bike to the forest edge (0:12)'
			elseif BikeLoc = 3:
				$bikeaction = 'Bike to the forest edge (0:32)'
			end
			act '<<$bikeaction>>':
				*clr & cla
				menu_off = 1
				if BikeLoc = 1:
					minut += 12
					run_exp += rand(0,1)
					stren_exp += rand(0,1)
					fat -= 1
					pcs_mood += 5
					pcs_sweat += 1
				elseif BikeLoc = 3:
					minut += 32
					run_exp += rand(0,2)
					stren_exp += rand(0,2)
					fat -= 2
					pcs_mood += 0
					pcs_sweat += 2
				end
				BikeLoc = 2
				gs'stat'

				gs 'bicycle', 'clothes'
				*nl
				'You jump onto your bike and ride to the edge of the forest.'
				
				act 'Finish your ride':gt 'gadforest','forest_edge'
			end
		end
		
		if BikeLoc ! 3 and hunterscabin = 1 and huntersKnow = 1:
			if BikeLoc = 1:
				$bikeaction = 'Bike to the hunters cabin (0:44)'
			elseif BikeLoc = 2:
				$bikeaction = 'Bike to the hunters cabin (0:32)'
			end
			act '<<$bikeaction>>':
				*clr & cla
				menu_off = 1
				if BikeLoc = 1:
					minut += 44
					run_exp += rand(0,3)
					stren_exp += rand(0,3)
					fat -= 3
					pcs_mood += 15
					pcs_sweat += 3
				elseif BikeLoc = 2:
					minut += 32
					run_exp += rand(0,2)
					stren_exp += rand(0,2)
					fat -= 2
					pcs_mood += 10
					pcs_sweat += 2
				end
				BikeLoc = 3
				gs'stat'

				gs 'bicycle', 'clothes'
				*nl
				'You jump onto your bike and ride to the hunters cabin.'

				act 'Finish your ride':gt 'swamp_yard','start'
			end
		end
	end
end

! this gs handles images for biking for Sveta in the various locations of gadukino
! Example: gs 'bicycle', 'clothes'
if $ARGS[0] = 'clothes':
	!need images for biking during snow/rain/winter/nighttime: currently only have summer daytime images
	!if sunWeather = 0 and (month < 4 or month > 10): snowing
	!else sunWeather = 0 and month >= 4 and month <= 10: raining
	!elseif month < 4 or month > 10 and hour < 6 or hour >= 22: winter nighttime
	!elseif month < 4 or month > 10: winter daytme
	!elseif hour < 6 or hour >= 22: summer nighttime
	!else summer daytime
		if $clothingworntype = 'nude':
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_nude.jpg"></center>'
		elseif PCloSkirt > 0:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike_2.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/bike.jpg"></center>'
		end
	!end
end

--- bicycle ---------------------------------

