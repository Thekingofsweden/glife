# obroom

if $ARGS[0] = 'start':
	$sexloc = 'obroom'
	CLOSE ALL
	$metka = 'start'
	$loc = 'obroom'
	$metkaM = 'start'
	$locM = 'obroom'
    $location_type = 'private'
	$menu_loc = 'obroom'
	$menu_arg = 'start'
	menu_off = 0
	cla
	*clr
	odkomp = 1
	gs 'stat'
	'<center><b>Your room</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/obroom.jpg"></center>'
	'This is your room which you share with another girl named Vika Zhurova.'
	'At the right of the entrance you find an old <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> (where you can choose outfits and organize your clothing).'
	'There''s a <a href="exec:gt ''mirror'',''start''">mirror</a> hanging on the wall.'
	'Your <a href="exec:gt ''bed'',''start''">bed</a> is positioned in one of the corners of the room.'
	if komp = 1:'There''s a <a href="exec:gt ''Komp'',''start''">laptop</a> laying on your bed.'
	*nl
	'You can access your <a href="exec: quick_dress = 1 & gt ''wardrobe'', ''default''">default clothing</a> options directly.'

	gs 'exercise', 'start'
	''
	if hour > 14 and hour < 20 and npc_rel['A220']>=60 and birthday = day and birthmonth = month and vikaslut = 1 and bordel = 1 and slutty > 0 and b_vika_present_Day!day:
		vika_sleep = 0
		'<a href="exec:gt''Vika''">Vika</a> seems excited when she notices you, trying to hide a box behind her back.'
	elseif hour > 14 and hour < 20:
		vika_sleep = 0
		'<a href="exec:gt''Vika''">Vika</a> is laying on her bed.'
	end
	if hour > 3 and hour < 8 and vikaslut = 0:
		vika_sleep = 1
		'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after being out the whole night.'
	end
	if hour > 3 and hour < 8 and vikaslut = 1:
		vika_sleep = 1
		'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after working hard in the brothel all night.'
	end

	act 'Walk out in the corridor':
		if $clothingworntype ! 'nude':
			odkomp = 0
			minut += 5
			gt 'dorm', 'korr'
		else
			'You need to get dressed.'
		end
	end

	act 'Read the course literature (2:00)':
		cla
		menu_off = 1
		minut += 120
		intel_exp += 1 + (mentats_dose - rand(0,mentats_dose))
		gs 'stat'
		'You spend two hours reading the course literature. You feel like you''ve picked up some stuff from the book that will come in handy in time for the examination.'

		act 'Finish':gt 'obroom', 'start'
	end

	if student > 0 and kursovik < 100:
		act 'Do some coursework (1:00)':
			cla
			*clr
			menu_off = 1
			minut += 60
			pcs_mood -= 10
			kursovik += rand(pcs_intel / 20, pcs_intel / 10)

			if kursovik > 100:kursovik = 100
			gs 'stat'

			'<center><img <<$set_imgh>> src="images/pc/activities/study.jpg"></center>'
			'You sit for an hour and prepare your work for the class. You''ve finish <<kursovik>> percent of the work.'

			act 'Finish':gt 'obroom', 'start'
		end
	end

dynamic $wearpan
dynamic $removepan
end

--- obroom ---------------------------------

