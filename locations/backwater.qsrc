# backwater

$location_type = 'secluded'
$loc = 'backwater'
$loc_arg = ''
$menu_loc = 'backwater'
$menu_arg = ''
menu_off = 0
gs 'gadukino_event', 'sound'
clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)

if $ARGS[0] = '':
	if huntersRape = 2:
		clr & cla
		'<center><img <<$set_imgh>> src="images/locations/gadukino/sex/hunter/backwaterrelax.jpg"></center>'
		*nl 'You sit down by the shore, resting.'
		menu_off = 1
		minut += 5
		huntersRape = 3
		gs 'stat'

		act 'Further':gt 'hunters', 'start'
	else
		if $clothingworntype = 'nude' and igor_away = 'no' or (hunters_outside = 1 and hunters_available = 1) and rand(1,10) = 1: gt 'backwater','hunter' else gt 'backwater','alone'
	end
end

if $ARGS[0] = 'alone':
	'<center><B>Backwater</B></center>'
	if month >= 4 and month <= 10:
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/'+iif(DayStage < 4,'backwater.jpg','backwater_night.jpg')+'"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/'+iif(DayStage < 4,'backwater_winter.jpg','backwater_winter_night.jpg')+'"></center>'
	end
	*nl
	'A small creek with relatively clean water. If the weather is warm you can clean yourself up. Also, if your clothes are dirty you can wash them here.'
	*nl

	if clothesAtLocation = 1:'Your clothes lie on the shore.'


	act 'Go back to the hut':
		minut += 5
		hunter_refuse = 0
		gt 'swamp_yard', 'start'
	end


	if $clothingworntype = 'nude':
		act 'Wash self':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/'+iif($pantyworntype = 'none','backwaterswim2.jpg','backwaterswim1.jpg')+'"></center>'
			*nl 'You washed yourself in the creek.'
			menu_off = 1
			noShampoo = 1
			dynamic $showerdin
			face_write = 0
			body_write = 0
			minut += 30
			pcs_mood += 10
			if willday_bath ! daystart: willday_bath = daystart & pcs_willpwr += 1
			inhib_exp += rand(1,3)
			if huntersRape = 1: huntersRape = 2
			if deodorant_on = 1: gs 'sweat', 'remove_deo' & '<br>Your deodorant gets washed away in the water.'
			act 'Finish': gt 'backwater'
		end
	end
	if cumloc[11] = 1 or cumloc[12] = 1:
		act 'Clean the sperm from your face (0:10)':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim1.jpg"></center>'
			*nl 'You carefully wash off the sperm in the creek.'
			menu_off = 1
			pcs_makeup = 1
			cumspclnt = 9
			gs 'cum_cleanup'
			cumspclnt = 13
			gs 'cum_cleanup'
			if pcs_sweat > 19 : pcs_sweat -= 5
			minut += 10
			if huntersRape = 1: huntersRape = 2
			gs 'stat'

			act 'Finish': gt 'backwater'
		end
	end
	if body_write > 0 or face_write > 0:
		act 'Wash the writings off your body (0:10)':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/wash1.jpg"></center>'
			*nl 'You wash off the indecent inscriptions on your body.'
			menu_off = 1
			body_write = 0
			face_write = 0
			minut += 10
			gs'stat'

			act 'Finish': gt 'backwater'
		end
	end
	if cumloc[6] = 1 or cumloc[7] = 1:
		act 'Wipe the cum off your clothes (0:10)':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/cumwash.jpg"></center>'
			*nl 'You clean the cum stains off your clothes.'
			menu_off = 1
			cumspclnt = 6
			gs 'cum_cleanup'
			minut += 10
			if huntersRape = 1: huntersRape = 2
			gs'stat'

			act 'Finish':gt 'backwater'
		end
	end
	if spdirty > 0:
		act 'Quick wash (0:15)':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/facesp.jpg"></center>'
			*nl 'You carefully wash your face, removing any impurities.'
			menu_off = 1
			cumspclnt = 4
			gs 'cum_cleanup'
			if pcs_sweat < 30: pcs_sweat = 15 + rand(0,4) else pcs_sweat -= 10 + rand(0,4)
			minut += 15
			if huntersRape = 1: huntersRape = 2
			gs'stat'
			act 'Finish':gt 'backwater'
		end
	end

	if clothesAtLocation = 0:
		if $clothingworntype ! 'nude' and temper >= 15:
			act'Get undressed':
				*clr & cla
				menu_off = 1
				if huntersKnowSlut > 0 or (hunters_outside = 0 and hunters_available = 0) or (hour < 7 or hour > 22) or (pcs_inhib >= 25 or exhibitionist_lvl > 1):
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/'+iif(PCloSkirt > 0,'backwaterundress_ski.jpg','backwaterundress.jpg')+'"></center>'
					'You do not hesitate getting undressed, not caring if anyone is watching.'
					minut+=5
					if huntersRape = 1: huntersRape = 2
					gs 'clothing', 'strip_all', 'backwater'

					act'Back': gt 'backwater'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/noundress.jpg"></center>'
					'You hesitate getting undressed, what if someone suddenly sees you?'

					act'Back': gt 'backwater'
				end
			end
		else
		'It''s too cold to undress. Maybe you should bathe inside the hut'
		end
	else
		if $clothingworntype ! 'nude':
			act 'Pick up your clothes':
				gs 'clothing', 'recover_lost_clothes', 'backwater'
				!! 'You pick up your clothes laying on the ground.'
				gt 'backwater'
			end
		elseif forced_naked_day < daystart:
			act 'Get dressed':
				gs 'clothing', 'recover_lost_clothes', 'backwater', 1
				gs 'underwear', 'wear'
				!! 'You pick up your clothes laying on the ground and get dressed.'
				gt 'backwater'
			end
		end
	end

end

if $ARGS[0] = 'hunter':
	'<center><B>Backwater</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hunter_looks.jpg"></center>'
	'While cleaning yourself in the backwater, you suddenly get the feeling that you are not alone here. Turning around, you see one of the hunters. He''s just standing there watching you, his camouflage is almost indistinguishable compared to the bushes and trees.'
	menu_off = 1
	minut += 5
	gs 'stat'

	if clothesAtLocation = 1:
		act'Get dressed':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude1.jpg"></center>'
			*nl 'Noticing the hunter, you rush over to your clothes and begin dressing yourself in panic. As you get fully dressed, you look over your shoulder, expecting the hunter standing there, but there is no one there.'
			gs 'underwear', 'wear'
			gs 'clothing', 'recover_lost_clothes', 'backwater', 1
			minut += 5

			act'Further':gt 'backwater'
		end
	else
		act'Hide':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude2.jpg"></center>'
			'Covering your breasts and crotch you jump into the nearest bush. In your state of fear and confusion, you start scanning the area but you don''t see anyone there. You quickly run back towards the cabin.'
			minut += 1

			act'Go back to the hut':gt 'swamp_yard', 'start'
		end
	end
	act'Stay naked':
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude3.jpg"></center>'
		'You decide that you have nothing to be afraid of, and continue on completely nude. The hunter looks at you for a minute or so, all of a sudden you hear a "Hmph", him turning around and going away.'
		hunterslut += 1
		hunterknowday = day
		gs'stat'

		act'Further':gt 'backwater'
	end
end

--- backwater ---------------------------------

