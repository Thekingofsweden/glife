# mother
!!Model Olga Kabaeva
motherAge = age + 19

if momslut >= 2 and mother < 40:mother = 40
gs 'stat'
gs'motherQW'
gs'dinmother'

'<center><b><font color="maroon">Mama</font></b></center>'
'<center><img src="images/qwest/alter/mother.jpg"></center>'
'Your mother Natasha is still quite young and attractive. She''s only <<motherAge>> years. She works at the station cafe.'

if (motherKnowWhore > 0 or motherKnowSpravka > 0) and motherQW = 0:motherQW = 1
if motherQW = 1 and mother > 40:mother = 40
if mother < 20:'Your relationship with your mom is terrible.'
if mother >= 20 and mother < 40:'Your relationship with your mom is tense.'
if mother >= 40 and mother < 60:'You have a normal relationship with your mother.'
if mother >= 60 and mother < 80:'You have a good relationship with your mother.'
if mother >= 80:'You have a great relationship with your mother.'

act 'Move away':gt $loc, $metka

if motherWorry = 1 and indorf = 0:
	motherWorry = 0
	mother -= 1
	'Mama is awake and stands in the hallway waiting for you. "Where the hell have you been? You know what time it is?"'
elseif motherWorry = 1 and indorf > 0:
	motherWorry = 0
end


if $loc = 'gkafe':
	cla
	'Mom saw you and frowned: "Why come? I said many times, Do not go to my job. There`s nothing to do young girls!"'
	act '<B>Move away</B>':gt 'gkafe'
exit
end

!check hair color
if hcolfronce=0:hcolmotherremember=hcol&hcolfronce=1
if hcolmotherremember!hcol:
if hcol=0:$texthcol='I decided to become a burning brunette?'
if hcol=1:$texthcol='I decided to get my natural hair color?'
if hcol=2:$texthcol='I decided to become a redhead-shameless?'
if hcol=3:$texthcol='I decided to become a blonde?'
'Mother with interesoma looked at your hair: "Ooh-you, Repainted! <<$texthcol>>"'
hcolmotherremember=hcol
end

if $clothingworntype ! 'nude' and pregTalkFamily = 0 and pregTalkMom = 0 and pregtime > 150:
	cla
	'Mother is looking at your clothes clearly acting under the belly-"<<$name>>, I flew all-yet, Mount thou my. Look, do not try to do an abortion!".'
	pregTalkFamily = 1
	pregTalkMom = 1
	act '<B>Move away</B>':gt $locM, $metkaM
	exit
end

if $clothingworntype = 'nude' and pregTalkFamily = 0 and pregTalkMom = 0 and pregtime > 90:
	cla
	'Mother is looking at your belly-"<<$name>>, I flew all-yet, Mount thou my. Look, do not try to do an abortion!".'
	pregTalkFamily = 1
	pregTalkMom = 1
	act '<B>Move away</B>':gt $locM, $metkaM
	exit
end

$mother_clothes[1] = {
	if motherKnowWhore = 0 and motherKnowSpravka = 0 and motherQW = 0:
		if momslut < 2:
			'The mother looks at you eyes wide:'
			'- "<<$name>>, Here is the number of, Are not you ashamed to go in such a? Well dressed jogging!"'
		else
			'His mother shook her head sadly:'
			'- "You would have your ass-is closed, and then another , and everyone will know the house, you whore"'
		end
	elseif (motherKnowWhore > 0 or motherKnowSpravka > 0) and motherQW = 1:
		if momslut < 2:
			'The mother looked at you glare:'
			'- "Even at home, his bare ass shine, His infant whore! I went I dressed quickly and do not get caught in my eyes!"'
		else
			'His mother shook her head sadly:'
			'- "You would have your ass-is closed, and then another , and everyone will know the house, you whore"'
		end
	elseif motherQW >= 2:
		'Mother looked sternly at you:'
		'- "<<$name>>, I certainly understand it all, but could you at least not at home dress, like you used to have their fancy man pacing? You have the floor asses out-towels for video!"'
	end
}

$mother_clothes[2] = {
	if motherKnowWhore = 0 and motherKnowSpravka = 0 and motherQW = 0:
		if momslut < 2:
			'Your mother looks at you, her eyes wide.'
			'"<<$name>>, you''re not a kid anymore, you don''t walk around my home naked. Now go and get dressed!"'
		else
			'Your mother shakes her head sadly.'
			'"You could atleast dress at home, do you want all the family to know that you''re a whore?"'
		end
	elseif (motherKnowWhore > 0 or motherKnowSpravka > 0) and motherQW = 1:
		if momslut < 2:
			'Your mother glares at you.'
			'- "Even his home by a pussy all over the world, His infant whore! I went I dressed quickly and do not get caught in my eyes!"'
		else
			'Your mother shakes her head sadly.'
			'"You could atleast dress at home, do you want all the family to know that you''re a whore?"'
		end
	elseif motherQW >= 2:
		'Your mother looks at you sternly.'
		'"<<$name>>, I certainly understand, but could you at least dress at home? If you want to impress some fancy man, do it at his place!"'
	end
}

if $clothingworntype = 'nude' and towel = 1 and tanga = 0:
	mother -= 2
	''
	dynamic $mother_clothes[1]
elseif $clothingworntype = 'nude':
	mother -= 2
	''
	dynamic $mother_clothes[2]
end

if pirsA >= 1 and pirsAmother=0 and motherpirsadaytalk!daystart:
!Mother tongue piercing notes
pirseerand=rand(1,100)
motherpirsadaytalk=daystart
	if pirseerand>=75:
	'Mother looks you in the mouth: "<<$name>>, and what have you got there in the language shines?"'
		act'Nothing':
			cla
			'You shake my head: "Nothing." and then turn away ,that the mother had not noticed tongue piercing.'
			act '<B>Move away</B>':gt $locM, $metkaM
		end
		
		act'Show pierced tongue':
			cls
			pirsAmother=1
			gs'stat'
			'<center><img src="images/picb/pirsA.jpg" ></center>'
			'You open your mouth and draw out the tongue showing the mother tongue piercing.'
			'Mother shakes her head "<<$name>>, tongue piercing made lewd women, serving men mouth."'
			act '<B>Move away</B>':gt $locM, $metkaM
		end
	exit
	end
end

if pirsB = 1 and pirsBmother=0 and motherpirsbdaytalk!daystart:
!Mother says lip piercing
motherpirsadbytalk=daystart
'Mother looks at your lips that appeared piercing "And this is why?"'
	act'I just liked':
		cls
		pirsBmother=1
		gs'stat'
		'<center><img src="images/picb/pirsB.jpg" ></center>'
		'You shrug your shoulders and say,, just you like, So you decided to make a lip piercing.'
		act '<B>Move away</B>':gt $locM, $metkaM
	end
exit
end

if pirsC = 1 and pirsCmother=0:
!mother notices earrings
pirsCmother=1
'Mother is looking at your ears noticing earrings: "I look at you decided to wear earrings? Very cute, suits you."'
end

if pirsD = 1 and pirsDmother=0:
!mother notices a nose ring
pirsDmother=1
'Mother looks a bit shocked at your nose ring: "Jesus, <<$name>>, and this is why else? I understand, bulls ring is inserted into the nose, that would be for them to carry the ring, but you-why?"'
end

if pirsE = 1 and pirsEmother=0:
!mother notices eyebrow piercing
pirsEmother=1
'Mother shakes her head looking at your eyebrow piercing: "<<$name>>, Well, what do you need that stuff in eyebrow?", - then sadly shakes his head - "Oh youth, youth..."'
end

if mother > 65 and mesec > 0 and preg = 0 and tampon = 0:
	act 'Ask for a tampon':
		'You tell your mother about, what you are menstruating and ended tampons. She looks at you knowingly, brings some of his and recommends you next time take care of this in advance.'
		tampon=3
		mother -= 15
		minut += 3
		cla 
		act '<B>Move away</B>':gt $locM, $metkaM
		exit
	end
end

act 'Chat':
	cls
	minut+=rand(10,15)
	mother+=1
	gs'stat'
	'<center><b><font color = maroon>Mom</font></b></center>'
	'<center><img src="images/qwest/alter/mother.jpg"></center>'
	'Your mother Natasha is still quite young and attractive. She is only <<motherAge>> years. She works in the station cafe.'
	if mother<20:'You have a terrible row with my mother.'
	if mother>=20 and mother<40:'You and mom tensions.'
	if mother>=40 and mother<60:'You have a normal relationship with his mother.'
	if mother>=60 and mother<80:'You have a good relationship with his mother.'
	if mother>=80:'You have a great relationship with my mother.'
	act '<B>Move away</B>':gt $locM, $metkaM
	if mother<20:
	dynamic $d_mother[1]
	exit
	elseif mother>=20 and mother<40:
	dynamic $d_mother[2]
	exit
	elseif mother >=40:
		dynamic $d_mother[3]
	end
end

if SchoolAtestat = 1 and SAtestatGift = 0:
	SAtestatGift = 1
	money += 5000
	gs 'stat'
	'Mama was delighted, "Now that you graduated from high school I want to give you five thousand rubles. Go buy yourself a gift."'
end

if hour > 5:
	if abort = 1 and pregTalkFamily = 1:
		pregTalkFamily = 2
		mother = 0
		'Mother looked sadly at you "You are a fool! You already had an abortion, what if now you can not have children? Do you think you will be forever young and your whole life will be happy?"'
	end

	if momslut = 1:
		act 'Say, you saw your mother fucking her boss in the cafe':
			cls
			minut += 5
			momslut = 2
			gs 'stat'
			'<center><b><font color="maroon">Mama</font></b></center>'
			'<center><img src="images/qwest/alter/mother.jpg"></center>'
			'You said to your mother, you''ve seen her in the back room of the cafe with her boss. Your mother got noticeably pale and sat down on a stool. "And what did you see?" You answered, you saw as she sucked her boss, and then he fucked her doggy style. Your mother lowered her eyes "Well yes, I sleep with Slava. But I want to warn you, don''t try to blackmail me." The mother looked into your eyes "All you can do, is to destroy our family."'

			act 'Leave':gt $locM, $metkaM
		end
	end

	if preg = 1 and pregTalkFamily = 0:
		act 'Tell your mother that you are pregnant':
			cls
			minut += 5
			mother = 20
			pregTalkFamily = 1
			gs 'stat'
			'<center><b><font color="maroon">Mama</font></b></center>'
			'<center><img src="images/qwest/alter/mother.jpg"></center>'
			'You told your mother  you are pregnant. Your mother, shocked, sat on a stool. She said sadly "Well. I was afraid of this. Now do not try to have an abortion, if you do, it will be difficult to have children in the future. You''ll have to have the child."'

			act 'Leave':gt $locM, $metkaM
		end
	end

	act 'Asking for money':
		cls
		minut += 5
		gs 'stat'
		'<center><b><font color="maroon">Mama</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'You asked your mother for money.'

		if SchoolAtestat = 0:
			if mother >= 80:
				if motherPayDay = daystart:
					'Mother frowned "Daughter, I already gave you money!"'

				else
					mother -= 1
					motherPayDay = daystart

					if Suspeh >= 70:
						if Suspeh < 90:
							money += 150
							'Frowning mother gave you 150 rubles, "When you do better in school, I will give more. Meanwhile, it''s all that you deserve!"'

							act 'Move away':gt $locM, $metkaM

							exit
						else
							money += 300
							'Your mother smiled and gave you 300 rubles, "Okay, here is your money."'

							act 'Move away':gt $locM, $metkaM

							exit
						end
					else
						'Mother frowned "When you do better in school, there will be money, as long as you only deserve the belt, you get nothing!"'
					end
				end
			elseif mother >= 60 and mother < 80:
				if motherPayDay = daystart:
					'Mother frowned "No daughter. I already gave you money. Enough for today."'
				else
					mother -= 1
					motherPayDay = daystart

					if Suspeh >= 70:
						if Suspeh < 90:
							money += 100
							'Frowning mother gave you $ 100, "When you are a better student, I will give more. Meanwhile, it''s all that you deserve!"'

							act 'Move away':gt $locM, $metkaM

							exit
						else
							money += 250
							'The mother smiled and gave you 250 rubles, "Okay, have some money."'

							act 'Move away':gt $locM, $metkaM

							exit
						end
					else
						'Mother frowned "When you are a better student, I will give you money, as long as you deserve only the belt, you get nothing!"'
					end
				end
			elseif mother >= 40 and mother < 60:
				if motherPayDay = daystart:
					'Mother frowned "Stop pestering me. You will get nothing!"'
				elseif motherPayDay ! daystart:
					mother -= 1
					motherPayDay = daystart

					if Suspeh >= 70:
						if Suspeh < 90:
							money += 50
							'Frowning mother gave you 50 rubles, "Do better in school and I will give more. Meanwhile, it''s all that you deserve!"'

							act 'Move away':gt $locM, $metkaM

							exit
						else
							money += 200
							'Your mother smiled and gave you 200 rubles, "Okay, take some money."'

							act 'Move away':gt $locM, $metkaM

							exit
						end
					else
						'Mother frowned "Until you get better in school, there''s no money for you!"'
					end
				end
			elseif mother < 40 and motherKnowWhore = 0 and motherKnowSpravka = 0:
				'Mother frowned, "You do not you deserve any money!"'
			elseif (motherKnowWhore >= 1 or motherKnowSpravka >= 1) and mother < 40:
				'"You ask for more money, whore? Get out of here, run to your stud and beg him for money"'
			end
		elseif SchoolAtestat > 0:
			'You''re old enough, earn your own money.'
		end

		act 'Move away':gt $locM, $metkaM
	end

	act 'Ask about this father':
		cls
		minut += 5
		gs 'stat'
		'<center><b><font color="maroon">Mama</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'

		if mother < 80:
			'You asked your mother about your real father. She pulled a face and unhappily said. "<<$name>>, I am not going to discuss this topic now. Just know that Vova, your stepfather raised you and Anya as his own daughters."'
		elseif mother >= 80:
			'You asked your mother about your real father. She pulled a face and unhappily said. "I have no idea where he is and what he does. We were at school together. We had your sister, Anka. Our parents made us marry. Michael, your father, and you made me thumped. We divorced and I married Vovk. Vova already had Kolka. But he brought you and Anya up as his own daughters. He looked after you, and bathed you, and took you for walks. And told you tales at night. "'
		end

		act 'Move away':gt $locM, $metkaM
	end

	act 'Ask for new clothes':
		cls
		minut += 5
		gs 'stat'
		'<center><b><font color="maroon">Mama</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'
		'You asked for new clothes.'
		if gorslut = 6:
			'Mother outraged: "<<$name>>, you have a lot of customers and you`re still asking me clothes? It`s me you should ask. Buy myself."'
			act '<B>Move away</B>':gt $locM, $metkaM
			exit
		end

		if SchoolAtestat = 0:
			if mother >= 80:
				if motherClotheMonth ! month:
					'Mom sighed, "Here we go again. You''ve got nothing to wear. Let''s go to the store, grief onion."'

					act 'Go to the store':
						cls
						mother = 70
						motherClotheMonth = month
						minut += 30
						gs 'stat'
						'You went to the store with your mother and began to try out clothes. Shopping with your mom is quite interesting, she never lets you choose your own but buys clothes that would suit you.'

						act 'choose clothes':
							cls
							gs 'stat'
							:markbuyclomother
							numrand = rand(1, 130)

							if newclo[numrand] = 0:
								newclo[numrand] = 1
								newcloH[numrand] = 50
								newcloB[numrand] = bedra
								'<center><img src="images/clothes/newclo/<<numrand>>.jpg"></center>'
							else
								jump 'markbuyclomother'
							end

							'Mom bought you clothes.'

							act 'Leave':gt $locM, $metkaM
						end
					end

					exit
				else
					'Your mother shook her head "My daughter, our budget is limited, this month we already bought you clothes. Now wait for the next month."'
				end
			else
				'Mom shook her head "no money."'
			end
		else
			'Mom shook her head, "You''re an adult, you should earn your own money."'
		end

		act 'Move away':gt $locM, $metkaM
	end
end

if $loc = 'kuhrPar':
	act 'Offer help':
		cls
		minut += 60
		mother += rand(1, 5)
		manna -= 10
		TotalMomPom += 1
		MomPom += 1
		gs 'stat'
		'You offered to help your mother with the housework.'

		if MomPom = 1:
			'Mom asked you to wash the dishes. You spend an hour washing the dishes in the kitchen.'
		elseif MomPom = 2:
			'Mom asked you to peel the potatoes. You spend an hour in the kitchen peeling potatoes.'
		elseif MomPom = 3:
			'Mom asked you to clean the floor. You spend an hour washing the floors in the apartment.'
		elseif MomPom = 4:
			'Mom asked you to do your laundry. You spend an hour washing clothes in the bathroom.'
		elseif MomPom = 5:
			'Mom asked you to iron the clothes. You spend an hour in the hall ironing clothes.'
		elseif MomPom = 6:
			sdacharand = rand(10, 50)
			money += sdacharand
			gs 'stat'
			'Mom asked you to run to the store for groceries. You walked for an hour for food, you spent <<sdacharand>>rubles.'
		elseif MomPom = 7:
			'Mom asked you to run to the pharmacy for medicines. Mom''s sister, Lyudmila, works there and she will give medicine for free, then she finds mom and aunt Luda. Mom wrote you a list and you went to the pharmacy, behind the counter as always sat aunt Luda. "Hi <<$nickname>>. How are you? How are you feeling?" You greeted your aunt and gave her a piece of paper with the list of what your mother needs. Aunt Luda quickly gathered the necessary drugs and gave them to you. You came back to the apartment and gave your mother the medication.'
		elseif MomPom = 8:
			MomPom = 1
		end

		act 'Move away':gt $locM, $metkaM
	end
end

if SchoolAtestat = 0:
	if GspravkaT = 0 and GspravkaTimes >= 3 and hour >= 6 and hour <= 21:
		cla
		mother -= 1
		'Mother frowned "How long can you avoid getting tested? Let''s go to the gynecologist right now!" Mother took you by the hand and led you to the clinic.'

		act 'Go to the gynecologist for help':
			cla
			minut += 15
			'You lay on the examination table and spread your legs. The gynecologist examined your vagina.'

			if vagina > 0:
				Gspravka = 7
				GspravkaT = 2
				'The gynecologist wrote that you are not a virgin.'
			else
				Gspravka = 7
				GspravkaT = 1
				'The gynecologist wrote that you are a virgin.'
			end

			act 'Leave':gt $locM, $metkaM
		end

		exit
	elseif GspravkaT = 0 and GspravkaTimes < 3:
		GspravkaTimes += 1
		'"You need to go to the clinic. The gynecologist must give you a referal for school."'
	elseif GspravkaT = 2 and motherKnowSpravka = 0:
		cla

		if motherKnowWhore = 0:
			mother = 0
			motherKnowSpravka = 1

			if motherQW = 0:motherQW = 1

			if momslut < 2:
				'Your mother grabbed you by the ear "Oh you slut,  We worked hard with your father, to raise you as a worthwile person and find a good husband, and you''re a whore who has already put her pussy to work. Get out of my sight!"'
			else
				'Mother shook her head sadly "Well. Look no zalet."'
			end
		elseif motherKnowWhore > 0 and motherQW < 2:
			mother -= 10
			motherKnowSpravka = 1

			if motherQW = 0:motherQW = 1
			'Your mother looked at the results "It is not even surprising. Fuck with someone-in the alley, and even home all hahalskih "gifts" come. B my eyes have not seen you."'
		elseif motherQW >= 2:
			motherKnowSpravka = 2
			'"And why do you bring that to me? You can even make photocopies and hand them out at school. I doubt it will change your reputation" -  your mother said coldly.'
		end

		act 'Move away':gt $locM, $metkaM

		exit

		end
	end

if motherQW = 1 and mother >= 20 and $loc = 'kuhrPar':
	act 'Try to reconcile':
		cla
		cls
		'<center><b><font color="maroon">Mama</font></b></center>'
		'<center><img src="images/qwest/alter/mother.jpg"></center>'

		mqwtRand = rand(1, 10)

		if mqwtRand <= 2:
			mother -= rand(1, 5)
			minut += 30
			'You tried to talk with your mother and make up. Whether you are too aggressive, or she is in a bad mood, but your attempt for "reconciliation" becomes yet another scandal.'

			act 'Move away':gt $locM, $metkaM
		elseif mqwtRand > 2 and mqwtRand <= 8:
			mother += rand(0, 1)
			minut += 30
			'You tried to talk with your mother and make up. Despite that, the whole half-hour conversation consists mainly of a fairly aggressive monologue from your mother about morality, diseases and about what awaits you in the future, if you do not change your ways. Still, there is some improvement in your relationship.'

			act 'Move away':gt $locM, $metkaM
		elseif mqwtRand > 8:
			gt'motherQW','motherQW_2'
		end
	end
end

if alko > 0 and alkomday ! daystart:
	alkomday = daystart
	mother -= 1
	'Mom sniffed "<<$name>> you smell of alcohol, what is this mess? You''re still too young to drink that!"'
end

if (cumface > 0 or cumfrot > 0) and whoremday ! daystart:
	cla
	whoremday = daystart
	if motherKnowWhore = 0 and motherKnowSpravka = 0 and motherQW = 0:
		mother = 0
		if motherKnowWhore = 0:motherKnowWhore = 1

		if momslut < 2:
			'Your mother looked at you, rolled her eyes and blushed. You had noticeable cum stains.'
			'"Oh, you''re a juvenile whore, what is that to you? I tell everyone that my daughter is decent, but she fucks around. Slut! Go away, get out of my sight!"'
		else
			'Mother shook her head sadly "When you do not even wipe the sperm off you, then the whole town will know you are a  whore."'
		end

		act 'Move away':gt $locM, $metkaM
	elseif (motherKnowWhore > 0 or motherKnowSpravka > 0) and motherQW = 1:
		monther -= 20

		if motherKnowWhore = 0:motherKnowWhore = 1

		if momslut < 2:
			'Your mother glared at you. There were obvious sperm stains on you.'
			'- Again, you fucking little whore? I was already ashamed of you! Get out of here, get out of my sight!'
		else
			'Mother shook her head sadly "You do not even wipe the sperm off you. Must the whole town must know, you whore?"'
		end

		act 'Move away':gt $locM, $metkaM
	elseif motherQW >= 2:
		monther -= 10

		if motherKnowWhore = 0:motherKnowWhore = 1

		'Mother looked sternly at you. There was sperm all over you.'
		'- <<$nickname>>, I certainly understand it all, but could you at least clean up the artwork of your fancy man? Quickly to the bathroom, maybe you have not been seen yet!'

		act 'Move away':gt 'vanrPar'
	end
end

if glamur > 0 and hour > 9:'Mom looked at you, "Why are you so dressed up?"'

if week = 5 and dostupindorf = 1:
	if hour > 9:'Mom continued to go about her business and told you "<<$nickname>>, tomorrow we will go to the village, at 12 o''clock. If you want to come with us, then come to me before 12."'
elseif week = 6 and dostupindorf = 1:
	if hour > 9 and hour < 12:
		'Mom asks you "<<$nickname>>, well? Will you come with us to the village?"'

		act 'Go to the country':hour = 12 & minut = 0 & indorf = 1 & gt 'famdorfride'
	end
end


--- mother ---------------------------------

